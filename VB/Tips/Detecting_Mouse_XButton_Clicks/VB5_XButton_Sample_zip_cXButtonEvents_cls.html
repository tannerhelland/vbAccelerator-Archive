<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Tips/Detecting_Mouse_XButton_Clicks/VB5_XButton_Sample_zip_cXButtonEvents_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:22 GMT -->
<head>
<title>vbAccelerator - Contents of code file: cXButtonEvents.cls</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: cXButtonEvents.cls" /><link rel="stylesheet" href="../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../index.html">VB</a>&#160;.&#160;<a href="../index.html">Tips</a>&#160;.&#160;<a href="article.html">Detecting Mouse XButton Clicks</a>&#160;.&#160;<a href="VB5_XButton_Sample.html">VB5 XButton Sample</a>&#160;.&#160;cXButtonEvents.cls</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_XButton_Sample.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 XButton Sample</a> (18K)</p><p /><p class="nav"><a href="VB6_XButton_Sample.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 XButton Sample</a> (17K)</p><br /><br /><img src="../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:12647</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=12647&amp;type=Zip&amp;title=VB5%20XButton%20Sample%2Ezip%5FcXButtonEvents">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../res/update.png" width="8" height="8" alt="Update" />27 Jun 2003<br />First Posted</p><br /><br /><img src="../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1020">Restrict Mouse Movement to an Area of the Desktop</a></p><p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=12338">Win32 Hooks in VB - The vbAccelerator Hook Library</a></p><p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2964">Moving, Clicking and Tracking the MousePointer in Code</a></p><p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=12334">(Incomplete) Docking Forms In VB</a></p><br /><br /><img src="../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: cXButtonEvents.cls</h1><pre>VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "cXButtonEvents"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' ---------------------------------------------------------------------
' vbAccelerator Software License
' Version 1.0
' Copyright (c) 2002 vbAccelerator.com
'
' Redistribution and use in source and binary forms, with or
' without modification, are permitted provided that the following
' conditions are met:
'
' 1. Redistributions of source code must retain the above copyright
'    notice, this list of conditions and the following disclaimer.
'
' 2. Redistributions in binary form must reproduce the above copyright
'    notice, this list of conditions and the following disclaimer in
'    the documentation and/or other materials provided with the distribution.
'
' 3. The end-user documentation included with the redistribution, if any,
'    must include the following acknowledgment:
'
'  "This product includes software developed by vbAccelerator
 (http://vbaccelerator.com/)."
'
' Alternately, this acknowledgment may appear in the software itself, if
' and wherever such third-party acknowledgments normally appear.
'
' 4. The name "vbAccelerator" must not be used to endorse or promote products
'    derived from this software without prior written permission. For written
'    permission, please contact vbAccelerator through steve@vbaccelerator.com.
'
' 5. Products derived from this software may not be called "vbAccelerator",
'    nor may "vbAccelerator" appear in their name, without prior written
'    permission of vbAccelerator.
'
' THIS SOFTWARE IS PROVIDED "AS IS" AND ANY EXPRESSED OR IMPLIED WARRANTIES,
' INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY
' AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL
' VBACCELERATOR OR ITS CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
' INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
' BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF
' USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
' THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
' (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
' THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
'
' ---------------------------------------------------------------------

Private Type POINTAPI
   x As Long
   y As Long
End Type

Private Type RECT
   left As Long
   top As Long
   right As Long
   bottom As Long
End Type

Private Const WM_XBUTTONDOWN = &amp;H20B
Private Const WM_XBUTTONUP = &amp;H20C
Private Const WM_XBUTTONDBLCLK = &amp;H20D
Private Const WM_NCXBUTTONDOWN = &amp;HAB
Private Const WM_NCXBUTTONUP = &amp;HAC
Private Const WM_NCXBUTTONDBLCLK = &amp;HAD

Private Const VK_XBUTTON1 = &amp;H5&amp;          '/* NOT contiguous with L &amp; RBUTTON */
Private Const VK_XBUTTON2 = &amp;H6&amp;          '/* NOT contiguous with L &amp; RBUTTON */

Private Const NC_XBUTTON1_MASK = &amp;H10000
Private Const NC_XBUTTON2_MASK = &amp;H20000

Private Const MK_LBUTTON = &amp;H1
Private Const MK_RBUTTON = &amp;H2
Private Const MK_SHIFT = &amp;H4
Private Const MK_CONTROL = &amp;H8
Private Const MK_MBUTTON = &amp;H10
Private Const MK_XBUTTON1 = &amp;H20
Private Const MK_XBUTTON2 = &amp;H40

Private Declare Function GetCursorPos Lib "user32" (lpPoint As POINTAPI) As Long
Private Declare Function ScreenToClient Lib "user32" (ByVal hwnd As Long,
 lpPoint As POINTAPI) As Long
Private Declare Function GetAsyncKeyState Lib "user32" (ByVal vKey As Long) As
 Integer
Private Declare Function WindowFromPoint Lib "user32" (ByVal xPoint As Long,
 ByVal yPoint As Long) As Long
Private Declare Function GetParent Lib "user32" (ByVal hwnd As Long) As Long

Implements ISubclass
Implements IWindowsHook

Public Enum XMouseButtonConstants
   XBUTTON1 = MK_XBUTTON1
   XBUTTON2 = MK_XBUTTON2
End Enum

Public Event XMouseDown(Button As XMouseButtonConstants, Shift As
 ShiftConstants, x As Single, y As Single, bConsumed As Boolean)
Public Event XMouseUp(Button As XMouseButtonConstants, Shift As ShiftConstants,
 x As Single, y As Single, bConsumed As Boolean)
Public Event XBtnDblClick(Button As XMouseButtonConstants, Shift As
 ShiftConstants, x As Single, y As Single, bConsumed As Boolean)

Private m_hWnd As Long
Private m_objTo As Object
Private m_bAnyChild As Boolean
Private m_eHookButtonDown As XMouseButtonConstants


Public Sub Attach( _
      objTo As Object, _
      Optional ByVal bAnyChild As Boolean = False _
   )
Dim hWndA As Long
   Detach
   Set m_objTo = objTo
   hWndA = m_objTo.hwnd
   m_bAnyChild = bAnyChild
   If (bAnyChild) Then
      m_bAnyChild = True
      InstallHook Me, WH_MOUSE
   Else
      m_bAnyChild = False
      AttachMessage Me, hWndA, WM_XBUTTONDOWN
      AttachMessage Me, hWndA, WM_XBUTTONUP
      AttachMessage Me, hWndA, WM_XBUTTONDBLCLK
      AttachMessage Me, hWndA, WM_NCXBUTTONDOWN
      AttachMessage Me, hWndA, WM_NCXBUTTONUP
      AttachMessage Me, hWndA, WM_NCXBUTTONDBLCLK
   End If
   m_hWnd = hWndA
End Sub
Public Sub Detach()
   If (m_hWnd) Then
      If (m_bAnyChild) Then
         RemoveHook Me, WH_MOUSE
      Else
         DetachMessage Me, m_hWnd, WM_XBUTTONDOWN
         DetachMessage Me, m_hWnd, WM_XBUTTONUP
         DetachMessage Me, m_hWnd, WM_XBUTTONDBLCLK
         DetachMessage Me, m_hWnd, WM_NCXBUTTONDOWN
         DetachMessage Me, m_hWnd, WM_NCXBUTTONUP
         DetachMessage Me, m_hWnd, WM_NCXBUTTONDBLCLK
      End If
      m_hWnd = 0
   End If
   Set m_objTo = Nothing
End Sub

Private Sub Class_Terminate()
   Detach
End Sub

Private Property Let ISubclass_MsgResponse(ByVal RHS As SSubTimer.EMsgResponse)
   '
   ISubclass_MsgResponse = emrPreprocess
   '
End Property

Private Property Get ISubclass_MsgResponse() As SSubTimer.EMsgResponse
   '
   '
End Property

Private Function ISubclass_WindowProc(ByVal hwnd As Long, ByVal iMsg As Long,
 ByVal wParam As Long, ByVal lParam As Long) As Long
   '
Dim tP As POINTAPI
Dim x As Single
Dim y As Single
Dim eShift As ShiftConstants
Dim eBtn As XMouseButtonConstants
Dim bConsumed As Boolean
   
   Select Case iMsg
   Case WM_XBUTTONDOWN, WM_XBUTTONUP, WM_XBUTTONDBLCLK
      tP.x = (lParam And &amp;HFFFF)
      tP.y = (lParam And &amp;H7FFF0000) \ &amp;H10000
      If (lParam And &amp;H80000000) Then
         tP.y = tP.y Or &amp;H8000&amp;
      End If
      x = m_objTo.ScaleX(tP.x, vbTwips)
      y = m_objTo.ScaleX(tP.y, vbTwips)
      eBtn = (wParam And &amp;H60&amp;)
      eShift = (wParam And &amp;HC&amp;)
      If (iMsg = WM_XBUTTONDOWN) Then
         RaiseEvent XMouseDown(eBtn, eShift, x, y, bConsumed)
      ElseIf (iMsg = WM_XBUTTONUP) Then
         RaiseEvent XMouseUp(eBtn, eShift, x, y, bConsumed)
      Else
         RaiseEvent XBtnDblClick(eBtn, eShift, x, y, bConsumed)
      End If
      If (bConsumed) Then
         ISubclass_WindowProc = True
      End If
   
   Case WM_NCXBUTTONDOWN, WM_NCXBUTTONUP, WM_NCXBUTTONDBLCLK
      GetCursorPos tP
      ScreenToClient m_hWnd, tP
      x = m_objTo.ScaleX(tP.x, vbTwips)
      y = m_objTo.ScaleX(tP.y, vbTwips)
      If (lParam And &amp;H10000) = &amp;H10000 Then
         eBtn = XBUTTON1
      Else
         eBtn = XBUTTON2
      End If
      eShift = vbShiftMask * Abs(Not (GetAsyncKeyState(vbKeyShift) = 0)) Or _
            vbCtrlMask * Abs(Not (GetAsyncKeyState(vbKeyControl) = 0)) Or _
            vbAltMask * Abs(Not (GetAsyncKeyState(vbKeyMenu) = 0))
      If (iMsg = WM_XBUTTONDOWN) Then
         RaiseEvent XMouseDown(eBtn, eShift, x, y, bConsumed)
      ElseIf (iMsg = WM_XBUTTONUP) Then
         RaiseEvent XMouseUp(eBtn, eShift, x, y, bConsumed)
      Else
         RaiseEvent XBtnDblClick(eBtn, eShift, x, y, bConsumed)
      End If
      If (bConsumed) Then
         ISubclass_WindowProc = True
      End If
      
   End Select
   '
End Function

Private Function IWindowsHook_HookProc(ByVal eType As
 vbalWinHook.EHTHookTypeConstants, ByVal nCode As Long, ByVal wParam As Long,
 ByVal lParam As Long, bConsume As Boolean) As Long
Dim tP As POINTAPI
Dim tR As RECT
Dim x As Single
Dim y As Single
Dim eShift As ShiftConstants
Dim hWndA As Long
Dim bOver As Boolean

   If ((wParam = WM_XBUTTONDOWN) Or _
      (wParam = WM_XBUTTONUP) Or _
      (wParam = WM_XBUTTONDBLCLK) Or _
      (wParam = WM_NCXBUTTONDOWN) Or _
      (wParam = WM_NCXBUTTONUP) Or _
      (wParam = WM_NCXBUTTONDBLCLK)) Then
   
      If Not (GetAsyncKeyState(VK_XBUTTON1) = 0) Then
         m_eHookButtonDown = XBUTTON1
      ElseIf Not (GetAsyncKeyState(VK_XBUTTON2) = 0) Then
         m_eHookButtonDown = XBUTTON2
      End If
         
      GetCursorPos tP
      hWndA = WindowFromPoint(tP.x, tP.y)
      Do While Not (bOver) And Not (hWndA = 0)
         If (hWndA = m_hWnd) Then
            bOver = True
         Else
            hWndA = GetParent(hWndA)
         End If
      Loop
         
      If bOver Then
         ScreenToClient m_hWnd, tP
         x = m_objTo.ScaleX(tP.x, vbTwips)
         y = m_objTo.ScaleX(tP.y, vbTwips)
         eShift = vbShiftMask * Abs(Not (GetAsyncKeyState(vbKeyShift) = 0)) Or _
               vbCtrlMask * Abs(Not (GetAsyncKeyState(vbKeyControl) = 0)) Or _
               vbAltMask * Abs(Not (GetAsyncKeyState(vbKeyMenu) = 0))
         
         bConsume = True ' If not true, the thing goes a bit mad!
         If (wParam = WM_XBUTTONDOWN) Or (wParam = WM_NCXBUTTONDOWN) Then
            RaiseEvent XMouseDown(m_eHookButtonDown, eShift, x, y, bConsume)
         ElseIf (wParam = WM_XBUTTONUP) Or (wParam = WM_NCXBUTTONUP) Then
            RaiseEvent XMouseUp(m_eHookButtonDown, eShift, x, y, bConsume)
         Else
            RaiseEvent XBtnDblClick(m_eHookButtonDown, eShift, x, y, bConsume)
         End If
      End If
      
   End If
   
End Function
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../index.html">VB</a>&#160;.&#160;<a href="../index.html">Tips</a>&#160;.&#160;<a href="article.html">Detecting Mouse XButton Clicks</a>&#160;.&#160;<a href="VB5_XButton_Sample.html">VB5 XButton Sample</a>&#160;.&#160;cXButtonEvents.cls</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 5 July 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Tips/Detecting_Mouse_XButton_Clicks/VB5_XButton_Sample_zip_cXButtonEvents_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:22 GMT -->
</html>