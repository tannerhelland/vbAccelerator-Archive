<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Writing_CDs/Writing_Audio_CDs/VB6_Audio_Burner_zip_frmAudioBurner_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:41 GMT -->
<head>
<title>vbAccelerator - Contents of code file: frmAudioBurner.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: frmAudioBurner.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /><link rel="SHORTCUT ICON" href="http://www.vbaccelerator.com/home/res/vbaccel.ico" /><link rel="copyright" href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp" /><link rel="contents" href="VB6_Audio_Burner.html" /><link rel="meta" type="application/rdf+xml" href="VB6%20Audio%20Burner.html" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Writing CDs</a>&#160;.&#160;<a href="article.html">Writing Audio CDs</a>&#160;.&#160;<a href="VB6_Audio_Burner.html">VB6 Audio Burner</a>&#160;.&#160;frmAudioBurner.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Audio_Burner.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Audio Burner</a> (81K)</p><p /><p class="nav"><a href="VB6_Audio_Burner.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Audio Burner</a> (78K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:15091</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=15091&amp;type=Zip&amp;title=VB6%20Audio%20Burner%2Ezip%5FfrmAudioBurner">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" />﻿<p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />2 Jun 2004<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=14968">Image Mastering API (IMAPI) Library for VB</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=14976">VB IMAPI (CD Burning) Interfaces (IVBIMAPI.tlb)</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=14973">Using WinAmp In Plugins From VB</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: frmAudioBurner.frm</h1><p>This file is part of the download <a href="VB6_Audio_Burner.html">VB6 Audio Burner</a>, which is described in the article <a href="article.html">Writing Audio CDs</a>.</p><pre>VERSION 5.00
Begin VB.Form frmAudioBurner 
   Caption         =   "vbAccelerator Audio CD Burner"
   ClientHeight    =   6360
   ClientLeft      =   3540
   ClientTop       =   2550
   ClientWidth     =   6570
   BeginProperty Font 
      Name            =   "Tahoma"
      Size            =   8.25
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "frmAudioBurner.frx":0000
   LinkTopic       =   "Form1"
   ScaleHeight     =   6360
   ScaleWidth      =   6570
   Begin VB.PictureBox picStatus 
      Align           =   2  'Align Bottom
      BorderStyle     =   0  'None
      Height          =   735
      Left            =   0
      ScaleHeight     =   735
      ScaleWidth      =   6570
      TabIndex        =   13
      Top             =   5625
      Width           =   6570
      Begin VB.PictureBox picProgress 
         BorderStyle     =   0  'None
         Height          =   315
         Left            =   60
         ScaleHeight     =   315
         ScaleWidth      =   6315
         TabIndex        =   14
         Top             =   0
         Width           =   6315
      End
      Begin VB.Label lblStatus 
         Height          =   255
         Left            =   60
         TabIndex        =   15
         Top             =   420
         Width           =   6315
      End
   End
   Begin VB.PictureBox picBurn 
      BorderStyle     =   0  'None
      Height          =   735
      Left            =   60
      ScaleHeight     =   735
      ScaleWidth      =   6315
      TabIndex        =   9
      Top             =   4680
      Width           =   6315
      Begin VB.CommandButton cmdBurn 
         Caption         =   "&amp;Burn"
         Height          =   435
         Left            =   1560
         TabIndex        =   12
         Top             =   120
         Width           =   1335
      End
      Begin VB.CheckBox chkSimulate 
         Caption         =   "&amp;Simulate Burn"
         Height          =   255
         Left            =   3060
         TabIndex        =   11
         Top             =   120
         Value           =   1  'Checked
         Width           =   2775
      End
      Begin VB.CheckBox chkEject 
         Caption         =   "&amp;Eject when Done"
         Height          =   255
         Left            =   3060
         TabIndex        =   10
         Top             =   420
         Value           =   1  'Checked
         Width           =   2775
      End
   End
   Begin VB.PictureBox picFiles 
      BorderStyle     =   0  'None
      ClipControls    =   0   'False
      Height          =   4155
      Left            =   60
      ScaleHeight     =   4155
      ScaleWidth      =   6315
      TabIndex        =   2
      Top             =   480
      Width           =   6315
      Begin VB.ListBox lstFiles 
         Height          =   3570
         Left            =   1560
         TabIndex        =   7
         Top             =   0
         Width           =   4275
      End
      Begin VB.CommandButton cmdAdd 
         Caption         =   "&amp;Add..."
         Height          =   435
         Left            =   1560
         TabIndex        =   6
         Top             =   3660
         Width           =   1335
      End
      Begin VB.CommandButton cmdRemove 
         Caption         =   "&amp;Remove..."
         Height          =   435
         Left            =   2940
         TabIndex        =   5
         Top             =   3660
         Width           =   1335
      End
      Begin VB.CommandButton cmdMoveUp 
         Caption         =   ""
         BeginProperty Font 
            Name            =   "Wingdings"
            Size            =   8.25
            Charset         =   2
            Weight          =   400
            Underline       =   0   'False
            Italic          =   0   'False
            Strikethrough   =   0   'False
         EndProperty
         Height          =   315
         Left            =   5880
         TabIndex        =   4
         Top             =   1500
         Width           =   375
      End
      Begin VB.CommandButton cmdMoveDown 
         Caption         =   ""
         BeginProperty Font 
            Name            =   "Wingdings"
            Size            =   8.25
            Charset         =   2
            Weight          =   400
            Underline       =   0   'False
            Italic          =   0   'False
            Strikethrough   =   0   'False
         EndProperty
         Height          =   315
         Left            =   5880
         TabIndex        =   3
         Top             =   1860
         Width           =   375
      End
      Begin VB.Label lblFiles 
         Caption         =   "&amp;Files:"
         Height          =   255
         Left            =   0
         TabIndex        =   8
         Top             =   60
         Width           =   1515
      End
   End
   Begin VB.ComboBox cboRecorder 
      Height          =   315
      Left            =   1620
      Style           =   2  'Dropdown List
      TabIndex        =   1
      Top             =   60
      Width           =   4815
   End
   Begin VB.Label lblRecorder 
      Caption         =   "&amp;Recorder:"
      Height          =   255
      Left            =   60
      TabIndex        =   0
      Top             =   120
      Width           =   1515
   End
End
Attribute VB_Name = "frmAudioBurner"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private Enum EApplicationMode
   eIdle
   eBurning
End Enum

Private WithEvents m_cDiscMaster As cDiscMaster
Attribute m_cDiscMaster.VB_VarHelpID = -1
Private m_cRedbook As cRedbookDiscMaster
Private m_bCancel As Boolean
Private m_eMode As EApplicationMode

Private m_cProgress As pcProgressBar

Private Sub createCD()
Dim lFreeBlocks As Long
Dim lTotalBlocks As Long
Dim lBlockSize As Long
Dim iFile As Long
Dim iRecorder As Long
Dim cRecorder As cDiscRecorder
Dim bCloseExclusive As Boolean
Dim cInfo As cMediaInfo
Dim cProps As cDiscRecorderProperties
Dim cProp As cProperty
Dim iProp As Long
   
   setApplicationMode eBurning
   
   Set m_cProgress = New pcProgressBar
   m_cProgress.XpStyle = True
   m_cProgress.DrawObject = picProgress
   m_cProgress.BarColor = vbHighlight
   picProgress.Visible = True
   
   showStatus "Opening Recorder for burning.."
   Set m_cRedbook = m_cDiscMaster.RedbookDiscMaster
   
   iRecorder = cboRecorder.ItemData(cboRecorder.ListIndex)
   m_cDiscMaster.Recorders(iRecorder).SetAsActive
   Set cRecorder = m_cDiscMaster.Recorders.ActiveRecorder
   
   showStatus "Checking media type.."
   cRecorder.OpenExclusive
   bCloseExclusive = True
   Set cInfo = cRecorder.MediaInfo
   If (cInfo.MediaPresent) And _
      ((cInfo.mediaflags And MEDIA_WRITABLE) = MEDIA_WRITABLE) Then
   
      cRecorder.CloseExclusive
      bCloseExclusive = False
      
      If Not (m_bCancel) Then
      
         If (cInfo Is Nothing) Then
            MsgBox "Please insert writable CD media into the " &amp;
             cboRecorder.List(cboRecorder.ListIndex) &amp; " recorder.",
             vbExclamation
         Else
            lFreeBlocks = cInfo.FreeBlocks
            
            ' Generate the progress info
            lBlockSize = m_cRedbook.AudioBlockSize
            For iFile = 1 To lstFiles.ListCount
               lTotalBlocks = lTotalBlocks + lstFiles.ItemData(iFile - 1) \
                lBlockSize
            Next iFile
            m_cProgress.Value = 0
            m_cProgress.Max = lTotalBlocks
            
            If (lTotalBlocks &lt;= lFreeBlocks) Then
            
               ' First, create the CD Image
               showStatus "Creating the CD Image..."
               createCDImage
            
               If Not (m_bCancel) Then
               
                  Dim lErr As Long
                  On Error Resume Next
                  m_cDiscMaster.RecordDisc (chkSimulate.Value = vbChecked),
                   (chkEject.Value = vbChecked)
                  lErr = Err.Number
                  On Error GoTo 0
                  If Not (lErr = 0) Then
                     MsgBox "An error occurred whilst recording the disc: " &amp;
                      DecodeIMAPIError(lErr), vbExclamation
                  End If
                              
               End If
               
            Else
               MsgBox "The compilation is too long to burn onto the selected
                media.", vbExclamation
            End If
            
         End If
      End If
   Else
      MsgBox "Please insert writable media into the " &amp;
       cboRecorder.List(cboRecorder.ListIndex) &amp; " recorder.", vbExclamation
   End If
      
   If (m_bCancel) Then
      On Error Resume Next
      m_cDiscMaster.ClearFormatContent
      On Error GoTo 0
   End If
   
   If (bCloseExclusive) Then
      cRecorder.CloseExclusive
   End If
   
   setApplicationMode eIdle
      
End Sub

Private Sub createCDImage()
Dim iFile As Long
Dim sFile As String
      
   For iFile = 1 To lstFiles.ListCount
      sFile = lstFiles.List(iFile - 1)
      showStatus "Adding track " &amp; sFile &amp; "..."
      addTrack sFile
      If m_bCancel Then
         Exit For
      End If
   Next iFile

End Sub

Private Function addTrack(ByVal sFile As String) As Long
Dim lBlockSize As Long
Dim cWav As cWavReader
Dim lTrackSize As Long
Dim lTrackBlocks As Long
Dim bMore As Boolean
Dim lReadSize As Long
Dim lWrittenSize As Long
Dim lWrittenBlocks As Long

   lBlockSize = m_cRedbook.AudioBlockSize
   
   Set cWav = New cWavReader
   cWav.ReadBufferSize = lBlockSize \ 4
   cWav.OpenFile sFile
      
   lTrackSize = cWav.AudioLength * 4
   lTrackBlocks = lTrackSize \ lBlockSize
   If (lTrackSize Mod lBlockSize) &gt; 0 Then
      lTrackBlocks = lTrackBlocks + 1
   End If
      
   m_cRedbook.CreateAudioTrack lTrackBlocks
   
   Do
      bMore = cWav.Read
      If (bMore) Then
         lReadSize = cWav.ReadSize * 4
         cWav.ZeroUnusedBufferBytes
         m_cRedbook.AddAudioTrackBlocks cWav.ReadBufferPtr, lBlockSize
         
         lWrittenSize = lWrittenSize + lReadSize
         lWrittenBlocks = lWrittenBlocks + 1
         m_cProgress.Value = m_cProgress.Value + 1
         DoEvents
      End If
      
   Loop While (bMore) And Not (m_bCancel)

   If Not (m_bCancel) Then
      m_cRedbook.CloseAudioTrack
   End If
   
   cWav.CloseFile
   
   addTrack = lWrittenBlocks
   '
End Function

Private Sub setApplicationMode(ByVal eMode As EApplicationMode)
   
   m_eMode = eMode
   If (eMode = eBurning) Then
      
      m_bCancel = False
      cmdBurn.Caption = "Cancel"
      
      enableControl cboRecorder, False
      enableControl lstFiles, False
      enableControl cmdMoveUp, False
      enableControl cmdMoveDown, False
      enableControl cmdAdd, False
      enableControl cmdRemove, False
      enableControl chkSimulate, False
      enableControl chkEject, False
      
   Else
   
      cmdBurn.Caption = "&amp;Burn"
      enableControl cboRecorder, (cboRecorder.ListCount &gt; 0)
      enableControl cmdAdd, True
      enableControl lstFiles, True
      lstFiles_Click
      
      picProgress.Visible = False
      
      showStatus "Ready"
      
   End If
   
End Sub

Private Sub enableControl(ctl As Control, ByVal bState As Boolean)
Dim oBackColor As OLE_COLOR
   oBackColor = IIf(bState, vbWindowBackground, vbButtonFace)
   ctl.Enabled = bState
   If TypeOf ctl Is ListBox Then
      ctl.BackColor = oBackColor
   ElseIf TypeOf ctl Is ComboBox Then
      ctl.BackColor = oBackColor
   End If
End Sub

Private Sub showRecorders()
Dim iRecorder As Long
   
   Set m_cDiscMaster = New cDiscMaster
   m_cDiscMaster.Initialise

   With m_cDiscMaster.Recorders
      For iRecorder = 1 To .Count
         With .Recorder(iRecorder)
            If (.SupportsRedbook) Then
               cboRecorder.AddItem .VendorId &amp; " " &amp; .ProductId &amp; " " &amp;
                .RevisionId
               cboRecorder.ItemData(cboRecorder.NewIndex) = iRecorder
            End If
         End With
      Next iRecorder
   End With
   
   If (cboRecorder.ListCount &gt; 0) Then
      enableControl cboRecorder, True
      cboRecorder.ListIndex = 0
   End If

   showStatus "Ready."

End Sub

Private Sub showStatus(ByVal sStatus As String)
   lblStatus.Caption = sStatus
   lblStatus.Refresh
End Sub

Private Sub addFile(ByVal sFile As String)
   
   ' Check not already there
   Dim iFile As Long
   For iFile = 1 To lstFiles.ListCount
      If (lstFiles.List(iFile - 1) = sFile) Then
         MsgBox "This file already exists in your compilation.", vbInformation
         Exit Sub
      End If
   Next iFile
   
   ' Confirm valid wave file
   Dim lErr As Long
   Dim bR As Boolean
   Dim cWav As New cWavReader
   On Error Resume Next
   bR = cWav.OpenFile(sFile)
   lErr = Err.Number
   On Error GoTo 0
   
   If (bR And (lErr = 0)) Then
      lstFiles.AddItem sFile
      lstFiles.ItemData(lstFiles.NewIndex) = cWav.AudioLength * 4
      If (lstFiles.ListIndex &lt;&gt; lstFiles.NewIndex) Then
         lstFiles.ListIndex = lstFiles.NewIndex
      Else
         lstFiles_Click
      End If
   Else
      MsgBox "'" &amp; sFile &amp; "' is not a 16bit stereo 44.1kHz Wave File.",
       vbInformation
   End If
   
End Sub


Private Sub cboRecorder_Click()
Dim iRecorder As Long
   If (cboRecorder.ListIndex &gt; -1) Then
      iRecorder = cboRecorder.ItemData(cboRecorder.ListIndex)
      enableControl lstFiles, True
      enableControl cmdAdd, True
      enableControl cmdRemove, (lstFiles.ListIndex &gt; -1)
   Else
      enableControl lstFiles, False
      enableControl cmdAdd, False
      enableControl cmdRemove, False
   End If
End Sub

Private Sub cmdAdd_Click()
Dim cD As New pcCommonDialog
Dim sFile As String
Dim vFile As Variant
   
   If (cD.VBGetOpenFileName(sFile, _
      MultiSelect:=True, _
      Filter:="Wave Files (*.WAV)|*.WAV|All Files (*.*)|*.*", _
      DefaultExt:="WAV", _
      Owner:=Me.hWnd, flags:=OFN_EXPLORER)) Then
      
      For Each vFile In cD.GetMultiSelectFileNames(sFile)
         addFile vFile
      Next
   End If
   
End Sub

Private Sub cmdBurn_Click()
   '
   If (m_eMode = eIdle) Then
      createCD
   Else
      m_bCancel = True
   End If
   '
End Sub

Private Sub cmdMoveDown_Click()
Dim iIndexNow As Long
Dim sFile As String
Dim iSize As Long
   iIndexNow = lstFiles.ListIndex
   If (iIndexNow &lt; lstFiles.ListCount - 1) And (lstFiles.ListCount &gt; 1) Then
      sFile = lstFiles.List(iIndexNow)
      iSize = lstFiles.ItemData(iIndexNow)
      lstFiles.RemoveItem iIndexNow
      lstFiles.AddItem sFile, iIndexNow + 1
      lstFiles.ItemData(iIndexNow + 1) = iSize
      lstFiles.ListIndex = iIndexNow + 1
   End If
End Sub

Private Sub cmdMoveUp_Click()
Dim iIndexNow As Long
Dim sFile As String
Dim iSize As Long
   iIndexNow = lstFiles.ListIndex
   If (iIndexNow &gt; 0) And (lstFiles.ListCount &gt; 1) Then
      sFile = lstFiles.List(iIndexNow)
      iSize = lstFiles.ItemData(iIndexNow)
      lstFiles.RemoveItem iIndexNow
      lstFiles.AddItem sFile, iIndexNow - 1
      lstFiles.ItemData(iIndexNow - 1) = iSize
      lstFiles.ListIndex = iIndexNow - 1
   End If
End Sub

Private Sub cmdRemove_Click()
Dim sFile As String
Dim iIndex As Long
   If (lstFiles.ListIndex &gt; -1) Then
      iIndex = lstFiles.ListIndex
      sFile = lstFiles.List(iIndex)
      If (vbYes = MsgBox("Are you sure you want to remove '" &amp; sFile &amp; "' from
       the compilation?", vbQuestion Or vbYesNo)) Then
         lstFiles.RemoveItem iIndex
         If (lstFiles.ListCount &gt; 0) Then
            If (iIndex &lt; lstFiles.ListCount) Then
               lstFiles.ListIndex = iIndex
            Else
               lstFiles.ListIndex = lstFiles.ListCount - 1
            End If
         Else
            enableControl cmdBurn, False
            enableControl chkSimulate, False
            enableControl chkEject, False
         End If
      End If
   End If
End Sub

Private Sub Form_Load()
   
   enableControl cboRecorder, False
   enableControl lstFiles, False
   enableControl cmdAdd, False
   enableControl cmdRemove, False
   enableControl cmdMoveUp, False
   enableControl cmdMoveDown, False
   enableControl cmdBurn, False
   enableControl chkSimulate, False
   enableControl chkEject, False
   picProgress.Visible = False
   
   showStatus "Examining available disc recorders..."
   
   Me.Show
   Me.Refresh
   
   Screen.MousePointer = vbHourglass
   showRecorders
   Screen.MousePointer = vbDefault

End Sub

Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
   
   showStatus "Shutting down, please wait..."
   enableControl cboRecorder, False
   enableControl lstFiles, False
   enableControl cmdAdd, False
   enableControl cmdRemove, False
   enableControl cmdMoveUp, False
   enableControl cmdMoveDown, False
   enableControl cmdBurn, False
   Me.Enabled = False
   Me.Refresh
   
   Screen.MousePointer = vbHourglass
   
   Set m_cRedbook = Nothing
   m_cDiscMaster.ClearUp
   Set m_cDiscMaster = Nothing
   
   Screen.MousePointer = vbDefault
   
End Sub

Private Sub Form_Resize()
   On Error Resume Next
   cboRecorder.Width = Me.ScaleWidth - cboRecorder.Left - picFiles.Left
   picFiles.Width = Me.ScaleWidth - picFiles.Left * 2
   picFiles.Height = Me.ScaleHeight - picFiles.TOp - picStatus.Height -
    picBurn.Height
   picBurn.TOp = picFiles.TOp + picFiles.Height
   picBurn.Width = Me.ScaleWidth - picFiles.Left * 2
End Sub

Private Sub lstFiles_Click()
   enableControl cmdRemove, (lstFiles.ListIndex &gt; -1)
   enableControl cmdMoveUp, (lstFiles.ListIndex &gt; 0)
   enableControl cmdMoveDown, (lstFiles.ListIndex &lt; lstFiles.ListCount - 1)
   enableControl cmdBurn, (lstFiles.ListCount &gt; 0)
   enableControl chkSimulate, (lstFiles.ListCount &gt; 0)
   enableControl chkEject, (lstFiles.ListCount &gt; 0)
End Sub

Private Sub m_cDiscMaster_AddProgress(ByVal nCompleted As Long, ByVal nTotal As
 Long)
   '
   ' Raised as items are added to the stash.
   ' Not used in this example.
   '
End Sub

Private Sub m_cDiscMaster_BlockProgress(ByVal nCurrentBlock As Long, ByVal
 nTotalBlocks As Long)
   '
   DoEvents
   m_cProgress.Value = nCurrentBlock
   m_cProgress.Max = nTotalBlocks
   '
End Sub

Private Sub m_cDiscMaster_BurnComplete(ByVal status As Long)
   '
   showStatus "Burn Complete"
   picProgress.Visible = False
   '
End Sub

Private Sub m_cDiscMaster_ClosingDisc(ByVal nEstimatedSeconds As Long)
   '
   showStatus "Closing Disc: Estimated time left: " &amp; nEstimatedSeconds
   '
End Sub

Private Sub m_cDiscMaster_EraseComplete(ByVal status As Long)
   '
   showStatus "Disc erase complete."
   '
End Sub

Private Sub m_cDiscMaster_PnPActivity()
   '
   Debug.Print "PnPActivity"
   '
End Sub

Private Sub m_cDiscMaster_PreparingBurn(ByVal nEstimatedSeconds As Long)
   '
   showStatus "Preparing to burn disc: Estimated time: " &amp; nEstimatedSeconds
   '
End Sub

Private Sub m_cDiscMaster_QueryCancel(bCancel As Boolean)
   '
   DoEvents
   bCancel = m_bCancel
   '
End Sub

Private Sub m_cDiscMaster_TrackProgress(ByVal nCurrentTrack As Long, ByVal
 nTotalTracks As Long)
   '
   If (nCurrentTrack = nTotalTracks) Then
      showStatus "Completed burning tracks."
   Else
      showStatus "Burning track " &amp; nCurrentTrack + 1 &amp; " of " &amp; nTotalTracks
   End If
   '
End Sub

Private Sub picBurn_Resize()
   '
End Sub

Private Sub picFiles_Resize()
Dim lW As Long
Dim lH As Long

   On Error Resume Next
   '
   lW = picFiles.ScaleWidth - lstFiles.Left - cmdMoveUp.Width - 4 *
    Screen.TwipsPerPixelX
   lH = picFiles.ScaleHeight - cmdAdd.Height - 4 * Screen.TwipsPerPixelY
   lstFiles.Width = lW
   lstFiles.Height = lH
   cmdMoveUp.Left = lstFiles.Left + lW + 2 * Screen.TwipsPerPixelX
   cmdMoveUp.TOp = lstFiles.TOp + (lstFiles.Height - (cmdMoveUp.Height +
    Screen.TwipsPerPixelY) * 2) \ 2
   cmdMoveDown.Left = cmdMoveUp.Left
   cmdMoveDown.TOp = cmdMoveUp.TOp + cmdMoveUp.Height + 2 *
    Screen.TwipsPerPixelY
   cmdAdd.TOp = lstFiles.TOp + lH + 2 * Screen.TwipsPerPixelY
   cmdRemove.TOp = cmdAdd.TOp
   '
End Sub

Private Sub picProgress_Paint()
   If Not m_cProgress Is Nothing Then
      m_cProgress.Draw
   End If
End Sub

Private Sub picStatus_Resize()
   '
   On Error Resume Next
   picProgress.Width = picStatus.ScaleWidth - picProgress.Left * 2
   lblStatus.Width = picProgress.Width
   '
End Sub
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Writing CDs</a>&#160;.&#160;<a href="article.html">Writing Audio CDs</a>&#160;.&#160;<a href="VB6_Audio_Burner.html">VB6 Audio Burner</a>&#160;.&#160;frmAudioBurner.frm</p><br /><table style="font-size: 100%;"><tr><td valign="top"><a href="http://creativecommons.org/licenses/by/1.0/"><img src="../../../../../res/cc.png" width="88" height="31" alt="Creative Commons Licence" /></a></td></a></td><td valign="top"><p class="nav" style="padding-bottom: 4px;">All contents of this web site are licensed under a <a href="http://creativecommons.org/licenses/by/1.0/">Creative Commons Licence</a>, except where otherwise noted.</p><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2004 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>, vbAccelerator Ltd.<br />Last Updated: 9 June 2004</p></td></tr></table></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Writing_CDs/Writing_Audio_CDs/VB6_Audio_Burner_zip_frmAudioBurner_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:41 GMT -->
</html>