<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Animated_Cursors_in_VB/vbAccelerator_Animated_Cursor_Classes_Sample_zip_cAniCursor_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:55 GMT -->
<head>
<title>vbAccelerator - Contents of code file: cAniCursor.cls</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: cAniCursor.cls" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Animated Cursors in VB</a>&#160;.&#160;<a href="vbAccelerator_Animated_Cursor_Classes_Sample.html">vbAccelerator Animated Cursor Classes Sample</a>&#160;.&#160;cAniCursor.cls</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="vbAccelerator_Animated_Cursor_Classes_Sample.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />vbAccelerator Animated Cursor Classes Sample</a> (39K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:593</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=593&amp;type=Zip&amp;title=vbAccelerator%20Animated%20Cursor%20Classes%20Sample%2Ezip%5FcAniCursor">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />1 Jul 1999<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=5248">Animated Cursors</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=4565">Using Animated Cursors in .NET</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: cAniCursor.cls</h1><pre>VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "cAniCursor"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' ===========================================================================
' FileName:    cAniCursor
' Author:      Steve McMahon
' Date:        20 March 1999
'
' Loads and maintains an animated or standard cursor
' from a resource or file and allows the frames to
' be drawn onto a DC.
'
' ---------------------------------------------------------------------------
' Visit vbAccelerator at
'     http://vbaccelerator.com
' ===========================================================================

Private Declare Function DrawIconEx Lib "user32" (ByVal hDC As Long, ByVal
 xLeft As Long, ByVal yTop As Long, ByVal hIcon As Long, ByVal cxWidth As Long,
 ByVal cyWidth As Long, ByVal istepIfAniCur As Long, ByVal hbrFlickerFreeDraw
 As Long, ByVal diFlags As Long) As Long
Private Declare Function LoadImageLong Lib "user32" Alias "LoadImageA" (ByVal
 hInst As Long, ByVal lpsz As Long, ByVal uType As Long, ByVal cx As Long,
 ByVal cy As Long, ByVal uFlags As Long) As Long
Private Declare Function LoadImageString Lib "user32" Alias "LoadImageA" (ByVal
 hInst As Long, ByVal lpsz As String, ByVal uType As Long, ByVal cx As Long,
 ByVal cy As Long, ByVal uFlags As Long) As Long

Private Const IMAGE_BITMAP = 0&amp;
Private Const IMAGE_ICON = 1&amp;
Private Const IMAGE_CURSOR = 2&amp;

Private Const LR_LOADMAP3DCOLORS = &amp;H1000&amp;
Private Const LR_LOADFROMFILE = &amp;H10&amp;
Private Const LR_LOADTRANSPARENT = &amp;H20&amp;

Private Const DI_MASK = &amp;H1&amp;
Private Const DI_IMAGE = &amp;H2&amp;
Private Const DI_NORMAL = &amp;H3&amp;
Private Const DI_COMPAT = &amp;H4&amp;
Private Const DI_DEFAULTSIZE = &amp;H8&amp;

Private Declare Function CreateDCAsNull Lib "gdi32" Alias "CreateDCA" (ByVal
 lpDriverName As String, lpDeviceName As Any, lpOutput As Any, lpInitData As
 Any) As Long
Private Declare Function CreateCompatibleBitmap Lib "gdi32" (ByVal hDC As Long,
 ByVal nWidth As Long, ByVal nHeight As Long) As Long
Private Declare Function CreateCompatibleDC Lib "gdi32" (ByVal hDC As Long) As
 Long
Private Declare Function DeleteDC Lib "gdi32" (ByVal hDC As Long) As Long
Private Declare Function DestroyCursor Lib "user32" (ByVal hCursor As Long) As
 Long
Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As
 Long
Private Declare Function CreateSolidBrush Lib "gdi32" (ByVal crColor As Long)
 As Long
Private Declare Function OleTranslateColor Lib "OLEPRO32.DLL" (ByVal OLE_COLOR
 As Long, ByVal HPALETTE As Long, pccolorref As Long) As Long


Private Declare Function SetClassLong Lib "user32" Alias "SetClassLongA" (ByVal
 hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Const GCL_HCURSOR = (-12)

Private m_hCur As Long
Private m_iFrame As Long
Private m_iNumFrames As Long

Private Type tClassCursor
   hWnd As Long
   hCurOld As Long
End Type
Private m_tClassCursor() As tClassCursor
Private m_iCount As Long
Private m_bInDevelopment As Boolean

Public Function SetAsDefaultCursor(ByVal lhWnd As Long, ByVal bState As Boolean)
Dim lIndex As Long
Dim lFirstFreeIndex As Long
Dim i As Long

   If bState Then
      For i = 1 To m_iCount
         If m_tClassCursor(i).hWnd = lhWnd Then
            lIndex = i
            Exit For
         ElseIf m_tClassCursor(i).hWnd = 0 Then
            lFirstFreeIndex = i
         End If
      Next i
      If lIndex = 0 Then
         If lFirstFreeIndex = 0 Then
            m_iCount = m_iCount + 1
            ReDim Preserve m_tClassCursor(1 To m_iCount) As tClassCursor
            lIndex = m_iCount
         Else
            lIndex = lFirstFreeIndex
         End If
      End If
      If m_tClassCursor(lIndex).hCurOld = 0 Then
         m_tClassCursor(lIndex).hWnd = lhWnd
         m_tClassCursor(lIndex).hCurOld = SetClassLong(lhWnd, GCL_HCURSOR,
          m_hCur)
      End If
   Else
      For i = 1 To m_iCount
         If m_tClassCursor(i).hWnd = lhWnd Then
            lIndex = i
            Exit For
         End If
      Next i
      If lIndex &lt;&gt; 0 Then
         If m_tClassCursor(lIndex).hCurOld &lt;&gt; 0 Then
            SetClassLong lhWnd, GCL_HCURSOR, m_tClassCursor(lIndex).hCurOld
            m_tClassCursor(lIndex).hWnd = 0
            m_tClassCursor(lIndex).hCurOld = 0
         End If
      End If
   End If

End Function


Private Function TranslateColor(ByVal oClr As OLE_COLOR, _
                        Optional hPal As Long = 0) As Long
Const CLR_INVALID = -1
    ' Convert Automation color to Windows color
    If OleTranslateColor(oClr, hPal, TranslateColor) Then
        TranslateColor = CLR_INVALID
    End If
End Function


Public Property Get hCursor() As Long
   ' Returns the windows handle of the animated
   ' cursor maintained by this class.  Do not
   ' call DeleteObject on this cursor!
   hCursor = m_hCur
End Property

Public Function LoadFromFile(ByVal sFileName As String, Optional ByVal
 bMapSysColours As Boolean, Optional ByVal bNotAnimated As Boolean = False) As
 Boolean
Dim lErr As Long
Dim lFlags As Long
   Destroy
   ' Load the animated cursor from the file:
   lFlags = LR_LOADFROMFILE
   ' Set flag for sys colour mapping:
   If bMapSysColours Then
      lFlags = lFlags Or LR_LOADMAP3DCOLORS
   End If
   m_hCur = LoadImageString(App.hInstance, sFileName, IMAGE_CURSOR, 0, 0,
    lFlags)
   ' Check for errors:
   lErr = Err.LastDllError
   If (m_hCur &lt;&gt; 0) Then
      ' Ok!
      If Not (bNotAnimated) Then
         EvaluateFrames
      End If
      LoadFromFile = True
   Else
      ' Failed.  Return error code:
      Err.Raise 10000 Or lErr, App.EXEName &amp; ".cAniCursor", WinAPIError(lErr)
   End If
End Function
Public Function LoadFromResource(ByVal lResourceID As Long, Optional ByVal
 bMapSysColours As Boolean, Optional ByVal hInstance As Long = 0, Optional
 ByVal bNotAnimated As Boolean = False) As Boolean
Dim lErr As Long
Dim lFlags As Long
   ' Remove existing instance:
   Destroy
   ' Ensure hInstance correct - if missing assume local module:
   If (hInstance = 0) Then
      hInstance = App.hInstance
   End If
   ' Check for sys colour mapping:
   If bMapSysColours Then
      lFlags = lFlags Or LR_LOADMAP3DCOLORS
   End If
   ' Load the animated cursor from the resource:
   If IsNT Then
      m_hCur = LoadImageLong(hInstance, lResourceID, IMAGE_CURSOR, 0, 0, lFlags)
      ' Check for errors:
      lErr = Err.LastDllError
      If (m_hCur &lt;&gt; 0) Then
         ' Ok!
         If Not (bNotAnimated) Then
            EvaluateFrames
         End If
         LoadFromResource = True
      Else
         ' Failed.  Return error code:
         If InDevelopment Then
            Err.Raise 10000, App.EXEName &amp; ".cAniCursor", "Cursors cannot be
             loaded from resource in the IDE."
         Else
            Err.Raise 10000 Or lErr, App.EXEName &amp; ".cAniCursor",
             WinAPIError(lErr)
         End If
      End If
   Else
      Dim b() As Byte
      Dim iFile As Integer
      Dim sTemp As String
      ' Otherwise, workaround:
      b = LoadResData(2001, 21)
      sTemp = TempFileName("ANI")
      On Error Resume Next
      Kill sTemp
      iFile = FreeFile
      Open sTemp For Binary Access Write Lock Read As #iFile
      Put #iFile, , b
      Close #iFile
      
      LoadFromFile sTemp
      
      Kill sTemp
   End If
   
End Function
Private Function InDevelopment() As Boolean
   Debug.Assert pbInDevelopment()
   InDevelopment = m_bInDevelopment
End Function
Private Function pbInDevelopment() As Boolean
   m_bInDevelopment = True
   pbInDevelopment = True
End Function
Private Sub EvaluateFrames()
Dim i As Long
Dim hDCMem As Long
Dim hDCDesk As Long
Dim lR As Long
   ' Determine the number of frames in the animated cursor
   ' by trying to draw it into a memory DC:
   hDCDesk = CreateDCAsNull("DISPLAY", ByVal 0&amp;, ByVal 0&amp;, ByVal 0&amp;)
   If (hDCDesk &lt;&gt; 0) Then
      hDCMem = CreateCompatibleDC(hDCDesk)
      If hDCMem &lt;&gt; 0 Then
         Do
            lR = DrawIconEx(hDCMem, 0, 0, m_hCur, 0, 0, i, 0, DI_NORMAL Or
             DI_DEFAULTSIZE)
            If lR = 0 Then
               m_iNumFrames = i
               Exit Do
            Else
               i = i + 1
            End If
         Loop While i &lt; 1024 ' SPM - There must be a smarter way to determine
          that an image
                             ' has only one frame than this!
         DeleteDC hDCMem
      End If
      DeleteDC hDCDesk
   End If
End Sub

Public Property Get Frame() As Long
   ' Returns the current frame:
   Frame = m_iFrame
End Property
Public Property Let Frame(ByVal iFrame As Long)
   ' Set the frame if in range
   If (iFrame &gt;= 0) And (iFrame &lt; m_iNumFrames) Then
      m_iFrame = iFrame
   Else
      Err.Raise 9, App.EXEName &amp; ".cAniCursor"
   End If
End Property
Public Property Get FrameCount() As Long
   FrameCount = m_iNumFrames
End Property
Public Sub Step()
   m_iFrame = m_iFrame + 1
   If (m_iFrame &lt; 0) Then m_iFrame = 0
   If (m_iFrame &gt;= m_iNumFrames) Then m_iFrame = 0
End Sub
Public Sub Destroy()
Dim i As Long
   If m_hCur &lt;&gt; 0 Then
      For i = 1 To m_iCount
         If m_tClassCursor(i).hCurOld &lt;&gt; 0 Then
            SetClassLong m_tClassCursor(i).hWnd, GCL_HCURSOR,
             m_tClassCursor(i).hCurOld
         End If
      Next i
      Erase m_tClassCursor
      m_iCount = 0
      DestroyCursor m_hCur
      m_hCur = 0
      m_iNumFrames = 0
      m_iFrame = 0
   End If
End Sub
Public Sub Draw( _
      ByVal hDC As Long, _
      Optional ByVal lLeft As Long = 0, _
      Optional ByVal lTop As Long = 0, _
      Optional ByVal lWidth As Long = 0, _
      Optional ByVal lHeight As Long = 0, _
      Optional ByVal lBackColor As OLE_COLOR = -1 _
   )
' Draw the current frame of the animated cursor on the specified DC.
' optionally you can set left and top position.  If you specify
' width and height then the drawing code will attempt to draw the
' animated cursor at the specified size.
' Setting the backcolor will cause windows to automatically fill the
' background behind the animated cursor with that colour.  The cursor
' will no longer be drawn transparently.

Dim lFlags As Long
Dim hBr As Long
   If (lWidth = 0) Or (lHeight = 0) Then
      lFlags = DI_NORMAL Or DI_DEFAULTSIZE
   Else
      lFlags = DI_NORMAL
   End If
   If lBackColor &lt;&gt; -1 Then
      ' Windows can automatically fill the background
      ' using this brush:
      hBr = CreateSolidBrush(TranslateColor(lBackColor))
   End If
   DrawIconEx hDC, lLeft, lTop, m_hCur, lWidth, lHeight, m_iFrame, hBr, lFlags
   If hBr &lt;&gt; 0 Then
      DeleteObject hBr
   End If
End Sub

Private Sub Class_Terminate()
   ' Ensure we clear up any resources:
   Destroy
End Sub
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Animated Cursors in VB</a>&#160;.&#160;<a href="vbAccelerator_Animated_Cursor_Classes_Sample.html">vbAccelerator Animated Cursor Classes Sample</a>&#160;.&#160;cAniCursor.cls</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Animated_Cursors_in_VB/vbAccelerator_Animated_Cursor_Classes_Sample_zip_cAniCursor_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:55 GMT -->
</html>