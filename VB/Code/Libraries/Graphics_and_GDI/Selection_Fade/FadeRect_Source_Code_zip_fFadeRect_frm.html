<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Selection_Fade/FadeRect_Source_Code_zip_fFadeRect_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:47 GMT -->
<head>
<title>vbAccelerator - Contents of code file: fFadeRect.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: fFadeRect.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Fading Out Selected Areas Using UpdatedLayeredWindow</a>&#160;.&#160;<a href="FadeRect_Source_Code.html">FadeRect Source Code</a>&#160;.&#160;fFadeRect.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="FadeRect_Source_Code.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />FadeRect Source Code</a> (30K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:3495</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=3495&amp;type=Zip&amp;title=FadeRect%20Source%20Code%2Ezip%5FfFadeRect">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />3 Feb 2003<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2946">Window Shapes Using Layering</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3016">Alpha DIBSections</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: fFadeRect.frm</h1><pre>VERSION 5.00
Begin VB.Form fFadeRect 
   BorderStyle     =   0  'None
   Caption         =   "Form2"
   ClientHeight    =   2070
   ClientLeft      =   4545
   ClientTop       =   5160
   ClientWidth     =   4620
   LinkTopic       =   "Form2"
   MaxButton       =   0   'False
   MinButton       =   0   'False
   ScaleHeight     =   2070
   ScaleWidth      =   4620
   ShowInTaskbar   =   0   'False
End
Attribute VB_Name = "fFadeRect"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private Type SIZEAPI
   cx As Long
   cy As Long
End Type

Private Type POINTAPI
   x As Long
   y As Long
End Type

Private Type RECT
   left As Long
   top As Long
   right As Long
   bottom As Long
End Type

Private Type BLENDFUNCTION
   BlendOp As Byte
   BlendFlags As Byte
   SourceConstantAlpha As Byte
   AlphaFormat As Byte
End Type

Private Const AC_SRC_OVER As Long = &amp;H0&amp;
Private Const ULW_COLORKEY As Long = &amp;H1&amp;
Private Const ULW_ALPHA As Long = &amp;H2&amp;
Private Const ULW_OPAQUE As Long = &amp;H4&amp;

Private Const WS_EX_TOPMOST As Long = &amp;H8&amp;
Private Const WS_EX_TRANSPARENT  As Long = &amp;H20&amp;
Private Const WS_EX_TOOLWINDOW As Long = &amp;H80&amp;
Private Const WS_EX_LAYERED As Long = &amp;H80000
Private Const WS_POPUP = &amp;H80000000
Private Const WS_VISIBLE = &amp;H10000000
Private Const SPI_GETSELECTIONFADE As Long = &amp;H1014&amp;

Private Declare Function CreateWindowEX Lib "user32" Alias "CreateWindowExA"
 (ByVal dwExStyle As Long, ByVal lpClassName As String, ByVal lpWindowName As
 String, ByVal dwStyle As Long, ByVal x As Long, ByVal y As Long, ByVal nWidth
 As Long, ByVal nHeight As Long, ByVal hWndParent As Long, ByVal hMenu As Long,
 ByVal hInstance As Long, lpParam As Any) As Long
Private Declare Function DestroyWindow Lib "user32" (ByVal hwnd As Long) As Long
Private Declare Function SystemParametersInfo Lib "user32" Alias
 "SystemParametersInfoA" (ByVal uAction As Long, ByVal uParam As Long, ByRef
 lpvParam As Any, ByVal fuWinIni As Long) As Long
Private Declare Function UpdateLayeredWindow Lib "user32" ( _
    ByVal hwnd As Long, _
    ByVal hdcDst As Long, _
    pptDst As Any, _
    psize As Any, _
    ByVal hdcSrc As Long, _
    pptSrc As Any, _
    ByVal crKey As Long, _
    pblend As BLENDFUNCTION, _
    ByVal dwFlags As Long) As Long
Private Const GWL_STYLE = (-16)
Private Const GWL_EXSTYLE = (-20)
Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA"
 (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long

Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
    lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)

Private Declare Function IsWindowVisible Lib "user32" (ByVal hwnd As Long) As
 Long
Private Declare Function GetWindowRect Lib "user32" (ByVal hwnd As Long, lpRect
 As RECT) As Long

' Set the position of a window:
Private Const HWND_NOTOPMOST = -2
Private Const HWND_TOP = 0
Private Const HWND_TOPMOST = -1
Private Const HWND_BOTTOM = 1
Private Const SWP_NOACTIVATE = &amp;H10
Private Const SWP_SHOWWINDOW = &amp;H40
Private Declare Function SetWindowPos Lib "user32" (ByVal hwnd As Long, ByVal
 hWndInsertAfter As Long, ByVal x As Long, ByVal y As Long, ByVal cx As Long,
 ByVal cy As Long, ByVal wFlags As Long) As Long

Private Declare Function OleTranslateColor Lib "oleaut32.dll" (ByVal lOleColor
 As Long, ByVal lHPalette As Long, lColorRef As Long) As Long
Private Const CLR_NONE As Long = -1&amp;

Private Declare Function CreateDCAsNull Lib "gdi32" Alias "CreateDCA" (ByVal
 lpDriverName As String, lpDeviceName As Any, lpOutput As Any, lpInitData As
 Any) As Long
Private Declare Function DeleteDC Lib "gdi32" (ByVal hdc As Long) As Long

Private WithEvents m_tmr As CTimer
Attribute m_tmr.VB_VarHelpID = -1

Private m_hWnd As Long
Private m_bAlpha As Byte
Private m_bFadeStepSize As Byte
Private m_bForceFade As Boolean
Private m_oTransparentColor As OLE_COLOR
Private m_lColor As Long
Private m_lFlags As Long

Public Property Get TransparentColor() As OLE_COLOR
   TransparentColor = m_oTransparentColor
End Property
Public Property Let TransparentColor(ByVal oColor As OLE_COLOR)
   m_oTransparentColor = oColor
End Property

Public Property Get ForceFade() As Boolean
   ForceFade = m_bForceFade
End Property
Public Property Let ForceFade(ByVal value As Boolean)
   m_bForceFade = value
End Property

Public Property Get FadeEffectsSelected() As Boolean
Dim lFade As Long
   SystemParametersInfo SPI_GETSELECTIONFADE, 0, lFade, 0
   FadeEffectsSelected = Not (lFade = 0)
End Property

Public Property Get FadeStepSize() As Byte
   FadeStepSize = m_bFadeStepSize
End Property
Public Property Let FadeStepSize(ByVal value As Byte)
   If (value &lt; 1) Then
      Err.Raise 380, App.EXEName &amp; ".fFadeRect", "FadeStepSize must be between
       1 and 255."
   Else
      m_bFadeStepSize = value
   End If
End Property

Public Sub FadeRect( _
      ByVal lSrcLeft As Long, _
      ByVal lSrcTop As Long, _
      ByVal lLeft As Long, _
      ByVal lTop As Long, _
      ByVal lWidth As Long, _
      ByVal lheight As Long, _
      Optional ByVal lhDCSrc As Long = 0 _
   )
Dim lFade As Long
Dim tSize As SIZEAPI
Dim tBlend As BLENDFUNCTION
Dim lBlend As Long
Dim tPtSrc As POINTAPI
Dim bDelDC As Boolean
Dim lhDC As Long
    
   ' // Be nice and respect the user's wishes: Do they want the fade?
   If Not m_bForceFade Then
      SystemParametersInfo SPI_GETSELECTIONFADE, 0, lFade, 0
      If (lFade = 0) Then
         Unload Me
         Exit Sub
      End If
   End If
   
   If (lhDCSrc = 0) Then
      lhDC = CreateDCAsNull("DISPLAY", ByVal 0&amp;, ByVal 0&amp;, ByVal 0&amp;)
      bDelDC = True
   Else
      lhDC = lhDCSrc
   End If
   
   Me.Move lLeft * Screen.TwipsPerPixelX, lTop * Screen.TwipsPerPixelY
   SetWindowPos m_hWnd, HWND_TOPMOST, lLeft, lTop, 0, 0, SWP_NOACTIVATE Or
    SWP_SHOWWINDOW

   If Not (m_hWnd = 0) Then
      Dim tR As RECT
      GetWindowRect m_hWnd, tR
   
      tSize.cx = lWidth
      tSize.cy = lheight
   
      tPtSrc.x = lSrcLeft
      tPtSrc.y = lSrcTop
   
      m_bAlpha = 255
      tBlend.BlendOp = AC_SRC_OVER
      tBlend.BlendFlags = 0
      tBlend.AlphaFormat = 0
      tBlend.SourceConstantAlpha = m_bAlpha
      
      m_lColor = TranslateColor(m_oTransparentColor)
      m_lFlags = ULW_ALPHA
      If Not (m_oTransparentColor = CLR_NONE) Then
         m_lFlags = m_lFlags Or ULW_COLORKEY
      End If
      On Error Resume Next ' in case run on non-2k+ systems
      UpdateLayeredWindow m_hWnd, ByVal 0&amp;, ByVal 0&amp;, tSize, lhDC, tPtSrc,
       m_lColor, _
            tBlend, m_lFlags
    
      ' Finally set the animation timer
      Set m_tmr = New CTimer
      m_tmr.Interval = 25
   End If
   
   If bDelDC Then
      DeleteDC lhDCSrc
   End If
   
End Sub


Private Sub Form_Initialize()
   m_bFadeStepSize = 15
   m_oTransparentColor = vbButtonFace
End Sub

Private Sub Form_Load()
Dim lExStyle As Long
Dim lStyle As Long

   m_hWnd = Me.hwnd
   lExStyle = WS_EX_LAYERED Or WS_EX_TRANSPARENT Or WS_EX_TOPMOST Or
    WS_EX_TOOLWINDOW
   lStyle = WS_POPUP Or WS_VISIBLE
   SetWindowLong m_hWnd, GWL_STYLE, lStyle
   SetWindowLong m_hWnd, GWL_EXSTYLE, lExStyle
   
End Sub

Private Sub Form_MouseDown(Button As Integer, Shift As Integer, x As Single, y
 As Single)
   ' This won't happen since we've given the Window
   ' the WS_EX_TRANSPARENT style, which means it is
   ' invisible to mouse clicks.
   MsgBox "Clicked", vbExclamation
End Sub

Private Sub m_tmr_ThatTime()
Dim tBlend As BLENDFUNCTION
Dim lBlend As Long
   
   If (m_bAlpha &gt;= m_bFadeStepSize) Then
      m_bAlpha = m_bAlpha - m_bFadeStepSize
      
      tBlend.BlendOp = AC_SRC_OVER
      tBlend.BlendFlags = 0
      tBlend.AlphaFormat = 0
      tBlend.SourceConstantAlpha = m_bAlpha
 
      On Error Resume Next ' for non-2k+ systems
      UpdateLayeredWindow m_hWnd, ByVal 0&amp;, ByVal 0&amp;, ByVal 0&amp;, ByVal 0&amp;, ByVal
       0&amp;, _
                    ByVal 0&amp;, tBlend, ULW_ALPHA
   Else
      ' complete
      Unload Me
      m_tmr.Interval = 0
      Set m_tmr = Nothing
   End If
   
End Sub


Private Function TranslateColor(ByVal oClr As OLE_COLOR, _
                                   Optional hPal As Long = 0) As Long
   ' Convert Automation color to Windows color
   If OleTranslateColor(oClr, hPal, TranslateColor) Then
       TranslateColor = -1
   End If
   
End Function


</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Fading Out Selected Areas Using UpdatedLayeredWindow</a>&#160;.&#160;<a href="FadeRect_Source_Code.html">FadeRect Source Code</a>&#160;.&#160;fFadeRect.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Selection_Fade/FadeRect_Source_Code_zip_fFadeRect_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:47 GMT -->
</html>