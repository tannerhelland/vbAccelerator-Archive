<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Drop_Shadows/VB6_Drop_Shadow_Code_zip_fDropShadow_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:47 GMT -->
<head>
<title>vbAccelerator - Contents of code file: fDropShadow.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: fDropShadow.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Creating Drop-Shadows</a>&#160;.&#160;<a href="VB6_Drop_Shadow_Code.html">VB6 Drop Shadow Code</a>&#160;.&#160;fDropShadow.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Drop_Shadow_Code.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Drop Shadow Code</a> (52K)</p><p /><p class="nav"><a href="VB6_Drop_Shadow_Code.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Drop Shadow Code</a> (49K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:3554</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=3554&amp;type=Zip&amp;title=VB6%20Drop%20Shadow%20Code%2Ezip%5FfDropShadow">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />3 Feb 2003<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2188">Sophisticated Control Over Window Sizing and Moving </a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3016">Alpha DIBSections</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3488">Fading Out Selected Areas Using UpdatedLayeredWindow</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: fDropShadow.frm</h1><pre>VERSION 5.00
Begin VB.Form fDropShadow 
   AutoRedraw      =   -1  'True
   BorderStyle     =   0  'None
   ClientHeight    =   2085
   ClientLeft      =   6315
   ClientTop       =   4785
   ClientWidth     =   1755
   ControlBox      =   0   'False
   LinkTopic       =   "Form2"
   MaxButton       =   0   'False
   MinButton       =   0   'False
   ScaleHeight     =   2085
   ScaleWidth      =   1755
   ShowInTaskbar   =   0   'False
   Visible         =   0   'False
End
Attribute VB_Name = "fDropShadow"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit


Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
    lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)

Private Type SAFEARRAYBOUND
    cElements As Long
    lLbound As Long
End Type
Private Type SAFEARRAY2D
    cDims As Integer
    fFeatures As Integer
    cbElements As Long
    cLocks As Long
    pvData As Long
    Bounds(0 To 1) As SAFEARRAYBOUND
End Type
Private Declare Function VarPtrArray Lib "msvbvm60.dll" Alias "VarPtr" (Ptr()
 As Any) As Long

Private Type RGBQUAD
    rgbBlue As Byte
    rgbGreen As Byte
    rgbRed As Byte
    rgbReserved As Byte
End Type
Private Type BITMAPINFOHEADER '40 bytes
    biSize As Long
    biWidth As Long
    biHeight As Long
    biPlanes As Integer
    biBitCount As Integer
    biCompression As Long
    biSizeImage As Long
    biXPelsPerMeter As Long
    biYPelsPerMeter As Long
    biClrUsed As Long
    biClrImportant As Long
End Type
Private Type BITMAPINFO
    bmiHeader As BITMAPINFOHEADER
    bmiColors As RGBQUAD
End Type
Private Declare Function CreateCompatibleDC Lib "gdi32" (ByVal hdc As Long) As
 Long
Private Declare Function GetDC Lib "user32" (ByVal hwnd As Long) As Long
Private Declare Function GetDesktopWindow Lib "user32" () As Long
' Note - this is not the declare in the API viewer - modify lplpVoid to be
' Byref so we get the pointer back:
Private Declare Function CreateDIBSection Lib "gdi32" _
    (ByVal hdc As Long, _
    pBitmapInfo As BITMAPINFO, _
    ByVal un As Long, _
    lplpVoid As Long, _
    ByVal handle As Long, _
    ByVal dw As Long) As Long

Private Type SIZEAPI
   cx As Long
   cy As Long
End Type

Private Type POINTAPI
   x As Long
   y As Long
End Type

Private Type RECT
   Left As Long
   Top As Long
   Right As Long
   Bottom As Long
End Type

Private Type BLENDFUNCTION
   BlendOp As Byte
   BlendFlags As Byte
   SourceConstantAlpha As Byte
   AlphaFormat As Byte
End Type

Private Const AC_SRC_OVER As Long = &amp;H0&amp;
Private Const ULW_COLORKEY As Long = &amp;H1&amp;
Private Const ULW_ALPHA As Long = &amp;H2&amp;
Private Const ULW_OPAQUE As Long = &amp;H4&amp;
Private Const AC_SRC_ALPHA = &amp;H1


Private Const WS_EX_TOPMOST As Long = &amp;H8&amp;
Private Const WS_EX_TRANSPARENT  As Long = &amp;H20&amp;
Private Const WS_EX_TOOLWINDOW As Long = &amp;H80&amp;
Private Const WS_EX_LAYERED As Long = &amp;H80000
Private Const WS_POPUP = &amp;H80000000
Private Const WS_VISIBLE = &amp;H10000000
Private Const SPI_GETDROPSHADOW  As Long = &amp;H1024&amp;

Private Declare Function CreateWindowEX Lib "user32" Alias "CreateWindowExA"
 (ByVal dwExStyle As Long, ByVal lpClassName As String, ByVal lpWindowName As
 String, ByVal dwStyle As Long, ByVal x As Long, ByVal y As Long, ByVal nWidth
 As Long, ByVal nHeight As Long, ByVal hWndParent As Long, ByVal hMenu As Long,
 ByVal hInstance As Long, lpParam As Any) As Long
Private Declare Function DestroyWindow Lib "user32" (ByVal hwnd As Long) As Long
Private Declare Function SystemParametersInfo Lib "user32" Alias
 "SystemParametersInfoA" (ByVal uAction As Long, ByVal uParam As Long, ByRef
 lpvParam As Any, ByVal fuWinIni As Long) As Long
Private Declare Function UpdateLayeredWindow Lib "user32" ( _
    ByVal hwnd As Long, _
    ByVal hdcDst As Long, _
    pptDst As Any, _
    psize As Any, _
    ByVal hdcSrc As Long, _
    pptSrc As Any, _
    ByVal crKey As Long, _
    pblend As BLENDFUNCTION, _
    ByVal dwFlags As Long) As Long
Private Const GWL_STYLE = (-16)
Private Const GWL_EXSTYLE = (-20)
Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA"
 (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long

Private Declare Function IsWindowVisible Lib "user32" (ByVal hwnd As Long) As
 Long
Private Declare Function GetWindowRect Lib "user32" (ByVal hwnd As Long, lpRect
 As RECT) As Long

' Set the position of a window:
Private Const HWND_NOTOPMOST = -2
Private Const HWND_TOP = 0
Private Const HWND_TOPMOST = -1
Private Const HWND_BOTTOM = 1
Private Const SWP_NOACTIVATE = &amp;H10
Private Const SWP_SHOWWINDOW = &amp;H40
Private Declare Function SetWindowPos Lib "user32" (ByVal hwnd As Long, ByVal
 hWndInsertAfter As Long, ByVal x As Long, ByVal y As Long, ByVal cx As Long,
 ByVal cy As Long, ByVal wFlags As Long) As Long

Private Declare Function CreateDCAsNull Lib "gdi32" Alias "CreateDCA" (ByVal
 lpDriverName As String, lpDeviceName As Any, lpOutput As Any, lpInitData As
 Any) As Long
Private Declare Function DeleteDC Lib "gdi32" (ByVal hdc As Long) As Long

Private Declare Function LoadImageString Lib "user32" Alias "LoadImageA" (ByVal
 hInst As Long, ByVal lpsz As String, ByVal uType As Long, ByVal cx As Long,
 ByVal cy As Long, ByVal uFlags As Long) As Long
Private Const IMAGE_BITMAP = 0
Private Const IMAGE_ICON = 1
Private Const IMAGE_CURSOR = 2
Private Const LR_LOADFROMFILE = &amp;H10
Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As
 Long

Private Declare Function BitBlt Lib "gdi32" (ByVal hDestDC As Long, ByVal x As
 Long, ByVal y As Long, ByVal nWidth As Long, ByVal nHeight As Long, ByVal
 hSrcDC As Long, ByVal xSrc As Long, ByVal ySrc As Long, ByVal dwRop As Long)
 As Long

Private Declare Function SelectObject Lib "gdi32" (ByVal hdc As Long, ByVal
 hObject As Long) As Long
Private Declare Function LoadImage Lib "user32" Alias "LoadImageA" (ByVal hInst
 As Long, ByVal lpsz As String, ByVal un1 As Long, ByVal n1 As Long, ByVal n2
 As Long, ByVal un2 As Long) As Long
Private Const BI_RGB = 0&amp;
Private Const BI_RLE4 = 2&amp;
Private Const BI_RLE8 = 1&amp;
Private Const DIB_RGB_COLORS = 0 '  color table in RGBs

Private Type BITMAP
    bmType As Long
    bmWidth As Long
    bmHeight As Long
    bmWidthBytes As Long
    bmPlanes As Integer
    bmBitsPixel As Integer
    bmBits As Long
End Type
Private Declare Function GetObjectAPI Lib "gdi32" Alias "GetObjectA" (ByVal
 hObject As Long, ByVal nCount As Long, lpObject As Any) As Long

' Start of structure:
Private Const BITMAPTYPE As Integer = &amp;H4D42
Private Type BITMAPFILEHEADER
   bfType As Integer '- type  ="BM" i.e &amp;H4D42 - 2
   bfSize As Long ' - size in bytes of file - 6
   bfReserved1 As Integer ' - reserved, must be 0 - 8
   bfReserved2 As Integer ' - reserved, must be 0 - 10
   bfOffBits As Long ' offset from this structure to the bitmap bits - 14
End Type

Private Const WM_DESTROY = &amp;H2
Private Const WM_SIZE = &amp;H5
Private Const WM_SIZING = &amp;H214
Private Const WM_MOVING = &amp;H216&amp;
Private Const WM_ENTERSIZEMOVE = &amp;H231&amp;
Private Const WM_EXITSIZEMOVE = &amp;H232&amp;

Private m_hDIb As Long
Private m_hBmpOld As Long
Private m_hDC As Long
Private m_hDD As Long
Private m_lPtr As Long
Private m_tBI As BITMAPINFO

Public Enum EShadowType
   RightShadow
   BottomShadow
End Enum

Private m_eShadowType As EShadowType
Private m_oShadow As Object
Private m_lShadowSize As Long
Private m_hWndAttach As Long
Private m_bInSizeMove As Boolean

Implements ISubclass

Public Property Get ShadowSize() As Long
   ShadowSize = m_lShadowSize
End Property
Public Property Let ShadowSize(ByVal lSize As Long)
   m_lShadowSize = lSize
End Property

Public Property Get Shadow() As Object
   Set Shadow = m_oShadow
End Property

Public Property Let Shadow(ByVal oShadow As Object)
   Set m_oShadow = oShadow
End Property
Public Property Set Shadow(ByVal oShadow As Object)
   Set m_oShadow = oShadow
End Property

Public Property Get ShadowType() As EShadowType
   ShadowType = m_eShadowType
End Property

Public Property Let ShadowType(ByVal value As EShadowType)
   m_eShadowType = value
End Property



Private Function CreateDIB( _
        ByVal lhDC As Long, _
        ByVal lWidth As Long, _
        ByVal lHeight As Long, _
        ByRef hDib As Long _
    ) As Boolean
    With m_tBI.bmiHeader
        .biSize = Len(m_tBI.bmiHeader)
        .biWidth = lWidth
        .biHeight = lHeight
        .biPlanes = 1
        .biBitCount = 32
        .biCompression = BI_RGB
        .biSizeImage = BytesPerScanLine * .biHeight
    End With
    hDib = CreateDIBSection( _
            lhDC, _
            m_tBI, _
            DIB_RGB_COLORS, _
            m_lPtr, _
            0, 0)
    CreateDIB = (hDib &lt;&gt; 0)
End Function

Private Function Create( _
        ByVal lWidth As Long, _
        ByVal lHeight As Long _
    ) As Boolean
   ClearUp
   m_hDC = CreateCompatibleDC(0)
   If (m_hDC &lt;&gt; 0) Then
       If (CreateDIB(m_hDC, lWidth, lHeight, m_hDIb)) Then
           m_hBmpOld = SelectObject(m_hDC, m_hDIb)
           Create = True
       Else
           DeleteObject m_hDC
           m_hDC = 0
       End If
   End If
End Function
Private Sub ClearUp()
    If (m_hDC &lt;&gt; 0) Then
        If (m_hDIb &lt;&gt; 0) Then
            SelectObject m_hDC, m_hBmpOld
            DeleteObject m_hDIb
        End If
        DeleteObject m_hDC
    End If
    m_hDC = 0: m_hDIb = 0: m_hBmpOld = 0: m_lPtr = 0
End Sub

Private Property Get BytesPerScanLine() As Long
    ' Scans must align on dword boundaries; easy with an alpha bitmap!
    BytesPerScanLine = m_tBI.bmiHeader.biWidth * 4
End Property

Private Property Get DibWidth() As Long
    DibWidth = m_tBI.bmiHeader.biWidth
End Property
Private Property Get DibHeight() As Long
    DibHeight = m_tBI.bmiHeader.biHeight
End Property

Private Sub CreateDropShadow( _
      ByVal bHorizontal As Boolean, _
      ByVal bLeftTop As Boolean _
   )
Dim bDib() As Byte
Dim x As Long, y As Long
Dim lC As Long, lInitC As Long, lSize As Long
Dim tSA As SAFEARRAY2D
    
    ' Get the bits in the from DIB section:
    With tSA
        .cbElements = 1
        .cDims = 2
        .Bounds(0).lLbound = 0
        .Bounds(0).cElements = m_tBI.bmiHeader.biHeight
        .Bounds(1).lLbound = 0
        .Bounds(1).cElements = BytesPerScanLine()
        .pvData = m_lPtr
    End With
    CopyMemory ByVal VarPtrArray(bDib()), VarPtr(tSA), 4
    
   If (bHorizontal) Then
      ' horizontal:
      lSize = DibHeight
      If (bLeftTop) Then
      
      Else
         ' at bottom:
         For x = 0 To BytesPerScanLine - 1 Step 4
            ' if x is within lSize of the start or end, then
            ' it contributes to alpha:
            
            If (x &lt; lSize * 4) Then
               lInitC = (255 * x) \ (lSize * 4)
            ElseIf (x &gt;= (BytesPerScanLine - lSize * 4)) Then
               lInitC = (((BytesPerScanLine - x) * 255) \ (4 * lSize))
            Else
               lInitC = 255
            End If
            
            For y = 0 To DibHeight - 1
               lC = (lInitC * y) \ DibHeight
               bDib(x + 3, y) = lC
               bDib(x + 2, y) = 0
               bDib(x + 1, y) = 0
               bDib(x, y) = 0
            Next y
            
         Next x
      End If
   Else
      ' vertical:
      lSize = BytesPerScanLine \ 4
      If (bLeftTop) Then
      Else
         ' at right:
         For y = 0 To DibHeight - 1
            ' if y is within lSize of the start or end, then
            ' it contributes to the alpha:
            
            'If (y &lt; lSize) Then
            '   lInitC = (255 * y) \ lSize
            If (y &gt;= (DibHeight - lSize)) Then
               lInitC = (255 * (DibHeight - y)) \ lSize
            Else
               lInitC = 255
            End If
            
            For x = 0 To BytesPerScanLine - 1 Step 4
               ' the amount of alpha depends on how far we are from the left:
               lC = (lInitC * (BytesPerScanLine - x)) \ BytesPerScanLine
               'Debug.Print lC
               bDib(x + 3, y) = lC
               bDib(x + 2, y) = 0
               bDib(x + 1, y) = 0
               bDib(x, y) = 0
            Next x
         Next y
      End If
   End If
    
    ' Clear the temporary array descriptor
    ' (This does not appear to be necessary, but
    ' for safety do it anyway)
    CopyMemory ByVal VarPtrArray(bDib), 0&amp;, 4
    
End Sub

Private Sub Form_Initialize()
   m_lShadowSize = 5
End Sub

Private Sub Form_Load()
   
   ' set as shadow form:
   Dim lExStyle As Long
   Dim lStyle As Long
   lExStyle = WS_EX_LAYERED Or WS_EX_TRANSPARENT Or WS_EX_TOPMOST Or
    WS_EX_TOOLWINDOW
   lStyle = WS_POPUP Or WS_VISIBLE
   SetWindowLong Me.hwnd, GWL_STYLE, lStyle
   SetWindowLong Me.hwnd, GWL_EXSTYLE, lExStyle
   
   ' Setup size:
   Dim tR As RECT
   GetWindowRect m_oShadow.hwnd, tR
      
   renderShadow tR, True
   
   ' Subclass:
   Attach
   
End Sub

Private Sub renderShadow(tR As RECT, ByVal bChange As Boolean)
   
   If bChange Then
      Dim tSize As SIZEAPI
      If (m_eShadowType = BottomShadow) Then
         tSize.cx = (tR.Right - tR.Left)
         tSize.cy = m_lShadowSize
      Else
         tSize.cx = m_lShadowSize
         tSize.cy = (tR.Bottom - tR.Top) - m_lShadowSize
      End If
      Create tSize.cx, tSize.cy
      If (m_eShadowType = BottomShadow) Then
         CreateDropShadow True, False
      Else
         CreateDropShadow False, False
      End If
      
      ' Draw:
      Dim tBlend As BLENDFUNCTION
      tBlend.BlendOp = AC_SRC_OVER
      tBlend.BlendFlags = 0
      tBlend.AlphaFormat = AC_SRC_ALPHA
      tBlend.SourceConstantAlpha = 96
      Dim tPtSrc As POINTAPI
      tPtSrc.x = 0
      tPtSrc.y = 0
      
      UpdateLayeredWindow Me.hwnd, ByVal 0&amp;, ByVal 0&amp;, tSize, m_hDC, tPtSrc, 0,
       _
               tBlend, ULW_ALPHA
   End If
   
   If (m_eShadowType = RightShadow) Then
      On Error Resume Next
      Me.Move _
         tR.Right * Screen.TwipsPerPixelX, _
         (tR.Top + m_lShadowSize) * Screen.TwipsPerPixelY, _
         m_lShadowSize * Screen.TwipsPerPixelX, _
         (tR.Bottom - tR.Top) * Screen.TwipsPerPixelY
   Else
      On Error Resume Next
      Me.Move _
         (tR.Left + m_lShadowSize) * Screen.TwipsPerPixelX, _
         tR.Bottom * Screen.TwipsPerPixelY, _
         (tR.Right - tR.Left) * Screen.TwipsPerPixelX, _
         m_lShadowSize * Screen.TwipsPerPixelY
   End If
End Sub


Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
   ClearUp
   Detach
End Sub

Private Sub Attach()
   Detach
   On Error Resume Next
   m_hWndAttach = m_oShadow.Parent.hwnd
   If Not (Err.Number = 0) Then
      m_hWndAttach = m_oShadow.hwnd
   End If
   On Error GoTo 0
   AttachMessage Me, m_hWndAttach, WM_DESTROY
   AttachMessage Me, m_hWndAttach, WM_MOVING
   AttachMessage Me, m_hWndAttach, WM_SIZING
   AttachMessage Me, m_hWndAttach, WM_SIZE
End Sub
Private Sub Detach()
   If Not m_hWndAttach = 0 Then
      DetachMessage Me, m_hWndAttach, WM_DESTROY
      DetachMessage Me, m_hWndAttach, WM_MOVING
      DetachMessage Me, m_hWndAttach, WM_SIZING
      DetachMessage Me, m_hWndAttach, WM_SIZE
   End If
End Sub


Private Property Let ISubClass_MsgResponse(ByVal RHS As EMsgResponse)
   '
End Property

Private Property Get ISubClass_MsgResponse() As EMsgResponse
   ' Process before windows:
   ISubClass_MsgResponse = emrPostProcess
End Property

Private Function ISubClass_WindowProc(ByVal hwnd As Long, ByVal iMsg As Long,
 ByVal wParam As Long, ByVal lParam As Long) As Long
Dim lR As Long
Dim tR As RECT
Dim lWidth As Long
Dim lHeight As Long

   Select Case iMsg
   Case WM_MOVING
      ' Form is moving:
      If (m_oShadow.hwnd = m_hWndAttach) Then
         CopyMemory tR, ByVal lParam, Len(tR)
         renderShadow tR, False
      Else
         GetWindowRect m_oShadow.hwnd, tR
         renderShadow tR, False
      End If
      
   Case WM_SIZING
      ' Form is sizing:
      If (m_oShadow.hwnd = m_hWndAttach) Then
         CopyMemory tR, ByVal lParam, Len(tR)
         renderShadow tR, True
      Else
         GetWindowRect m_oShadow.hwnd, tR
         renderShadow tR, False
      End If
      
   Case WM_SIZE
      ' This ensures that you don't have to separately
      ' check for maximize/minimize/restore/in code
      ' movement, as these aren't
      ' controlled by the UI sizing loop:
      If Not (m_bInSizeMove) Or Not (m_oShadow.hwnd = m_hWndAttach) Then
         GetWindowRect m_oShadow.hwnd, tR
         lWidth = tR.Right - tR.Left
         lHeight = tR.Bottom - tR.Top
         renderShadow tR, True
      End If
      
   Case WM_ENTERSIZEMOVE
      ' Enter modal sizing/moving loop
      m_bInSizeMove = True
   
   Case WM_EXITSIZEMOVE
      m_bInSizeMove = False
      
   Case WM_DESTROY
      ' Ensures the class terminates regardless
      ' of whether the user explicitly detaches
      ' it or not:
      Detach
      
   End Select
   
End Function

</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Graphics and GDI</a>&#160;.&#160;<a href="article.html">Creating Drop-Shadows</a>&#160;.&#160;<a href="VB6_Drop_Shadow_Code.html">VB6 Drop Shadow Code</a>&#160;.&#160;fDropShadow.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Graphics_and_GDI/Drop_Shadows/VB6_Drop_Shadow_Code_zip_fDropShadow_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:47 GMT -->
</html>