<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Subclassing/SSubTimer/bugTrak17-1.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:41 GMT -->
<head>
<title>vbAccelerator - Provide support for subclassing Unicode Windows</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="Prior to VB5, it was impossible to subclass a control without relying on a proprietary 
custom control. This greatly restricted what you could do if you wanted to play around with 
some of the neater customisation features - creating a new control was completely out, finding out 
in detail what was going on with menus was impossible and so on.The introduction of the AddressOf operator to VB5 now allows you to subclass, although 
it is not nearly as simple as it ought to be.The SSubTmr component is a more stable and consistent way of working with subclassing,
without some of the attendent difficulties. You can either use it as an external DLL to provide, or 
by including one module and one interface class, you can compile it directly into your application 
once you are happy the subclassing is working. SSubTmr is the basis for most of the VB controls
on this site.The component itself mostly uses the SubTimer.DLL component code from 
Bruce McKinney's excellent 'Hardcore Visual Basic' book, however it has a couple of 
very useful enhancements described in this article." /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Subclassing</a>&#160;.&#160;<a href="article.html">Subclassing Without The Crashes</a>&#160;.&#160;<a href="bugTrak.html">BugTrak Summary</a>&#160;.&#160;Provide support for subclassing Unicode Windows</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Graduated_Title_Bar_Sample.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Graduated Title Bar Sample</a> (44K)</p><p class="nav"><a href="VB5_Minimum_Form_Size_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Minimum Form Size Demonstration</a> (9K)</p><p class="nav"><a href="VB5_SSubTmr_Binary.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 SSubTmr Binary</a> (11K)</p><p class="nav"><a href="VB5_SSubTmr_Source_Code.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 SSubTmr Source Code</a> (34K)</p><p /><p class="nav"><a href="VB6_Graduated_Title_Bar_Sample.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Graduated Title Bar Sample</a> (38K)</p><p class="nav"><a href="VB6_Minimum_Form_Size_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Minimum Form Size Demonstration</a> (8K)</p><p class="nav"><a href="VB6_SSubTmr_Binary.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 SSubTmr Binary</a> (11K)</p><p class="nav"><a href="VB6_SSubTmr_Source_Code.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 SSubTmr Source Code</a> (41K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Article:17</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=17&amp;type=Article&amp;title=Subclassing%20Without%20The%20Crashes">Link to this page</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" />﻿<p class="nav"><a href="bugTrak.html">BugTrak</a></p><p class="nav"><a href="bugTrak.html#bugs"><img src="../../../../../res/btBug.png" width="16" height="16" alt="Bug" />&#160;Bugs:</a> 0 / 0</p><p class="nav"><a href="bugTrak.html#issues"><img src="../../../../../res/btIssue.png" width="16" height="16" alt="Issue" />&#160;Issues:</a> 1 / 1</p><p class="nav"><a href="bugTrak.html#questions"><img src="../../../../../res/btQuestion.png" width="16" height="16" alt="Question" />&#160;Questions:</a> 0 / 0</a></p><p class="nav">Updated:14 April 2003</p>
<br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />26 Jan 2003<br /><p class="update">Fixed problem with multiple attach/detach calls, 
and added automatic detach for the <i>WM_DESTROY</i>
message.</p><p class="update">Refactored code for clarity.</p></p><p class="update"><a href="updates.html">Update History &gt;</a></p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>Provide support for subclassing Unicode Windows</h1><h2>Summary</h2><table class="article"><tr><td>Id:</td><td>17.1</td></tr><tr><td>Type:</td><td><img src="../../../../../res/btIssue.png" width="16" height="16" alt="Issue" />&#160;Issue</td></tr><tr><td>Current Status:</td><td>Open</td></tr></table><h2>Detail</h2><table class="article"><tr class="header"><td>14&#160;Apr&#160;2003</td><td>Open</td><td width="100%">Matt Funnell</td></tr><tr><td colspan="3"><p>First of all, many thanks for the great code on your site - has been very, very useful..........</p><p>I came across a small problem when trying to subclass the internet explorer windows which seem to be Unicode windows.  I've made some small changes to the recent code from your site and have attached below if interested.</p><p>Thanks again for your great site and your efforts.</p><pre> 
' declares:
Private Declare Function IsWindow Lib "user32" ( _
    ByVal hWnd As Long) As Long
Private Declare Function IsWindowUnicode Lib "user32" ( _
    ByVal hWnd As Long) As Long
 
Private Declare Function GetProp Lib "user32" Alias "GetPropA" ( _
    ByVal hWnd As Long, ByVal lpString As String) As Long
Private Declare Function SetProp Lib "user32" Alias "SetPropA" ( _
    ByVal hWnd As Long, ByVal lpString As String, ByVal hData As Long) As Long
Private Declare Function RemoveProp Lib "user32" Alias "RemovePropA" ( _
    ByVal hWnd As Long, ByVal lpString As String) As Long
Private Declare Function GetPropW Lib "user32" ( _
    ByVal hWnd As Long, ByVal lpString As Long) As Long
Private Declare Function SetPropW Lib "user32" ( _
    ByVal hWnd As Long, ByVal lpString As Long, ByVal hData As Long) As Long
Private Declare Function RemovePropW Lib "user32" ( _
    ByVal hWnd As Long, ByVal lpString As Long) As Long
 
Private Declare Function CallWindowProc Lib "user32" _
Alias "CallWindowProcA" ( _
ByVal lpPrevWndFunc As Long, ByVal hWnd As Long, _
ByVal Msg As Long, _
    ByVal wParam As Long, ByVal lParam As Long) As Long
Private Declare Function CallWindowProcW Lib "user32" ( _
ByVal lpPrevWndFunc As Long, ByVal hWnd As Long, _
ByVal Msg As Long, _
    ByVal wParam As Long, ByVal lParam As Long) As Long
 
Private Declare Function SetWindowLong Lib "user32" _
Alias "SetWindowLongA" ( _
    ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function GetWindowLong Lib "user32" _
Alias "GetWindowLongA" ( _
    ByVal hWnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLongW Lib "user32" ( _
ByVal hWnd As Long, ByVal nIndex As Long, _
    ByVal dwNewLong As Long) As Long
Private Declare Function GetWindowLongW Lib "user32" ( _
    ByVal hWnd As Long, ByVal nIndex As Long) As Long
 
Private Declare Function GetWindowThreadProcessId Lib "user32" ( _
    ByVal hWnd As Long, lpdwProcessId As Long) As Long
Private Declare Function GetCurrentProcessId Lib "kernel32" () As Long
 
Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
    lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)
 
Private Const GWL_WNDPROC = (-4)
Private Const WM_DESTROY = &amp;H2
 
' SubTimer is independent of VBCore, so it hard codes error handling
 
Public Enum EErrorWindowProc
    eeBaseWindowProc = 13080 ' WindowProc
    eeCantSubclass           ' Can't subclass window
    eeAlreadyAttached        ' Message already handled by another class
    eeInvalidWindow          ' Invalid window
    eeNoExternalWindow       ' Can't modify external window
End Enum
 
Private m_iCurrentMessage As Long
Private m_iProcOld As Long
Private m_f As Long
 
 
Public Property Get CurrentMessage() As Long
   CurrentMessage = m_iCurrentMessage
End Property
 
Private Sub ErrRaise(e As Long)
Dim sText As String, sSource As String
   If e &gt; 1000 Then
      sSource = App.EXEName &amp; ".WindowProc"
      Select Case e
      Case eeCantSubclass
         sText = "Can't subclass window"
      Case eeAlreadyAttached
         sText = "Message already handled by another class"
      Case eeInvalidWindow
         sText = "Invalid window"
      Case eeNoExternalWindow
         sText = "Can't modify external window"
      End Select
      Err.Raise e Or vbObjectError, sSource, sText
   Else
      ' Raise standard Visual Basic error
      Err.Raise e, sSource
   End If
End Sub
 
Private Function SetWindowProc(ByVal hWnd As Long, ByVal lpFn As Long) As Long
   If (IsWindowUnicode(hWnd) = 0) Then
      ' Not a Unicode window:
      SetWindowProc = SetWindowLong(hWnd, GWL_WNDPROC, AddressOf WindowProc)
   Else
      ' Unicode window:
      SetWindowProc = SetWindowLongW(hWnd, GWL_WNDPROC, AddressOf WindowProc)
   End If
 
End Function
 
Private Property Get MessageCount(ByVal hWnd As Long) As Long
Dim sName As String
   sName = "C" &amp; hWnd
   MessageCount = GetProp(hWnd, sName)
End Property
Private Property Let MessageCount(ByVal hWnd As Long, ByVal count As Long)
Dim sName As String
Dim hData As Long
   m_f = 1
   sName = "C" &amp; hWnd
   m_f = SetProp(hWnd, sName, count)
   If (count = 0) Then
      hData = RemoveProp(hWnd, sName)
   End If
   logMessage "Changed message count for " &amp; Hex(hWnd) &amp; " to " &amp; count
End Property
 
Private Property Get OldWindowProc(ByVal hWnd As Long) As Long
Dim sName As String
   sName = hWnd
   OldWindowProc = GetProp(hWnd, sName)
End Property
Private Property Let OldWindowProc(ByVal hWnd As Long, ByVal lPtr As Long)
Dim sName As String
Dim hData As Long
   m_f = 1
   sName = hWnd
   m_f = SetProp(hWnd, sName, lPtr)
   If (lPtr = 0) Then
      hData = RemoveProp(hWnd, sName)
   End If
   logMessage "Changed Window Proc for " &amp; Hex(hWnd) &amp; " to " &amp; Hex(lPtr)
End Property
 
Private Property Get MessageClassCount( _
    ByVal hWnd As Long, ByVal iMsg As Long) As Long
Dim sName As String
   sName = hWnd &amp; "#" &amp; iMsg &amp; "C"
   MessageClassCount = GetProp(hWnd, sName)
End Property
 
Private Property Let MessageClassCount( _
    ByVal hWnd As Long, ByVal iMsg As Long, ByVal count As Long)
Dim sName As String
Dim hData As Long
   sName = hWnd &amp; "#" &amp; iMsg &amp; "C"
   m_f = SetProp(hWnd, sName, count)
   If (count = 0) Then
      hData = RemoveProp(hWnd, sName)
   End If
   logMessage "Changed message count for " &amp; Hex(hWnd) &amp; _
        " Message " &amp; iMsg &amp; " to " &amp; count
End Property
 
Private Property Get MessageClass( _
ByVal hWnd As Long, ByVal iMsg As Long, _
    ByVal index As Long) As Long
Dim sName As String
   sName = hWnd &amp; "#" &amp; iMsg &amp; "#" &amp; index
   MessageClass = GetProp(hWnd, sName)
End Property
Private Property Let MessageClass( _
ByVal hWnd As Long, ByVal iMsg As Long, _
    ByVal index As Long, ByVal classPtr As Long)
Dim sName As String
Dim hData As Long
   sName = hWnd &amp; "#" &amp; iMsg &amp; "#" &amp; index
   m_f = SetProp(hWnd, sName, classPtr)
   If (classPtr = 0) Then
      hData = RemoveProp(hWnd, sName)
   End If
   logMessage "Changed message class for " &amp; Hex(hWnd) &amp; _
     " Message " &amp; iMsg &amp; " Index " &amp; index &amp; " to " &amp; Hex(classPtr)
End Property
 
Sub AttachMessage( _
      iwp As ISubclass, _
      ByVal hWnd As Long, _
      ByVal iMsg As Long _
   )
Dim procOld As Long
Dim msgCount As Long
Dim msgClassCount As Long
Dim msgClass As Long
    
   ' --------------------------------------------------------------------
   ' 1) Validate window
   ' --------------------------------------------------------------------
   If IsWindow(hWnd) = False Then
      ErrRaise eeInvalidWindow
      Exit Sub
   End If
   If IsWindowLocal(hWnd) = False Then
      ErrRaise eeNoExternalWindow
      Exit Sub
   End If
 
   ' --------------------------------------------------------------------
   ' 2) Check if this class is already attached for this message:
   ' --------------------------------------------------------------------
   msgClassCount = MessageClassCount(hWnd, iMsg)
   If (msgClassCount &gt; 0) Then
      For msgClass = 1 To msgClassCount
         If (MessageClass(hWnd, iMsg, msgClass) = ObjPtr(iwp)) Then
            ErrRaise eeAlreadyAttached
            Exit Sub
         End If
      Next msgClass
   End If
 
   ' --------------------------------------------------------------------
   ' 3) Associate this class with this message for this window:
   ' --------------------------------------------------------------------
   MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) + 1
   If (m_f = 0) Then
      ' Failed, out of memory:
      ErrRaise 5
      Exit Sub
   End If
   
   ' --------------------------------------------------------------------
   ' 4) Associate the class pointer:
   ' --------------------------------------------------------------------
   MessageClass(hWnd, iMsg, MessageClassCount(hWnd, iMsg)) = ObjPtr(iwp)
   If (m_f = 0) Then
      ' Failed, out of memory:
      MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) - 1
      ErrRaise 5
      Exit Sub
   End If
 
   ' --------------------------------------------------------------------
   ' 5) Get the message count
   ' --------------------------------------------------------------------
   msgCount = MessageCount(hWnd)
   If msgCount = 0 Then
      
      ' Subclass window by installing window procedure
      procOld = SetWindowProc(hWnd, AddressOf WindowProc)
      If procOld = 0 Then
         ' remove class:
         MessageClass(hWnd, iMsg, MessageClassCount(hWnd, iMsg)) = 0
         ' remove class count:
         MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) - 1
         
         ErrRaise eeCantSubclass
         Exit Sub
      End If
      
      ' Associate old procedure with handle
      OldWindowProc(hWnd) = procOld
      If m_f = 0 Then
         ' SPM: Failed to VBSetProp, windows properties database problem.
         ' Has to be out of memory.
         
         ' Put the old window proc back again:
         SetWindowProc hWnd, procOld
         ' remove class:
         MessageClass(hWnd, iMsg, MessageClassCount(hWnd, iMsg)) = 0
         ' remove class count:
         MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) - 1
         
         ' Raise an error:
         ErrRaise 5
         Exit Sub
      End If
   End If
   
      
   ' Count this message
   MessageCount(hWnd) = MessageCount(hWnd) + 1
   If m_f = 0 Then
      ' SPM: Failed to set prop, windows properties database problem.
      ' Has to be out of memory
      
      ' remove class:
      MessageClass(hWnd, iMsg, MessageClassCount(hWnd, iMsg)) = 0
      ' remove class count contribution:
      MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) - 1
      
      ' If we haven't any messages on this window then remove the subclass:
      If (MessageCount(hWnd) = 0) Then
         ' put old window proc back again:
         procOld = OldWindowProc(hWnd)
         If Not (procOld = 0) Then
            SetWindowProc hWnd, procOld
            OldWindowProc(hWnd) = 0
         End If
      End If
      
      ' Raise the error:
      ErrRaise 5
      Exit Sub
   End If
       
End Sub
 
Sub DetachMessage( _
      iwp As ISubclass, _
      ByVal hWnd As Long, _
      ByVal iMsg As Long _
   )
Dim msgClassCount As Long
Dim msgClass As Long
Dim msgClassIndex As Long
Dim msgCount As Long
Dim procOld As Long
    
   ' --------------------------------------------------------------------
   ' 1) Validate window
   ' --------------------------------------------------------------------
   If IsWindow(hWnd) = False Then
      ' for compatibility with the old version, we don't
      ' raise a message:
      ' ErrRaise eeInvalidWindow
      Exit Sub
   End If
   If IsWindowLocal(hWnd) = False Then
      ' for compatibility with the old version, we don't
      ' raise a message:
      ' ErrRaise eeNoExternalWindow
      Exit Sub
   End If
    
   ' --------------------------------------------------------------------
   ' 2) Check if this message is attached for this class:
   ' --------------------------------------------------------------------
   msgClassCount = MessageClassCount(hWnd, iMsg)
   If (msgClassCount &gt; 0) Then
      msgClassIndex = 0
      For msgClass = 1 To msgClassCount
         If (MessageClass(hWnd, iMsg, msgClass) = ObjPtr(iwp)) Then
            msgClassIndex = msgClass
            Exit For
         End If
      Next msgClass
      
      If (msgClassIndex = 0) Then
         ' fail silently
         Exit Sub
      Else
         ' remove this message class:
         
         ' a) Anything above this index has to be shifted up:
         For msgClass = msgClassIndex To msgClassCount - 1
            MessageClass(hWnd, iMsg, msgClass) = MessageClass(hWnd, iMsg, msgClass + 1)
         Next msgClass
         
         ' b) The message class at the end can be removed:
         MessageClass(hWnd, iMsg, msgClassCount) = 0
         
         ' c) Reduce the message class count:
         MessageClassCount(hWnd, iMsg) = MessageClassCount(hWnd, iMsg) - 1
         
      End If
      
   Else
      ' fail silently
      Exit Sub
   End If
   
   ' ---------------------------------------------------------------------
   ' 3) Reduce the message count:
   ' ---------------------------------------------------------------------
   msgCount = MessageCount(hWnd)
   If (msgCount = 1) Then
      ' remove the subclass:
      procOld = OldWindowProc(hWnd)
      If Not (procOld = 0) Then
         ' Unsubclass by reassigning old window procedure
         SetWindowProc hWnd, procOld
      End If
      ' remove the old window proc:
      OldWindowProc(hWnd) = 0
   End If
   MessageCount(hWnd) = MessageCount(hWnd) - 1
   
End Sub
 
Private Function WindowProc( _
      ByVal hWnd As Long, _
      ByVal iMsg As Long, _
      ByVal wParam As Long, _
      ByVal lParam As Long _
   ) As Long
   
Dim procOld As Long
Dim msgClassCount As Long
Dim bCalled As Boolean
Dim pSubClass As Long
Dim iwp As ISubclass
Dim iwpT As ISubclass
Dim iIndex As Long
Dim bDestroy As Boolean
    
   ' Get the old procedure from the window
   procOld = OldWindowProc(hWnd)
   Debug.Assert procOld &lt;&gt; 0
    
   If (procOld = 0) Then
      ' we can't work, we're not subclassed properly.
      Exit Function
   End If
    
   ' SPM - in this version I am allowing more than one class to
   ' make a subclass to the same hWnd and Msg.  Why am I doing
   ' this?  Well say the class in question is a control, and it
   ' wants to subclass its container.  In this case, we want
   ' all instances of the control on the form to receive the
   ' form notification message.
    
   ' Get the number of instances for this msg/hwnd:
   bCalled = False
   
   If (MessageClassCount(hWnd, iMsg) &gt; 0) Then
      iIndex = MessageClassCount(hWnd, iMsg)
      
      Do While (iIndex &gt;= 1)
         pSubClass = MessageClass(hWnd, iMsg, iIndex)
         
         If (pSubClass = 0) Then
            ' Not handled by this instance
         Else
            ' Turn pointer into a reference:
            CopyMemory iwpT, pSubClass, 4
            Set iwp = iwpT
            CopyMemory iwpT, 0&amp;, 4
            
            ' Store the current message, so the client can check it:
            m_iCurrentMessage = iMsg
            
            With iwp
               ' Preprocess (only checked first time around):
               If (iIndex = 1) Then
                  If (.MsgResponse = emrPreprocess) Then
                     If Not (bCalled) Then
                        If (IsWindowUnicode(hWnd) = 0) Then
                           WindowProc = CallWindowProc(procOld, hWnd, iMsg, _
                                                  wParam, ByVal lParam)
                        Else
                           WindowProc = CallWindowProcW(procOld, hWnd, iMsg, _
                                                  wParam, ByVal lParam)
                        End If
                        bCalled = True
                     End If
                  End If
               End If
               ' Consume (this message is always passed to all control
               ' instances regardless of whether any single one of them
               ' requests to consume it):
               WindowProc = .WindowProc(hWnd, iMsg, wParam, ByVal lParam)
            End With
         End If
         
         iIndex = iIndex - 1
      Loop
      
      ' PostProcess (only check this the last time around):
      If Not (iwp Is Nothing) And Not (procOld = 0) Then
         If iwp.MsgResponse = emrPostProcess Then
            If Not (bCalled) Then
               If (IsWindowUnicode(hWnd) = 0) Then
                  WindowProc = CallWindowProc(procOld, hWnd, iMsg, _
                                         wParam, ByVal lParam)
               Else
                  WindowProc = CallWindowProcW(procOld, hWnd, iMsg, _
                                         wParam, ByVal lParam)
               End If
               bCalled = True
            End If
         End If
      End If
            
   Else
      ' Not handled:
      If (iMsg = WM_DESTROY) Then
         ' If WM_DESTROY isn't handled already, we should
         ' clear up any subclass
         pClearUp hWnd
         If (IsWindowUnicode(hWnd) = 0) Then
            WindowProc = CallWindowProc(procOld, hWnd, iMsg, _
                                    wParam, ByVal lParam)
         Else
            WindowProc = CallWindowProcW(procOld, hWnd, iMsg, _
                                    wParam, ByVal lParam)
         End If
      Else
         If (IsWindowUnicode(hWnd) = 0) Then
            WindowProc = CallWindowProc(procOld, hWnd, iMsg, _
                                    wParam, ByVal lParam)
         Else
            WindowProc = CallWindowProcW(procOld, hWnd, iMsg, _
                                    wParam, ByVal lParam)
         End If
      End If
   End If
    
End Function
Public Function CallOldWindowProc( _
      ByVal hWnd As Long, _
      ByVal iMsg As Long, _
      ByVal wParam As Long, _
      ByVal lParam As Long _
   ) As Long
Dim iProcOld As Long
   iProcOld = OldWindowProc(hWnd)
   If Not (iProcOld = 0) Then
      CallOldWindowProc = CallWindowProc(iProcOld, hWnd, iMsg, wParam, lParam)
   End If
End Function
 
Function IsWindowLocal(ByVal hWnd As Long) As Boolean
    Dim idWnd As Long
    Call GetWindowThreadProcessId(hWnd, idWnd)
    IsWindowLocal = (idWnd = GetCurrentProcessId())
End Function
 
Private Sub logMessage(ByVal sMsg As String)
   Debug.Print sMsg
End Sub
 
 
Private Sub pClearUp(ByVal hWnd As Long)
Dim msgCount As Long
Dim procOld As Long
   ' this is only called if you haven't explicitly cleared up
   ' your subclass from the caller.  You will get a minor
   ' resource leak as it does not clear up any message
   ' specific properties.
   msgCount = MessageCount(hWnd)
   If (msgCount &gt; 0) Then
      ' remove the subclass:
      procOld = OldWindowProc(hWnd)
      If Not (procOld = 0) Then
         ' Unsubclass by reassigning old window procedure
         SetWindowProc hWnd, procOld
      End If
      ' remove the old window proc:
      OldWindowProc(hWnd) = 0
      MessageCount(hWnd) = 0
   End If
End Sub
</pre></td></tr></table><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">Subclassing</a>&#160;.&#160;<a href="article.html">Subclassing Without The Crashes</a>&#160;.&#160;<a href="bugTrak.html">BugTrak Summary</a>&#160;.&#160;Provide support for subclassing Unicode Windows</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 14 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/Subclassing/SSubTimer/bugTrak17-1.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:41 GMT -->
</html>