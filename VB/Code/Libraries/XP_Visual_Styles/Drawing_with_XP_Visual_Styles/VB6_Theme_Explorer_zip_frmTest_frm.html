<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/XP_Visual_Styles/Drawing_with_XP_Visual_Styles/VB6_Theme_Explorer_zip_frmTest_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:45 GMT -->
<head>
<title>vbAccelerator - Contents of code file: frmTest.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: frmTest.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">XP Visual Styles</a>&#160;.&#160;<a href="article.html">Drawing with XP Visual Styles</a>&#160;.&#160;<a href="VB6_Theme_Explorer.html">VB6 Theme Explorer</a>&#160;.&#160;frmTest.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Theme_Explorer.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Theme Explorer</a> (47K)</p><p /><p class="nav"><a href="VB6_Theme_Explorer.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Theme Explorer</a> (44K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:4285</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=4285&amp;type=Zip&amp;title=VB6%20Theme%20Explorer%2Ezip%5FfrmTest">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />20 Feb 2003<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1416">vbAccelerator No Status Bar Control</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1414">vbAccelerator Progress Bar Class</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1427">Adding XP Visual Styles to Your Visual Basic Application</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: frmTest.frm</h1><pre>VERSION 5.00
Object = "{831FDD16-0C5C-11D2-A9FC-0000F8754DA1}#2.0#0"; "MSCOMCTL.OCX"
Begin VB.Form frmXPThemeExplorer 
   Caption         =   "VB XP Theme Explorer"
   ClientHeight    =   6195
   ClientLeft      =   2925
   ClientTop       =   2145
   ClientWidth     =   9900
   BeginProperty Font 
      Name            =   "Tahoma"
      Size            =   8.25
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "frmTest.frx":0000
   LinkTopic       =   "Form1"
   ScaleHeight     =   6195
   ScaleWidth      =   9900
   Begin VB.CheckBox chkIcon 
      Caption         =   "&amp;Icon"
      Height          =   255
      Left            =   2940
      TabIndex        =   9
      Top             =   1500
      Width           =   2535
   End
   Begin VB.CheckBox chkText 
      Caption         =   "&amp;Text:"
      Height          =   255
      Left            =   2940
      TabIndex        =   8
      Top             =   1140
      Width           =   1035
   End
   Begin VB.CheckBox chkCustomiseSize 
      Caption         =   "&amp;Customise Size"
      Height          =   255
      Left            =   2880
      TabIndex        =   7
      Top             =   60
      Value           =   1  'Checked
      Width           =   2535
   End
   Begin VB.TextBox txtText 
      Enabled         =   0   'False
      Height          =   315
      Left            =   4020
      TabIndex        =   6
      Text            =   "vbAccelerator.com"
      Top             =   1080
      Width           =   2715
   End
   Begin VB.TextBox txtHeight 
      Height          =   315
      Left            =   4020
      TabIndex        =   5
      Text            =   "24"
      Top             =   660
      Width           =   2715
   End
   Begin VB.TextBox txtWidth 
      Height          =   315
      Left            =   4020
      TabIndex        =   4
      Text            =   "96"
      Top             =   360
      Width           =   2715
   End
   Begin VB.PictureBox picSamples 
      AutoRedraw      =   -1  'True
      BackColor       =   &amp;H80000005&amp;
      Height          =   4155
      Left            =   2940
      ScaleHeight     =   4095
      ScaleWidth      =   3555
      TabIndex        =   1
      Top             =   1920
      Width           =   3615
   End
   Begin MSComctlLib.TreeView tvwThemes 
      Height          =   6075
      Left            =   60
      TabIndex        =   0
      Top             =   60
      Width           =   2775
      _ExtentX        =   4895
      _ExtentY        =   10716
      _Version        =   393217
      Indentation     =   450
      LabelEdit       =   1
      LineStyle       =   1
      Style           =   2
      Appearance      =   1
   End
   Begin VB.Label lblInfo 
      Caption         =   "Height:"
      Height          =   255
      Index           =   1
      Left            =   3180
      TabIndex        =   3
      Top             =   720
      Width           =   795
   End
   Begin VB.Label lblInfo 
      Caption         =   "Width:"
      Height          =   255
      Index           =   0
      Left            =   3180
      TabIndex        =   2
      Top             =   360
      Width           =   795
   End
   Begin VB.Menu mnuFileTOP 
      Caption         =   "&amp;File"
      Begin VB.Menu mnuFile 
         Caption         =   "&amp;Close"
         Index           =   0
      End
   End
   Begin VB.Menu mnuHelpTOP 
      Caption         =   "&amp;Help"
      Begin VB.Menu mnuHelp 
         Caption         =   "&amp;About..."
         Index           =   0
      End
   End
End
Attribute VB_Name = "frmXPThemeExplorer"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private m_cUxTheme As cUxTheme
Private m_cSysIml As cVBALSysImageList

Private Sub initThemeTree()
Dim sXmlFile As String
   sXmlFile = App.Path
   If (Right$(sXmlFile, 1) &lt;&gt; "\") Then
      sXmlFile = sXmlFile &amp; "\"
   End If
   sXmlFile = sXmlFile &amp; "uxtheme.xml"
   
Dim xmlTheme As New DOMDocument
   If (xmlTheme.Load(sXmlFile)) Then
      Dim nodClass As IXMLDOMElement
      Dim tvwTop As Node
      Set tvwTop = tvwThemes.Nodes.Add(, , "A", "All")
      For Each nodClass In xmlTheme.selectNodes("ThemeData/Class")
         Dim sClass As String
         sClass = getNodeAttribute(nodClass, "name")
         Dim tvwNodeClass As Node
         Set tvwNodeClass = tvwThemes.Nodes.Add(tvwTop, tvwChild, "C" &amp; sClass,
          sClass)
         Dim nodPart As IXMLDOMElement
         For Each nodPart In nodClass.selectNodes("Part")
            Dim sPart As String
            sPart = getNodeAttribute(nodPart, "name")
            Dim sId As String
            sId = getNodeAttribute(nodPart, "id")
            Dim tvwNodePart As Node
            Set tvwNodePart = tvwThemes.Nodes.Add(tvwNodeClass, tvwChild, "P" &amp;
             sPart &amp; "I:" &amp; sId, sPart &amp; " (" &amp; sId &amp; ")")
            Dim nodState As IXMLDOMElement
            For Each nodState In nodPart.selectNodes("State")
               Dim sState As String
               sState = getNodeAttribute(nodState, "name")
               sId = getNodeAttribute(nodState, "id")
               tvwThemes.Nodes.Add tvwNodePart, tvwChild, "S" &amp; sState &amp; "P" &amp;
                sPart &amp; "I:" &amp; sId, sState &amp; " (" &amp; sId &amp; ")"
            Next
         Next
      Next
      tvwTop.Expanded = True
   Else
      MsgBox "Failed to load the Theme Data as XML from file" &amp; vbCrLf &amp; "'" &amp;
       sXmlFile &amp; "'", vbExclamation
   End If
   
End Sub

Private Function getNodeAttribute(nodThis As IXMLDOMNode, ByVal sAttributeName
 As String) As String
Dim i As Long
   For i = 0 To nodThis.Attributes.length - 1
      If (nodThis.Attributes(i).baseName = sAttributeName) Then
         getNodeAttribute = nodThis.Attributes(i).nodeValue
         Exit For
      End If
   Next i
End Function

Private Sub chkCustomiseSize_Click()
   If (chkCustomiseSize.value = vbChecked) Then
      txtWidth.Enabled = True
      txtHeight.Enabled = True
      m_cUxTheme.TextAlign = m_cUxTheme.TextAlign Or DT_WORD_ELLIPSIS
      m_cUxTheme.UseThemeSize = False
   Else
      txtWidth.Enabled = True
      txtHeight.Enabled = True
      m_cUxTheme.TextAlign = m_cUxTheme.TextAlign And Not DT_WORD_ELLIPSIS
      m_cUxTheme.UseThemeSize = True
   End If
   drawSelectedItems
End Sub

Private Sub chkIcon_Click()
   If (chkIcon.value = vbChecked) Then
      m_cUxTheme.hIml = m_cSysIml.hIml
      m_cUxTheme.IconIndex = m_cSysIml.ItemIndex("C:\pants.txt")
   Else
      m_cUxTheme.hIml = 0
      m_cUxTheme.IconIndex = -1
   End If
   drawSelectedItems
End Sub

Private Sub chkText_Click()
   If (chkText.value = vbChecked) Then
      txtText.Enabled = True
      m_cUxTheme.Text = txtText.Text
   Else
      txtText.Enabled = False
      m_cUxTheme.Text = ""
   End If
   drawSelectedItems
End Sub

Private Sub Form_Load()

   ' Load the theme data:
   initThemeTree
   
   Set m_cSysIml = New cVBALSysImageList
   m_cSysIml.IconSizeX = 16
   m_cSysIml.IconSizeY = 16
   m_cSysIml.Create
   
   ' Set up the theme drawing class:
   Set m_cUxTheme = New cUxTheme
   With m_cUxTheme
      .hWnd = Me.hWnd
      .Width = CLng(txtWidth.Text)
      .Height = CLng(txtHeight.Text)
      .TextAlign = DT_CENTER Or DT_VCENTER Or DT_SINGLELINE Or DT_WORD_ELLIPSIS
   End With
   
   If (tvwThemes.Nodes.Count &gt; 0) Then
      Set tvwThemes.SelectedItem = tvwThemes.Nodes(1)
      drawSelectedItems
   End If
      
End Sub

Private Sub Form_Resize()
   On Error Resume Next
   tvwThemes.Move 2 * Screen.TwipsPerPixelX, 2 * Screen.TwipsPerPixelY,
    tvwThemes.Width, Me.ScaleHeight - 4 * Screen.TwipsPerPixelY
   picSamples.Move tvwThemes.left + tvwThemes.Width + 2 *
    Screen.TwipsPerPixelX, picSamples.tOp, _
      Me.ScaleWidth - (tvwThemes.left + tvwThemes.Width + 4 *
       Screen.TwipsPerPixelX), _
      Me.ScaleHeight - picSamples.tOp - 2 * Screen.TwipsPerPixelY
   drawSelectedItems
End Sub

Private Sub getNodeDetails( _
      ByRef nod As Node, _
      ByRef sClass As String, _
      ByRef lPartId As Long, _
      ByRef lStateId As Long _
   )
   Dim sKey As String
   Dim iPos As Long
   sClass = ""
   lPartId = -1
   lStateId = -1
   sKey = nod.Key
   ' what type of key is it?
   Select Case left(sKey, 1)
   Case "A" ' all
   Case "C" ' class
      sClass = Mid(sKey, 2)
   Case "P" ' part
      sClass = Mid(nod.Parent.Key, 2)
      iPos = InStr(sKey, ":")
      lPartId = CLng(Mid(sKey, iPos + 1))
   Case "S" ' state
      sClass = Mid(nod.Parent.Parent.Key, 2)
      iPos = InStr(nod.Parent.Key, ":")
      lPartId = CLng(Mid(nod.Parent.Key, iPos + 1))
      iPos = InStr(sKey, ":")
      lStateId = CLng(Mid(sKey, iPos + 1))
   End Select

End Sub

Private Sub drawSelectedItems()
   picSamples.Cls

   m_cUxTheme.hdc = picSamples.hdc
   m_cUxTheme.left = 4
   m_cUxTheme.tOp = 4

   Dim sClass As String
   Dim lPartId As Long
   Dim lStateId As Long
   getNodeDetails tvwThemes.SelectedItem, sClass, lPartId, lStateId

   Dim nod As Node
   Dim nodChild As Node
   Dim i As Long
   Dim j As Long
   If (Len(sClass) = 0) Then
      ' nothing specified, draw the classes vertically
      ' and the parts horizontally:
      Set nod = tvwThemes.SelectedItem.Child
      For i = 1 To tvwThemes.SelectedItem.children
         Set nodChild = nod.Child
         m_cUxTheme.left = 4
         For j = 1 To nod.children
            getNodeDetails nodChild, sClass, lPartId, lStateId
            m_cUxTheme.Class = sClass
            m_cUxTheme.Part = lPartId
            m_cUxTheme.State = 1
            m_cUxTheme.Draw
            m_cUxTheme.left = m_cUxTheme.left + m_cUxTheme.Width + 4
            Set nodChild = nodChild.Next
         Next
         m_cUxTheme.tOp = m_cUxTheme.tOp + m_cUxTheme.Height + 4
         Set nod = nod.Next
      Next
            
   ElseIf (lPartId &lt;= 0) Then
      ' if no part or state is specified, then we draw
      'parts vertically and states horizontally:
      m_cUxTheme.Class = sClass
      Set nod = tvwThemes.SelectedItem.Child
      For i = 1 To tvwThemes.SelectedItem.children
         Set nodChild = nod.Child
         m_cUxTheme.left = 4
         For j = 1 To nod.children
            getNodeDetails nodChild, sClass, lPartId, lStateId
            m_cUxTheme.Part = lPartId
            m_cUxTheme.State = lStateId
            m_cUxTheme.Draw
            m_cUxTheme.left = m_cUxTheme.left + m_cUxTheme.Width + 4
            Set nodChild = nodChild.Next
         Next
         m_cUxTheme.tOp = m_cUxTheme.tOp + m_cUxTheme.Height + 4
         Set nod = nod.Next
      Next
      
   ElseIf (lStateId &lt;= 0) Then
      ' if no state specified, draw the states vertically:
      m_cUxTheme.Class = sClass
      m_cUxTheme.Part = lPartId
      Set nod = tvwThemes.SelectedItem.Child
      For i = 1 To tvwThemes.SelectedItem.children
         getNodeDetails nod, sClass, lPartId, lStateId
         m_cUxTheme.State = lStateId
         m_cUxTheme.Draw
         m_cUxTheme.tOp = m_cUxTheme.tOp + m_cUxTheme.Height + 4
         Set nod = nod.Next
      Next
   Else
      ' just draw the selected part &amp; state:
      With m_cUxTheme
         .Class = sClass
         .Part = lPartId
         .State = lStateId
         .Draw
      End With
   End If
   
   picSamples.Refresh
End Sub

Private Sub mnuFile_Click(Index As Integer)
   Unload Me
End Sub

Private Sub mnuHelp_Click(Index As Integer)
   Me.Enabled = False
   frmAbout.Show vbModeless, Me
End Sub

Private Sub tvwThemes_NodeClick(ByVal Node As MSComctlLib.Node)
   drawSelectedItems
End Sub

Private Sub txtHeight_Change()
   If IsNumeric(txtHeight.Text) Then
      m_cUxTheme.Height = CLng(txtHeight.Text)
      drawSelectedItems
   End If
End Sub


Private Sub txtHeight_KeyPress(KeyAscii As Integer)
   Select Case KeyAscii
   Case Asc("0") To Asc("9")
   Case vbKeyBack, vbKeyClear, vbKeyDelete
   Case Else
      KeyAscii = 0
   End Select
End Sub

Private Sub txtText_Change()
   m_cUxTheme.Text = txtText.Text
   drawSelectedItems
End Sub

Private Sub txtWidth_Change()
   If IsNumeric(txtWidth.Text) Then
      m_cUxTheme.Width = CLng(txtWidth.Text)
      drawSelectedItems
   End If
End Sub

Private Sub txtWidth_KeyPress(KeyAscii As Integer)
   Select Case KeyAscii
   Case Asc("0") To Asc("9")
   Case vbKeyBack, vbKeyClear, vbKeyDelete
   Case Else
      KeyAscii = 0
   End Select
End Sub
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Libraries</a>&#160;.&#160;<a href="../index.html">XP Visual Styles</a>&#160;.&#160;<a href="article.html">Drawing with XP Visual Styles</a>&#160;.&#160;<a href="VB6_Theme_Explorer.html">VB6 Theme Explorer</a>&#160;.&#160;frmTest.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Libraries/XP_Visual_Styles/Drawing_with_XP_Visual_Styles/VB6_Theme_Explorer_zip_frmTest_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:45 GMT -->
</html>