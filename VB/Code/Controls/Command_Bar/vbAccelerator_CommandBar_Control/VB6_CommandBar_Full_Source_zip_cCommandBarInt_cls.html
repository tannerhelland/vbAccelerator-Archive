<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Controls/Command_Bar/vbAccelerator_CommandBar_Control/VB6_CommandBar_Full_Source_zip_cCommandBarInt_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:07 GMT -->
<head>
<title>vbAccelerator - Contents of code file: cCommandBarInt.cls</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: cCommandBarInt.cls" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Controls</a>&#160;.&#160;<a href="../index.html">Command Bar</a>&#160;.&#160;<a href="article.html">vbAccelerator CommandBar Control</a>&#160;.&#160;<a href="VB6_CommandBar_Full_Source.html">VB6 CommandBar Full Source</a>&#160;.&#160;cCommandBarInt.cls</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_CommandBar_Binary.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 CommandBar Binary</a> (144K)</p><p class="nav"><a href="VB5_CommandBar_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 CommandBar Demonstration</a> (31K)</p><p class="nav"><a href="VB5_CommandBar_Full_Source.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 CommandBar Full Source</a> (256K)</p><p /><p class="nav"><a href="VB6_CommandBar_Binary.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 CommandBar Binary</a> (144K)</p><p class="nav"><a href="VB6_CommandBar_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 CommandBar Demonstration</a> (30K)</p><p class="nav"><a href="VB6_CommandBar_Full_Source.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 CommandBar Full Source</a> (255K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:13869</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=13869&amp;type=Zip&amp;title=VB6%20CommandBar%20Full%20Source%2Ezip%5FcCommandBarInt">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" />﻿<p class="nav"><a href="bugTrak.html">BugTrak</a></p><p class="nav"><a href="bugTrak.html#bugs"><img src="../../../../../res/btBug.png" width="16" height="16" alt="Bug" />&#160;Bugs:</a> 8 / 8</p><p class="nav"><a href="bugTrak.html#issues"><img src="../../../../../res/btIssue.png" width="16" height="16" alt="Issue" />&#160;Issues:</a> 5 / 5</p><p class="nav"><a href="bugTrak.html#questions"><img src="../../../../../res/btQuestion.png" width="16" height="16" alt="Question" />&#160;Questions:</a> 0 / 0</a></p><p class="nav">Updated:11 February 2004</p>
<br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />25 Jan 2004<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: cCommandBarInt.cls</h1><pre>VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "cCommandBarInt"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' cComamndBarInt is the real cCommandBar object.
' It holds a collection of object pointers to cCommandBarItemInt
' objects as well as a collection of each of the controls which
' own this command bar.

Private m_colhWndUser As Collection
Private m_colhWndParent As Collection

Private m_sKey As String
Private m_sTitle As String
Private m_colItems As Collection

Friend Function AcceleratorMatches( _
      ByVal hWndActiveForm As Long, _
      ByVal vKey As Integer, _
      ByVal shiftMask As Long, _
      ctlNotify As vbalCommandBar _
   ) As cButtonInt
Dim vlPtr As Variant
Dim btn As cButtonInt
Dim cMatch As cButtonInt
   For Each vlPtr In m_colItems
      Set btn = ObjectFromPtr(vlPtr)
      If Not (btn Is Nothing) Then
         Set cMatch = btn.AcceleratorMatches(hWndActiveForm, vKey, shiftMask,
          True, ctlNotify)
         If Not (cMatch Is Nothing) Then
            Set AcceleratorMatches = cMatch
            Exit Function
         End If
      End If
   Next
End Function

Friend Function AddRefhWnd(ByVal hWnd As Long, ByVal hWndParent As Long)
   If Not (CollectionContains(m_colhWndUser, "H" &amp; hWnd)) Then
      m_colhWndUser.Add hWnd, "H" &amp; hWnd
      m_colhWndParent.Add hWndParent, "H" &amp; hWnd
      ' Ensure that the buttons get a reference to the parent
      ' form:
      Dim vlPtr As Variant
      Dim btn As cButtonInt
      For Each vlPtr In m_colItems
         Set btn = ObjectFromPtr(vlPtr)
         If Not (btn Is Nothing) Then
            btn.AddParenthWnd hWnd, hWndParent
         End If
      Next vlPtr
   End If
End Function
Friend Function ReleaseRef(ByVal hWnd As Long)
Dim hWndParent As Long
   hWndParent = m_colhWndParent("H" &amp; hWnd)
   m_colhWndUser.Remove "H" &amp; hWnd
   m_colhWndParent.Remove "H" &amp; hWnd
   ' Remove the parent form for the buttons:
   Dim vlPtr As Variant
   For Each vlPtr In m_colItems
      Dim btn As cButtonInt
      Set btn = ObjectFromPtr(vlPtr)
      If Not (btn Is Nothing) Then
         btn.RemoveParenthWnd hWndParent
      End If
   Next vlPtr
End Function
Friend Sub NotifyUsers(ByVal eventType As Long, Optional Item As cButtonInt =
 Nothing)
Dim vHwnd As Variant
Dim ctlCmdBar As vbalCommandBar
Static noRecurse As Boolean


   If Not (Item Is Nothing) Then
      If (eventType = CHANGENOTIFICATIONBUTTONCHECKCHANGE) Then
         
         If Not noRecurse Then
         
            Dim bThisIsChecked As Boolean
            Dim bFoundCheckedItem As Boolean
            Dim vlPtr As Variant
            Dim btnInt As cButtonInt
            Dim iIndex As Long
            Dim iFirstIndex As Long
            Dim iLastIndex As Long
            Dim iBtnIndex As Long
            Dim iCheckCount As Long
            Dim bFoundButton As Boolean
            
            ' Confirm which other items to
            ' change check state:
            If (Item.Style = eRadio) Or (Item.Style = eRadioNullable) Then
                           
               bThisIsChecked = Item.Checked
               bFoundCheckedItem = bThisIsChecked
               
               noRecurse = True
               
               For Each vlPtr In m_colItems
                  iIndex = iIndex + 1
                  Set btnInt = ObjectFromPtr(vlPtr)
                  If (btnInt.Key = Item.Key) Then
                     bFoundButton = True
                     iBtnIndex = iIndex
                     iLastIndex = iIndex
                  Else
                     If (bFoundButton) Then
                        If (btnInt.Style = eSeparator) Then
                           iLastIndex = iIndex - 1
                           Exit For
                        Else
                           iLastIndex = iIndex
                        End If
                     Else
                        If (iFirstIndex = 0) Then
                           iFirstIndex = iIndex
                        ElseIf (btnInt.Style = eSeparator) Then
                           iFirstIndex = iIndex + 1
                        End If
                     End If
                  End If
               Next
               
               If (iFirstIndex = 0) Then
                  iFirstIndex = 1
               End If
               
               If (Item.Style = eRadioNullable) Then
                  ' any other items in the same group
                  ' must be unchecked
                  If (Item.Checked) Then
                     For iIndex = iFirstIndex To iLastIndex
                        If (iIndex &lt;&gt; iBtnIndex) Then
                           Set btnInt = ObjectFromPtr(m_colItems(iIndex))
                           btnInt.Checked = False
                        End If
                     Next iIndex
                  End If
                  
               ElseIf (Item.Style = eRadio) Then
                  ' any other items in the same group must
                  ' be unchecked; if no items are checked
                  ' then this item must be rechecked.
                  If (Item.Checked) Then
                     For iIndex = iFirstIndex To iLastIndex
                        If Not (iIndex = iBtnIndex) Then
                           Set btnInt = ObjectFromPtr(m_colItems(iIndex))
                           btnInt.Checked = False
                        End If
                     Next iIndex
                  Else
                     For iIndex = iFirstIndex To iLastIndex
                        Set btnInt = ObjectFromPtr(m_colItems(iIndex))
                        iCheckCount = iCheckCount + Abs(btnInt.Checked)
                     Next iIndex
                     If (iCheckCount = 0) Then
                        Item.Checked = True
                     End If
                  End If
                  
               End If
               
               noRecurse = False
            End If
                     
         End If
      End If
   End If

   For Each vHwnd In m_colhWndUser
      If ControlFromhWnd(vHwnd, ctlCmdBar) Then
         If (ctlCmdBar.fInUse) Then
            ctlCmdBar.ChangeNotification Me, eventType, Item
         End If
      End If
   Next
   
End Sub
Friend Sub fInit(ByVal sKey As String)
   m_sKey = sKey
End Sub
Friend Property Get Key() As String
   Key = m_sKey
End Property
Friend Property Get IndexOf(ByVal sBtnKey As String) As Long
Dim iItem As Long
Dim vlPtr As Variant
Dim btnInt As cButtonInt
   For Each vlPtr In m_colItems
      iItem = iItem + 1
      Set btnInt = ObjectFromPtr(vlPtr)
      If (btnInt.Key = sBtnKey) Then
         IndexOf = iItem
         Exit For
      End If
   Next
End Property
Friend Property Get Title() As String
   Title = m_sTitle
End Property
Friend Property Let Title(ByVal sTitle As String)
   m_sTitle = sTitle
   NotifyUsers 3
End Property
Friend Property Get Count() As Long
   Count = m_colItems.Count
End Property
Friend Property Get Item(ByVal index As Variant) As cButtonInt
Dim lPtr As Long
   lPtr = m_colItems(index)
   If Not (lPtr = 0) Then
      Set Item = ObjectFromPtr(lPtr)
   End If
End Property
Friend Sub Add(button As cButtonInt)
Dim sKey As String
   sKey = button.Key
   If CollectionContains(m_colItems, sKey) Then
      gErr 5
   Else
      m_colItems.Add ObjPtr(button), sKey
      button.AddedToBar Me
      NotifyUsers 1
   End If
End Sub
Friend Sub InsertBefore(button As cButtonInt, buttonBefore As cButtonInt)
   If CollectionContains(m_colItems, button.Key) Then
      gErr 5
   Else
      If Not (CollectionContains(m_colItems, buttonBefore.Key)) Then
         gErr 3
      Else
         m_colItems.Add ObjPtr(button), button.Key, buttonBefore.Key
         Dim i As Long
         For i = 1 To m_colhWndUser.Count
            button.AddParenthWnd m_colhWndUser(i), m_colhWndParent(i)
         Next i
         button.AddedToBar Me
         NotifyUsers 1
      End If
   End If
End Sub
Friend Sub InsertAfter(button As cButtonInt, buttonAfter As cButtonInt)
   If CollectionContains(m_colItems, button.Key) Then
      gErr 5
   Else
      If Not (CollectionContains(m_colItems, buttonAfter.Key)) Then
         gErr 3
      Else
         m_colItems.Add ObjPtr(button), button.Key, , buttonAfter.Key
         Dim i As Long
         For i = 1 To m_colhWndUser.Count
            button.AddParenthWnd m_colhWndUser(i), m_colhWndParent(i)
         Next i
         button.AddedToBar Me
         NotifyUsers 1
      End If
   End If
End Sub
Friend Sub Remove(button As cButtonInt)
   If CollectionContains(m_colItems, button.Key) Then
      Dim vlhWnd As Variant
      For Each vlhWnd In m_colhWndParent
         button.RemoveParenthWnd vlhWnd
      Next vlhWnd
      m_colItems.Remove button.Key
      button.RemovedFromBar Me
      NotifyUsers 1
   Else
      gErr 3
   End If
End Sub
Friend Sub Clear()
Dim vlPtr As Variant
Dim itm As cButtonInt
   For Each vlPtr In m_colItems
      Set itm = ObjectFromPtr(vlPtr)
      Dim vlhWnd As Variant
      For Each vlhWnd In m_colhWndParent
         itm.RemoveParenthWnd vlhWnd
      Next vlhWnd
      If Not (itm Is Nothing) Then
         itm.RemovedFromBar Me
      End If
   Next
   Set m_colItems = New Collection
   NotifyUsers 1
End Sub
'Friend Sub CalculateMenuWithVisibleCheckSize( _
'      cMP As cMeasureButtonParams, _
'      ByRef menuWidth As Long, _
'      ByRef menuHeight As Long, _
'      ByRef Item() As cDisplayButtonInfo _
'   )
'
'   ' basically same as calculate menu size,
'   ' but we add an extra check width and ignore
'   ' if items are marked as invisible.
'   CalculateMenuSize cMP, menuWidth, menuHeight, Item(), True
'
'   menuWidth = menuWidth + cMP.IconHeight + 8
'   Dim iItem As Long
'   For iItem = 1 To m_colItems.Count
'      Item(iItem).Right = menuWidth
'   Next iItem
'
'End Sub
Friend Sub CalculateMenuSize( _
      cMP As cMeasureButtonParams, _
      ByRef menuWidth As Long, _
      ByRef menuHeight As Long, _
      ByRef Item() As cDisplayButtonInfo, _
      Optional ByVal bIncludeInvisible As Boolean = False _
   )
   
   '
   menuWidth = 0
   menuHeight = 0
   
   If (m_colItems.Count &gt; 0) Then
      
      ' ensure we calculate the required height:
      cMP.Height = 0
      
      Dim vlPtr As Variant
      Dim btnInt As cButtonInt
      Dim maxWidth As Long
      Dim btnWidth As Long
      Dim btnHeight As Long
      Dim iItem As Long
      Dim bLastHadNoCaption As Boolean
      Dim iConsecutiveNoCaptionItems As Long
      Dim iMaxConsecutiveNoCaptionItems As Long
      Dim iNoCaptionWidth As Long
      Dim lHeight As Long
      Dim x As Long
      Dim y As Long
      
      
      For Each vlPtr In m_colItems
         
         iItem = iItem + 1
         btnWidth = 0
         btnHeight = 0
      
         Set btnInt = ObjectFromPtr(vlPtr)
         If Not (btnInt Is Nothing) Then
            If (btnInt.Visible Or bIncludeInvisible) Then
               If Not (btnInt.InfrequentlyUsed) Or
                mCommandBars.ShowingInfrequentlyUsed Or bIncludeInvisible Then
                  
                  cMP.Height = 0
                  btnWidth = btnInt.Size(cMP, Item(iItem).Hidden)
                  btnHeight = cMP.Height
                  If (btnWidth &gt; maxWidth) Then
                     maxWidth = btnWidth
                  End If
                  
                  If Len(btnInt.Caption) = 0 And Not (btnInt.Style =
                   eSeparator) Then
                     bLastHadNoCaption = True
                     iConsecutiveNoCaptionItems = iConsecutiveNoCaptionItems + 1
                     iNoCaptionWidth = btnWidth
                  Else
                     bLastHadNoCaption = False
                     If (iConsecutiveNoCaptionItems &gt;
                      iMaxConsecutiveNoCaptionItems) Then
                        iMaxConsecutiveNoCaptionItems =
                         iConsecutiveNoCaptionItems
                     End If
                     iConsecutiveNoCaptionItems = 0
                  End If
                  
               End If
            End If
         End If
            
         Item(iItem).left = 0
         If (menuHeight = 0) Then
            menuHeight = 1
         End If
         Item(iItem).top = menuHeight
         menuHeight = menuHeight + btnHeight
         Item(iItem).bottom = menuHeight
         Item(iItem).right = btnWidth
      
      Next vlPtr
            
      If (iMaxConsecutiveNoCaptionItems &gt; 2) And Not (bIncludeInvisible) Then
         ' Recalc to place the items:
         ' How many items can we fit into the suggested menu width?
         If (maxWidth &lt; iNoCaptionWidth * 8 + 28) Then
            maxWidth = iNoCaptionWidth * 8 + 28 ' TODO fix magic number
         End If
         menuWidth = maxWidth
      
         iItem = 0
         bLastHadNoCaption = False
         iConsecutiveNoCaptionItems = 0
         y = 1
         
         For Each vlPtr In m_colItems
            
            iItem = iItem + 1
         
            Set btnInt = ObjectFromPtr(vlPtr)
            If Not (btnInt Is Nothing) Then
               If (btnInt.Visible) Then
                  If Not (btnInt.InfrequentlyUsed) Or
                   mCommandBars.ShowingInfrequentlyUsed Then
            
                     If Len(btnInt.Caption) = 0 And Not (btnInt.Style =
                      eSeparator) Then
                        If Not (bLastHadNoCaption) Then
                           bLastHadNoCaption = True
                        End If
                        If (iConsecutiveNoCaptionItems Mod 8) = 0 Then
                           If (iItem &gt; 1) Then
                              y = y + Item(iItem - 1).bottom - Item(iItem -
                               1).top
                           End If
                           x = 26 ' TODO fix magic number
                        Else
                           x = x + iNoCaptionWidth
                        End If
                        Item(iItem).left = x
                        Item(iItem).right = x + iNoCaptionWidth
                        lHeight = Item(iItem).bottom - Item(iItem).top
                        Item(iItem).top = y
                        Item(iItem).bottom = y + lHeight
                        iConsecutiveNoCaptionItems = iConsecutiveNoCaptionItems
                         + 1
                     Else
                        bLastHadNoCaption = False
                        iConsecutiveNoCaptionItems = 0
                        Item(iItem).right = menuWidth
                        lHeight = Item(iItem).bottom - Item(iItem).top
                        If (iItem &gt; 1) Then
                           y = y + Item(iItem - 1).bottom - Item(iItem - 1).top
                        End If
                        Item(iItem).top = y
                        Item(iItem).bottom = y + lHeight
                     End If
                  End If
               End If
            End If
            
         Next vlPtr
         
         menuHeight = y + Item(m_colItems.Count).bottom -
          Item(m_colItems.Count).top
         
      Else
      
         menuWidth = maxWidth
         ' Set the width for the items:
         For iItem = 1 To m_colItems.Count
            Item(iItem).right = menuWidth
         Next
         
      End If
      
   End If
   '
End Sub
Friend Sub Draw( _
      cDP As cDrawButtonParams, _
      Item() As cDisplayButtonInfo _
   )
   If (m_colItems.Count &gt; 0) Then
      
      Dim vlPtr As Variant
      Dim btnInt As cButtonInt
      Dim iItem As Long
      Dim bVisible As Boolean
      
      For Each vlPtr In m_colItems
         iItem = iItem + 1
         Set btnInt = ObjectFromPtr(vlPtr)
         If Not (btnInt Is Nothing) Then
            bVisible = btnInt.Visible And Not (Item(iItem).Hidden)
            If Not (bVisible) Then
               If (cDP.SizeStyle = COMMANDBARSIZESTYLEMENUVISIBLECHECK) Then
                  bVisible = Not (btnInt.VisibleCheck = vbGrayed)
               End If
            End If
            
            If (bVisible And (Item(iItem).bottom - Item(iItem).top) &gt; 0) Then
               cDP.left = Item(iItem).left
               cDP.top = Item(iItem).top
               cDP.Size = Item(iItem).right - Item(iItem).left
               cDP.Height = Item(iItem).bottom - Item(iItem).top
               cDP.MouseDownButton = Item(iItem).mouseDown
               cDP.MouseOverButton = Item(iItem).mouseOver
               cDP.MouseDownSplit = Item(iItem).MouseDownSplit
               cDP.MouseOverSplit = Item(iItem).MouseOverSplit
               cDP.ShowingMenu = Item(iItem).ShowingMenu
               btnInt.Draw cDP
            End If
         End If
      Next
   End If
End Sub
Friend Sub DrawOneButton( _
      cDP As cDrawButtonParams, _
      itemIndex As Long _
   )
Dim vlPtr As Variant
Dim btnInt As cButtonInt
   vlPtr = m_colItems(itemIndex)
   Set btnInt = ObjectFromPtr(vlPtr)
   If Not (btnInt Is Nothing) Then
      btnInt.Draw cDP
   End If
End Sub
Friend Sub ClickButton( _
      index As Long _
   )
   If (Item(index).VisibleCheck = vbGrayed) Then
      If (Item(index).Style = eCheck) Or (Item(index).Style = eRadio) Or
       (Item(index).Style = eRadioNullable) Then
         ' this will cause notify users to be called.
          Item(index).Checked = Not (Item(index).Checked)
      Else
         '
      End If
   Else
      Item(index).VisibleCheck = IIf(Item(index).VisibleCheck = vbChecked,
       vbUnchecked, vbChecked)
      Item(index).Checked = Not (Item(index).Checked)
   End If
   
End Sub
Friend Sub CalculateToolbarSize( _
      cMP As cMeasureButtonParams, _
      ByRef toolbarWidth As Long, _
      ByRef toolbarHeight As Long, _
      ByRef Item() As cDisplayButtonInfo _
   )
   
   ' The toolbar width is constrained.  We calculate
   ' the desired height for a single button and also
   ' hide buttons with lower priorities if there isn't
   ' enough room.  We also return the actual width
   ' needed for the bar in the toolbarWidth param
   '
   ' The first loop calculates the total width we end up with
   '
   
   Dim availableWidth As Long
   Dim availableHeight As Long
   Dim availableSize As Long
   
   availableWidth = toolbarWidth
   availableHeight = toolbarHeight
   If (cMP.Orientation = eLeft) Or (cMP.Orientation = eRight) Then
      availableSize = availableHeight
   Else
      availableSize = availableWidth
   End If
   toolbarWidth = 0
   toolbarHeight = 0
   
   If (m_colItems.Count &gt; 0) Then
      
      'cMP.Height = 0
      
      Dim vlPtr As Variant
      Dim btnInt As cButtonInt
      Dim btnWidth As Long
      Dim btnHeight As Long
      Dim iItem As Long
      Dim iReHide As Long
      Dim lastRight As Long
      Dim offset As Long
      Dim eLastStyle As EButtonStyle
      Dim lastBtnWidth As Long
      Dim bResizeAfterHiding As Boolean
      Dim removedSize As Long
      Dim removedSizeTarget As Long
      Dim hitTarget As Boolean
      Dim additionalSize As Long
      Dim additionalSizeTarget As Long
      Dim possibleAdditionalSize As Long
      Dim possibleUnHidden() As Long
      Dim possibleUnHiddenCount As Long
      Dim hasHidden As Boolean
      Dim hasVisibleSeparators As Boolean
      Dim toolbarRowHeight() As Long
      Dim toolbarRow As Long
      Dim iRow As Long
            
      Do
         If (cMP.RightToLeft) Then
            lastRight = availableSize
         Else
            lastRight = 0
         End If
      
         eLastStyle = 0
         toolbarWidth = 0
         toolbarHeight = 0
         lastBtnWidth = 0
         iItem = 0
         
         toolbarRow = 1
         ReDim toolbarRowHeight(1 To 1) As Long
         toolbarRowHeight(1) = 0

         For Each vlPtr In m_colItems
            iItem = iItem + 1
            
            Set btnInt = ObjectFromPtr(vlPtr)
            If Not (btnInt Is Nothing) Then
               If (Item(iItem).Hidden) Then
                  hasHidden = True
               End If
               If (btnInt.Visible And Not Item(iItem).Hidden) Then
                  If ((eLastStyle = eSeparator) And (btnInt.Style =
                   eSeparator)) Then
                     btnWidth = 0
                     btnHeight = 0
                  Else
                     If (btnInt.Style = eSeparator) Then
                        hasVisibleSeparators = True
                     End If
                     btnWidth = btnInt.Size(cMP, Item(iItem).Hidden)
                     btnHeight = cMP.Height
                     If (btnHeight &gt; toolbarRowHeight(toolbarRow)) Then
                        toolbarRowHeight(toolbarRow) = btnHeight
                     End If
                  End If
               Else
                  btnWidth = 0
                  btnHeight = 0
               End If
            End If
            lastBtnWidth = btnWidth
            If Not (lastBtnWidth = 0) Then
               eLastStyle = btnInt.Style
            End If
   
            If (cMP.Orientation = eLeft) Or (cMP.Orientation = eRight) Then
               Item(iItem).top = lastRight
               lastRight = lastRight + btnWidth
               Item(iItem).bottom = lastRight
               Item(iItem).left = 0
               Item(iItem).right = btnHeight
            Else
               If (cMP.RightToLeft) Then
                  Item(iItem).right = lastRight
                  lastRight = Item(iItem).right - btnWidth
                  If (lastRight &lt; 0) And (cMP.SizeStyle =
                   COMMANDBARSIZESTYLETOOLBARMENU) Then
                     toolbarRow = toolbarRow + 1
                     ReDim Preserve toolbarRowHeight(1 To toolbarRow) As Long
                     toolbarRowHeight(toolbarRow) = btnHeight
                     Item(iItem).left = availableSize
                     lastRight = availableSize - btnWidth
                  End If
                  Item(iItem).left = lastRight
               Else
                  Item(iItem).left = lastRight
                  lastRight = Item(iItem).left + btnWidth
                  If (lastRight &gt;= availableSize) And (cMP.SizeStyle =
                   COMMANDBARSIZESTYLETOOLBARMENU) Then
                     toolbarRow = toolbarRow + 1
                     ReDim Preserve toolbarRowHeight(1 To toolbarRow) As Long
                     toolbarRowHeight(toolbarRow) = btnHeight
                     Item(iItem).left = 0
                     lastRight = btnWidth
                  End If
                  Item(iItem).right = lastRight
               End If
               Item(iItem).top = 0
               For iRow = 1 To toolbarRow - 1
                  Item(iItem).top = Item(iItem).top + toolbarRowHeight(iRow)
               Next iRow
               Item(iItem).bottom = btnHeight + Item(iItem).top
            End If
         Next
         
         If (toolbarWidth &lt; lastRight) Then
            toolbarWidth = lastRight
         End If
               
         ' TODO: button priority
         If Not (bResizeAfterHiding) And Not (cMP.SizeStyle =
          COMMANDBARSIZESTYLETOOLBARMENU) Then
            If (toolbarWidth &gt; availableSize) Then
               ' start making items invisible working
               ' backwards from the end downwards:
               removedSizeTarget = toolbarWidth - availableSize
               For iItem = m_colItems.Count To 1 Step -1
                  vlPtr = m_colItems(iItem)
                  Set btnInt = ObjectFromPtr(vlPtr)
                  If Not (btnInt Is Nothing) Then
                     If (btnInt.Visible And Not Item(iItem).Hidden) Then
                        If Not (hitTarget) Then
                           Item(iItem).Hidden = True
                           If Not (btnInt.PanelControl Is Nothing) Then
                              btnInt.PanelControl.Visible = False
                           End If
                           removedSize = removedSize + Item(iItem).right -
                            Item(iItem).left
                           If (removedSize &gt;= removedSizeTarget) Then
                              hitTarget = True
                              If Not (hasVisibleSeparators) Then
                                 Exit For
                              End If
                           End If
                        Else
                           If (btnInt.Style = eSeparator) Then
                              Item(iItem).Hidden = True
                              If Not (btnInt.PanelControl Is Nothing) Then
                                 btnInt.PanelControl.Visible = False
                              End If
                              Exit For
                           Else
                              Item(iItem).Hidden = True
                              If Not (btnInt.PanelControl Is Nothing) Then
                                 btnInt.PanelControl.Visible = False
                              End If
                           End If
                        End If
                     End If
                  End If
               Next iItem
               bResizeAfterHiding = True
            
            ElseIf (hasHidden) Then
               ' Start making items visible working
               ' forwards from the start:
               additionalSizeTarget = availableSize - toolbarWidth
               For iItem = 1 To m_colItems.Count
                  vlPtr = m_colItems(iItem)
                  Set btnInt = ObjectFromPtr(vlPtr)
                  If Not (btnInt Is Nothing) Then
                     If (btnInt.Visible) Then
                        If (Item(iItem).Hidden) Then
                           If (btnInt.Style = eSeparator And
                            possibleUnHiddenCount &gt; 0) Then
                              ' its all good
                              Exit For
                           Else
                              Item(iItem).Hidden = False
                              possibleAdditionalSize = btnInt.Size(cMP,
                               Item(iItem).Hidden)
                              If (additionalSize + possibleAdditionalSize &lt;=
                               additionalSizeTarget) Then
                                 possibleUnHiddenCount = possibleUnHiddenCount
                                  + 1
                                 ReDim Preserve possibleUnHidden(1 To
                                  possibleUnHiddenCount) As Long
                                 possibleUnHidden(possibleUnHiddenCount) = iItem
                                 additionalSize = additionalSize +
                                  possibleAdditionalSize
                              Else
                                 Item(iItem).Hidden = True
                                 If Not (btnInt.PanelControl Is Nothing) Then
                                    btnInt.PanelControl.Visible = False
                                 End If
                                 If (hasVisibleSeparators) Then
                                    For iReHide = 1 To possibleUnHiddenCount
                                       Set btnInt =
                                        ObjectFromPtr(m_colItems(possibleUnHidde
                                       n(iReHide)))
                                       If Not (btnInt Is Nothing) Then
                                          Item(iItem).Hidden = True
                                          If Not (btnInt.PanelControl Is
                                         Nothing) Then
                                             btnInt.PanelControl.Visible = False
                                          End If
                                       End If
                                    Next iReHide
                                 End If
                                 Exit For
                              End If
                           End If
                        End If
                     End If
                  End If
               Next iItem
               bResizeAfterHiding = True
            End If
         Else
            bResizeAfterHiding = False
         End If
      Loop While bResizeAfterHiding
      
      toolbarHeight = 0
      For iRow = 1 To toolbarRow
         toolbarHeight = toolbarHeight + toolbarRowHeight(iRow)
      Next iRow
      
      ' If we're a vertical toolbar then there can be a difference
      ' between an individual button's width and the width of the
      ' toolbar as a whole:
      If (cMP.Orientation = eLeft Or cMP.Orientation = eRight) Then
         iItem = 0
         For Each vlPtr In m_colItems
            iItem = iItem + 1
            Set btnInt = ObjectFromPtr(vlPtr)
            If Not (btnInt Is Nothing) Then
               If btnInt.Style = eSeparator Then
                  Item(iItem).right = toolbarHeight
               Else
                  If (Item(iItem).right - Item(iItem).left &lt; toolbarHeight) Then
                     ' centre:
                     offset = (toolbarHeight - (Item(iItem).right -
                      Item(iItem).left)) / 2
                     Item(iItem).left = Item(iItem).left + offset
                     Item(iItem).right = Item(iItem).right + offset
                  End If
               End If
            End If
         Next
      End If
   End If
   
   
   '
   
End Sub
Friend Sub CalculateWrappableToolbarSize( _
      cMP As cMeasureButtonParams, _
      ByRef toolbarWidth As Long, _
      ByRef toolbarHeight As Long, _
      ByRef Item() As cDisplayButtonInfo _
   )
   
   ' The width is kind of constrained but it will be
   ' adjusted to the nearest snap position if we need
   ' to wrap
   
   
      
End Sub

Friend Sub Dispose()
   Set m_colItems = New Collection
   Set m_colhWndUser = New Collection
End Sub

Private Sub Class_Initialize()
   Set m_colhWndUser = New Collection
   Set m_colhWndParent = New Collection
   Set m_colItems = New Collection
End Sub

Private Sub Class_Terminate()
   Clear
End Sub
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Controls</a>&#160;.&#160;<a href="../index.html">Command Bar</a>&#160;.&#160;<a href="article.html">vbAccelerator CommandBar Control</a>&#160;.&#160;<a href="VB6_CommandBar_Full_Source.html">VB6 CommandBar Full Source</a>&#160;.&#160;cCommandBarInt.cls</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2004 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 1 February 2004</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Controls/Command_Bar/vbAccelerator_CommandBar_Control/VB6_CommandBar_Full_Source_zip_cCommandBarInt_cls.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:07 GMT -->
</html>