<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Controls/S_Grid_2/Music_Library/VB6_Music_Library_zip_frmAbout_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:06 GMT -->
<head>
<title>vbAccelerator - Contents of code file: frmAbout.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: frmAbout.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Controls</a>&#160;.&#160;<a href="../index.html">S Grid 2</a>&#160;.&#160;<a href="article.html">MusicLibrary SGrid Sample</a>&#160;.&#160;<a href="VB6_Music_Library.html">VB6 Music Library</a>&#160;.&#160;frmAbout.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Music_Library.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Music Library</a> (108K)</p><p /><p class="nav"><a href="VB6_Music_Library.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Music Library</a> (104K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:13983</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=13983&amp;type=Zip&amp;title=VB6%20Music%20Library%2Ezip%5FfrmAbout">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" />﻿<p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />24 Jan 2004<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1418">Two code only solutions for displaying Common/Dialogs</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=13703">SGrid 2.0</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1024">IShellFolder Extended Type Library Version 1.2 (ISHF_Ex.Tlb)</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2">vbAccelerator ImageList Control and Class v2.0</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3490">Reading and Writing MP3 ID3v1 and ID3v2 Tags</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2950">Compact a Long Path Name to fit a given space</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1420">Browsing For Folders</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: frmAbout.frm</h1><pre>VERSION 5.00
Begin VB.Form frmAbout 
   AutoRedraw      =   -1  'True
   BackColor       =   &amp;H80000005&amp;
   BorderStyle     =   3  'Fixed Dialog
   Caption         =   "%Title%"
   ClientHeight    =   4320
   ClientLeft      =   4815
   ClientTop       =   3105
   ClientWidth     =   6285
   BeginProperty Font 
      Name            =   "Tahoma"
      Size            =   8.25
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "frmAbout.frx":0000
   LinkTopic       =   "Form1"
   MaxButton       =   0   'False
   MinButton       =   0   'False
   ScaleHeight     =   288
   ScaleMode       =   3  'Pixel
   ScaleWidth      =   419
   ShowInTaskbar   =   0   'False
   StartUpPosition =   1  'CenterOwner
   Begin VB.Timer tmr 
      Enabled         =   0   'False
      Interval        =   1
      Left            =   300
      Top             =   3720
   End
   Begin VB.PictureBox picLogo 
      BackColor       =   &amp;H80000010&amp;
      BorderStyle     =   0  'None
      Height          =   1035
      Left            =   1365
      MouseIcon       =   "frmAbout.frx":000C
      MousePointer    =   99  'Custom
      Picture         =   "frmAbout.frx":015E
      ScaleHeight     =   1035
      ScaleWidth      =   4800
      TabIndex        =   7
      Tag             =   "http://vbaccelerator.com/"
      ToolTipText     =   "Click to visit vbAccelerator.com - Advanced VB, C#
       and VB.NET source code."
      Top             =   75
      Width           =   4800
   End
   Begin VB.TextBox txtAcknowledgements 
      ForeColor       =   &amp;H80000015&amp;
      Height          =   1215
      Left            =   1380
      Locked          =   -1  'True
      MultiLine       =   -1  'True
      ScrollBars      =   2  'Vertical
      TabIndex        =   4
      Top             =   2280
      Width           =   4815
   End
   Begin VB.PictureBox picNothing 
      AutoRedraw      =   -1  'True
      BackColor       =   &amp;H80000010&amp;
      BorderStyle     =   0  'None
      Height          =   1065
      Left            =   60
      ScaleHeight     =   71
      ScaleMode       =   3  'Pixel
      ScaleWidth      =   408
      TabIndex        =   3
      Top             =   60
      Width           =   6120
      Begin VB.Label lblLinkTo 
         Alignment       =   2  'Center
         BackStyle       =   0  'Transparent
         Height          =   255
         Left            =   1320
         TabIndex        =   8
         Top             =   810
         Width           =   4815
      End
   End
   Begin VB.CommandButton cmdOK 
      Caption         =   "OK"
      Height          =   435
      Left            =   4860
      TabIndex        =   0
      Top             =   3660
      Width           =   1335
   End
   Begin VB.PictureBox Picture1 
      BorderStyle     =   0  'None
      Height          =   1215
      Left            =   60
      ScaleHeight     =   1215
      ScaleWidth      =   6120
      TabIndex        =   5
      Top             =   60
      Width           =   6120
   End
   Begin VB.Label lblVersion 
      BackStyle       =   0  'Transparent
      Caption         =   "%Version%"
      Height          =   255
      Left            =   1380
      TabIndex        =   6
      Top             =   1980
      Width           =   4755
   End
   Begin VB.Line Line1 
      BorderColor     =   &amp;H80000010&amp;
      X1              =   4
      X2              =   412
      Y1              =   240
      Y2              =   240
   End
   Begin VB.Label lblAppName 
      BackStyle       =   0  'Transparent
      Caption         =   "%AppName%"
      Height          =   255
      Left            =   1380
      TabIndex        =   2
      Top             =   1320
      Width           =   4815
   End
   Begin VB.Label lblCopyright 
      BackStyle       =   0  'Transparent
      Caption         =   "%Copyright%"
      Height          =   495
      Left            =   1380
      TabIndex        =   1
      Top             =   3720
      Width           =   3435
   End
End
Attribute VB_Name = "frmAbout"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private Type RECT
   left As Long
   top As Long
   right As Long
   bottom As Long
End Type
Private Type TRIVERTEX
   x As Long
   y As Long
   Red As Integer
   Green As Integer
   Blue As Integer
   Alpha As Integer
End Type
Private Type GRADIENT_RECT
    UpperLeft As Long
    LowerRight As Long
End Type
Private Type GRADIENT_TRIANGLE
    Vertex1 As Long
    Vertex2 As Long
    Vertex3 As Long
End Type
Private Declare Function GradientFill Lib "msimg32" ( _
   ByVal hDC As Long, _
   pVertex As TRIVERTEX, _
   ByVal dwNumVertex As Long, _
   pMesh As GRADIENT_RECT, _
   ByVal dwNumMesh As Long, _
   ByVal dwMode As Long) As Long
Private Declare Function GradientFillTriangle Lib "msimg32" Alias
 "GradientFill" ( _
   ByVal hDC As Long, _
   pVertex As TRIVERTEX, _
   ByVal dwNumVertex As Long, _
   pMesh As GRADIENT_TRIANGLE, _
   ByVal dwNumMesh As Long, _
   ByVal dwMode As Long) As Long

Private Const GRADIENT_FILL_RECT_H As Long = 0
Private Const GRADIENT_FILL_RECT_V As Long = 1
Private Const GRADIENT_FILL_TRIANGLE As Long = &amp;H2

Private Declare Function OleTranslateColor Lib "OLEPRO32.DLL" (ByVal OLE_COLOR
 As Long, ByVal HPALETTE As Long, pccolorref As Long) As Long
Private Const CLR_INVALID = -1

Private Declare Function ShellExecute Lib "shell32.dll" Alias "ShellExecuteA"
 (ByVal hWnd As Long, ByVal lpOperation As String, ByVal lpFile As String,
 ByVal lpParameters As String, ByVal lpDirectory As String, ByVal nShowCmd As
 Long) As Long
Private Const SW_SHOWNORMAL = 1
Private Declare Function GetDeviceCaps Lib "gdi32" (ByVal hDC As Long, ByVal
 nIndex As Long) As Long
Private Const BITSPIXEL = 12         '  Number of bits per pixel

Private Type OSVERSIONINFO
   dwVersionInfoSize As Long
   dwMajorVersion As Long
   dwMinorVersion As Long
   dwBuildNumber As Long
   dwPlatformId As Long
   szCSDVersion(0 To 127) As Byte
End Type
Private Declare Function GetVersionEx Lib "kernel32" Alias "GetVersionExA"
 (lpVersionInfo As OSVERSIONINFO) As Long
Private Const VER_PLATFORM_WIN32_NT = 2

Private Declare Function CreateCompatibleDC Lib "gdi32" (ByVal hDC As Long) As
 Long
Private Declare Function ReleaseDC Lib "user32" (ByVal hWnd As Long, ByVal hDC
 As Long) As Long
Private Declare Function GetDC Lib "user32" (ByVal hWnd As Long) As Long
Private Declare Function GetDesktopWindow Lib "user32" () As Long
Private Declare Function SelectObject Lib "gdi32" (ByVal hDC As Long, ByVal
 hObject As Long) As Long
Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As
 Long
Private Declare Function GetObjectAPI Lib "gdi32" Alias "GetObjectA" (ByVal
 hObject As Long, ByVal nCount As Long, lpObject As Any) As Long
Private Declare Function DeleteDC Lib "gdi32" (ByVal hDC As Long) As Long
Private Declare Function BitBlt Lib "gdi32" (ByVal hDestDC As Long, ByVal x As
 Long, ByVal y As Long, ByVal nWidth As Long, ByVal nHeight As Long, ByVal
 hSrcDC As Long, ByVal xSrc As Long, ByVal ySrc As Long, ByVal dwRop As Long)
 As Long

Private Declare Function CreateDIBSection Lib "gdi32" _
    (ByVal hDC As Long, _
    pBitmapInfo As BITMAPINFO, _
    ByVal un As Long, _
    lplpVoid As Long, _
    ByVal handle As Long, _
    ByVal dw As Long) As Long

Private Type PictDesc
    cbSizeofStruct As Long
    picType As Long
    hImage As Long
    xExt As Long
    yExt As Long
End Type

Private Type GUID
    Data1 As Long
    Data2 As Integer
    Data3 As Integer
    Data4(0 To 7) As Byte
End Type

Private Declare Function OleCreatePictureIndirect Lib "OLEPRO32.DLL" ( _
      lpPictDesc As PictDesc, _
      riid As GUID, _
      ByVal fPictureOwnsHandle As Long, _
      ipic As IPicture _
    ) As Long
Private Type BITMAP
    bmType As Long
    bmWidth As Long
    bmHeight As Long
    bmWidthBytes As Long
    bmPlanes As Integer
    bmBitsPixel As Integer
    bmBits As Long
End Type
Private Const BI_RGB = 0&amp;
Private Const DIB_RGB_COLORS = 0 '  color table in RGBs

Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
    lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)

Private Type SAFEARRAYBOUND
    cElements As Long
    lLbound As Long
End Type
Private Type SAFEARRAY2D
    cDims As Integer
    fFeatures As Integer
    cbElements As Long
    cLocks As Long
    pvData As Long
    Bounds(0 To 1) As SAFEARRAYBOUND
End Type
Private Declare Function VarPtrArray Lib "msvbvm50.dll" Alias "VarPtr" (Ptr()
 As Any) As Long

Private Type RGBQUAD
    rgbBlue As Byte
    rgbGreen As Byte
    rgbRed As Byte
    rgbReserved As Byte
End Type
Private Type BITMAPINFOHEADER '40 bytes
    biSize As Long
    biWidth As Long
    biHeight As Long
    biPlanes As Integer
    biBitCount As Integer
    biCompression As Long
    biSizeImage As Long
    biXPelsPerMeter As Long
    biYPelsPerMeter As Long
    biClrUsed As Long
    biClrImportant As Long
End Type
Private Type BITMAPINFO
    bmiHeader As BITMAPINFOHEADER
    bmiColors As RGBQUAD
End Type

Private Type BLENDFUNCTION
  BlendOp As Byte
  BlendFlags As Byte
  SourceConstantAlpha As Byte
  AlphaFormat As Byte
End Type
' BlendOp:
Private Const AC_SRC_OVER = &amp;H0
' AlphaFormat:
Private Const AC_SRC_ALPHA = &amp;H1

Private Declare Function AlphaBlend Lib "msimg32.dll" ( _
  ByVal hdcDest As Long, _
  ByVal nXOriginDest As Long, _
  ByVal nYOriginDest As Long, _
  ByVal nWidthDest As Long, _
  ByVal nHeightDest As Long, _
  ByVal hDcSrc As Long, _
  ByVal nXOriginSrc As Long, _
  ByVal nYOriginSrc As Long, _
  ByVal nWidthSrc As Long, _
  ByVal nHeightSrc As Long, _
  ByVal lBlendFunction As Long _
) As Long

Private Declare Function SetTextColor Lib "gdi32" (ByVal hDC As Long, ByVal
 crColor As Long) As Long
Private Declare Function SetBkMode Lib "gdi32" (ByVal hDC As Long, ByVal
 nBkMode As Long) As Long
    Private Const OPAQUE = 2
    Private Const TRANSPARENT = 1
Private Declare Function DrawTextA Lib "user32" (ByVal hDC As Long, ByVal lpStr
 As String, ByVal nCount As Long, lpRect As RECT, ByVal wFormat As Long) As Long
Private Declare Function DrawTextW Lib "user32" (ByVal hDC As Long, ByVal lpStr
 As Long, ByVal nCount As Long, lpRect As RECT, ByVal wFormat As Long) As Long
    Private Const DT_LEFT = &amp;H0&amp;
    Private Const DT_TOP = &amp;H0&amp;
    Private Const DT_CENTER = &amp;H1&amp;
    Private Const DT_RIGHT = &amp;H2&amp;
    Private Const DT_VCENTER = &amp;H4&amp;
    Private Const DT_BOTTOM = &amp;H8&amp;
    Private Const DT_WORDBREAK = &amp;H10&amp;
    Private Const DT_SINGLELINE = &amp;H20&amp;
    Private Const DT_EXPANDTABS = &amp;H40&amp;
    Private Const DT_TABSTOP = &amp;H80&amp;
    Private Const DT_NOCLIP = &amp;H100&amp;
    Private Const DT_EXTERNALLEADING = &amp;H200&amp;
    Private Const DT_CALCRECT = &amp;H400&amp;
    Private Const DT_NOPREFIX = &amp;H800
    Private Const DT_INTERNAL = &amp;H1000&amp;
    Private Const DT_WORD_ELLIPSIS = &amp;H40000


Private m_hDib(0 To 1) As Long
Private m_hBmpOld(0 To 1) As Long
Private m_hDC(0 To 1) As Long
Private m_lPtr(0 To 1) As Long
Private m_tBI(0 To 1) As BITMAPINFO
Private m_lAlpha As Single

Private m_sAppName As String
Private m_sVersion As String
Private m_sCopyright As String
Private m_sCopyrightUrl As String
Private m_sTitle As String
Private m_sAcknowledgements As String
Private m_bShowing As Boolean

Private m_xDir() As Long
Private m_yDir() As Long

Private m_tOSV As OSVERSIONINFO


Private Sub SetAsUrl(ctl As Control, ByVal sUrl As String)
   If (Len(sUrl) &gt; 0) Then
      ctl.Tag = sUrl
      ctl.ForeColor = &amp;H800000
      ctl.FontUnderline = True
      ctl.MouseIcon = picLogo.MouseIcon
      ctl.MousePointer = 99
      If (Len(ctl.ToolTipText) = 0) Then
         ctl.ToolTipText = "Go to " &amp; sUrl
      End If
   Else
      If Len(ctl.Tag) &gt; 0 Then
         If (ctl.ToolTipText = "Go to " &amp; ctl.Tag) Then
            ctl.ToolTipText = ""
         End If
         ctl.Tag = ""
      End If
      ctl.ForeColor = vbWindowText
      ctl.FontUnderline = False
      ctl.MousePointer = vbDefault
   End If
End Sub

Private Sub UrlClick(ctl As Control)
   If Len(ctl.Tag) &gt; 0 Then
      ShellExecute Me.hWnd, "open", ctl.Tag, "", "", SW_SHOWNORMAL
   End If
End Sub

Public Property Get Acknowledgements() As String
   Acknowledgements = m_sAcknowledgements
End Property
Public Property Let Acknowledgements(ByVal sAcknowledgements As String)
   m_sAcknowledgements = sAcknowledgements
   If (m_bShowing) Then
      txtAcknowledgements = m_sAcknowledgements
   End If
End Property
Public Property Get Title() As String
   Title = m_sTitle
End Property
Public Property Let Title(ByVal sTitle As String)
   m_sTitle = sTitle
   If (m_bShowing) Then
      Me.Caption = m_sTitle
   End If
End Property
Public Property Get Copyright() As String
   Copyright = m_sCopyright
End Property
Public Property Let Copyright(ByVal sCopyright As String)
   m_sCopyright = sCopyright
   If (m_bShowing) Then
      lblCopyright.Caption = m_sCopyright
   End If
End Property
Public Property Get CopyrightUrl() As String
   CopyrightUrl = m_sCopyrightUrl
End Property
Public Property Let CopyrightUrl(ByVal sCopyrightUrl As String)
   m_sCopyrightUrl = sCopyrightUrl
   If (m_bShowing) Then
      SetAsUrl lblCopyright, m_sCopyrightUrl
   End If
End Property

Public Property Get Version() As String
   Version = m_sVersion
End Property
Public Property Let Version(ByVal sVersion As String)
   m_sVersion = sVersion
   If (m_bShowing) Then
      lblVersion.Caption = sVersion
   End If
End Property
Public Property Get AppName() As String
   AppName = m_sAppName
End Property
Public Property Let AppName(ByVal sAppName As String)
   m_sAppName = sAppName
   If (m_bShowing) Then
      lblAppName.Caption = m_sAppName
   End If
End Property


Private Sub cmdOK_Click()
   Unload Me
End Sub

Private Sub Form_Load()
   
   m_tOSV.dwVersionInfoSize = Len(m_tOSV)
   GetVersionEx m_tOSV
   
   ' Defaults:
   If Len(m_sAppName) = 0 Then
      m_sAppName = App.FileDescription
   End If
   If Len(m_sTitle) = 0 Then
      m_sTitle = App.Title
   End If
   If Len(m_sCopyright) = 0 Then
      m_sCopyright = App.LegalCopyright
   End If
   If Len(m_sVersion) = 0 Then
      m_sVersion = "Version: " &amp; App.Major &amp; "." &amp; App.Minor &amp; "." &amp;
       App.Revision
   End If
   If Len(m_sAcknowledgements) = 0 Then
      m_sAcknowledgements = App.Comments
   End If
   
   Me.Caption = m_sTitle
   lblAppName.Caption = m_sAppName
   lblCopyright.Caption = m_sCopyright
   SetAsUrl lblCopyright, m_sCopyrightUrl
   lblVersion.Caption = m_sVersion
   txtAcknowledgements.Text = m_sAcknowledgements
   
   If Me.Icon.handle = 0 Then
      Dim frm As Form
      For Each frm In Forms
         If (frm.BorderStyle = 2) Then
            Me.Icon = frm.Icon
            Exit For
         End If
      Next
   End If
   
   m_bShowing = True
   
End Sub

Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
   ClearUp 1
   ClearUp 0
End Sub

Private Sub Form_Resize()
   If (m_tOSV.dwMajorVersion &gt; 4) Or _
      (m_tOSV.dwMajorVersion = 4) And (m_tOSV.dwMinorVersion &gt;= 10) Then
      If (GetDeviceCaps(Me.hDC, BITSPIXEL) &gt; 8) Then
         Me.Cls
         Dim tR As RECT
         tR.left = 0
         tR.top = 0
         tR.right = Me.ScaleWidth
         tR.bottom = Me.ScaleHeight
         GradientFillTri Me.hDC, tR, _
            BlendColor(vb3DHighlight, vbButtonFace), vbButtonFace, _
            vbButtonFace, BlendColor(vbButtonFace, vbButtonShadow, 224)
         Me.Refresh
      End If
   End If
End Sub

Private Sub lblCopyright_Click()
   UrlClick lblCopyright
End Sub

Private Sub lblLinkTo_Click()
   UrlClick lblLinkTo
End Sub

Private Sub picLogo_Click()
   UrlClick picLogo
End Sub

Private Sub GradientFillTri( _
      ByVal lHDC As Long, _
      tR As RECT, _
      ByVal topLeftColor As OLE_COLOR, _
      ByVal topRightColor As OLE_COLOR, _
      ByVal bottomLeftColor As OLE_COLOR, _
      ByVal bottomRightColor As OLE_COLOR _
   )
Dim hBrush As Long
Dim lTopLeftColor As Long
Dim lTopRightColor As Long
Dim lBottomLeftColor As Long
Dim lBottomRightColor As Long
Dim lR As Long
   
   ' Use GradientFill:
   OleTranslateColor topLeftColor, 0, lTopLeftColor
   OleTranslateColor topRightColor, 0, lTopRightColor
   OleTranslateColor bottomLeftColor, 0, lBottomLeftColor
   OleTranslateColor bottomRightColor, 0, lBottomRightColor

   Dim tTV(0 To 3) As TRIVERTEX
   Dim tGR(0 To 1)  As GRADIENT_TRIANGLE
   
   setTriVertexColor tTV(0), lTopLeftColor
   tTV(0).x = tR.left
   tTV(0).y = tR.top
   
   setTriVertexColor tTV(1), lTopRightColor
   tTV(1).x = tR.right
   tTV(1).y = tR.top
   
   setTriVertexColor tTV(2), lBottomRightColor
   tTV(2).x = tR.right
   tTV(2).y = tR.bottom
   
   setTriVertexColor tTV(3), lBottomLeftColor
   tTV(3).x = tR.left
   tTV(3).y = tR.bottom
   
   tGR(0).Vertex1 = 0
   tGR(0).Vertex2 = 1
   tGR(0).Vertex3 = 2
    
   tGR(1).Vertex1 = 0
   tGR(1).Vertex2 = 2
   tGR(1).Vertex3 = 3
   
   GradientFillTriangle lHDC, tTV(0), 4, tGR(0), 2, GRADIENT_FILL_TRIANGLE
   
End Sub

Private Sub setTriVertexColor(tTV As TRIVERTEX, lColor As Long)
Dim lRed As Long
Dim lGreen As Long
Dim lBlue As Long
   lRed = (lColor And &amp;HFF&amp;) * &amp;H100&amp;
   lGreen = (lColor And &amp;HFF00&amp;)
   lBlue = (lColor And &amp;HFF0000) \ &amp;H100&amp;
   setTriVertexColorComponent tTV.Red, lRed
   setTriVertexColorComponent tTV.Green, lGreen
   setTriVertexColorComponent tTV.Blue, lBlue
End Sub
Private Sub setTriVertexColorComponent(ByRef iColor As Integer, ByVal
 lComponent As Long)
   If (lComponent And &amp;H8000&amp;) = &amp;H8000&amp; Then
      iColor = (lComponent And &amp;H7F00&amp;)
      iColor = iColor Or &amp;H8000
   Else
      iColor = lComponent
   End If
End Sub


Private Sub picNothing_Click()
   If (GetDeviceCaps(Me.hDC, BITSPIXEL) &gt; 8) Then
      If Not (tmr.Enabled) Then
         CreateFromHBitmap 0, picLogo.Picture.handle
         Create 1, m_tBI(0).bmiHeader.biWidth, m_tBI(0).bmiHeader.biHeight
         LoadPictureBlt 1, m_hDC(0)
         picLogo.Visible = False
         m_lAlpha = 255
         tmr_Timer
         tmr.Enabled = True
      Else
         picLogo.Visible = True
         tmr.Enabled = False
      End If
   End If
End Sub

Private Sub tmr_Timer()
Dim tSAIn As SAFEARRAY2D
Dim bDibIn() As Byte
Dim tSAOut As SAFEARRAY2D
Dim bDibOut() As Byte
Static lIndexFrom As Long
Static lIndexTo As Long

Dim xEnd As Long, yEnd As Long
Dim x As Long, y As Long, y2 As Long, x2 As Long

   lIndexFrom = lIndexTo
   If (lIndexFrom = 1) Then
      lIndexTo = 0
   Else
      lIndexTo = 1
   End If
   '
   ' Get the bits in the from DIB section:
   With tSAIn
       .cbElements = 1
       .cDims = 2
       .Bounds(0).lLbound = 0
       .Bounds(0).cElements = m_tBI(lIndexFrom).bmiHeader.biHeight
       .Bounds(1).lLbound = 0
       .Bounds(1).cElements = 4 * m_tBI(lIndexFrom).bmiHeader.biWidth
       .pvData = m_lPtr(lIndexFrom)
   End With
   CopyMemory ByVal VarPtrArray(bDibIn()), VarPtr(tSAIn), 4

   With tSAOut
       .cbElements = 1
       .cDims = 2
       .Bounds(0).lLbound = 0
       .Bounds(0).cElements = m_tBI(lIndexTo).bmiHeader.biHeight
       .Bounds(1).lLbound = 0
       .Bounds(1).cElements = 4 * m_tBI(lIndexTo).bmiHeader.biWidth
       .pvData = m_lPtr(lIndexTo)
   End With
   CopyMemory ByVal VarPtrArray(bDibOut()), VarPtr(tSAOut), 4

   xEnd = tSAIn.Bounds(1).cElements - 4
   yEnd = tSAIn.Bounds(0).cElements - 1
   For y = yEnd To 0 Step -1
      For x = 0 To xEnd Step 4
         
         y2 = y + (Rnd * 4) - 2
         If (y2 &lt; 0) Then
            y2 = yEnd + y2
         End If
         If (y2 &gt; yEnd) Then
            y2 = y2 - yEnd - 1
         End If
         
         x2 = x + Int(Rnd * 4) * 4 - 64
         If (x2 &lt; 0) Then
            x2 = xEnd + x2
         End If
         If (x2 &gt; xEnd) Then
            x2 = x2 - xEnd - 4
         End If
         
         bDibOut(x, y) = (14&amp; * bDibIn(x, y)) \ 16&amp; + (1&amp; * bDibIn(x2, y2) \
          16&amp;) + 16&amp;
         bDibOut(x + 1, y) = (14&amp; * bDibIn(x + 1, y)) \ 16&amp; + (1&amp; * bDibIn(x2 +
          1, y2) \ 16&amp;) + 16&amp;
         bDibOut(x + 2, y) = (14&amp; * bDibIn(x + 2, y)) \ 16&amp; + (1&amp; * bDibIn(x2 +
          2, y2) \ 16&amp;) + 16&amp;
         
      Next x
   Next y
   
   ' Clear the temporary array descriptor
   CopyMemory ByVal VarPtrArray(bDibIn), 0&amp;, 4
   CopyMemory ByVal VarPtrArray(bDibOut), 0&amp;, 4
   
   picNothing.Cls
   Dim lTextAlpha As Long
   Dim tR As RECT
   tR.left = 87
   tR.right = 87 + m_tBI(0).bmiHeader.biWidth
   tR.top = 1
   tR.bottom = 1 + m_tBI(0).bmiHeader.biHeight
   lTextAlpha = m_lAlpha + 128
   If (lTextAlpha &lt;= 255) Then
      SetBkMode picNothing.hDC, TRANSPARENT
      SetTextColor picNothing.hDC, BlendColor(vbButtonShadow, vb3DHighlight,
       lTextAlpha)
      DrawText picNothing.hDC, "vbAccelerator is a site providing advanced,
       free source code for VB, C# and VB.NET programmers.  Specialities are
       controls, user interface and imaging.  Everything is free and comes
       complete with full source code.", tR, DT_CENTER Or DT_VCENTER Or
       DT_WORDBREAK
      If (lTextAlpha &lt; 64) And (lblLinkTo.Tag = "") Then
         lblLinkTo.Caption = "http://vbaccelerator.com/"
         SetAsUrl lblLinkTo, "http://vbaccelerator.com/"
         lblLinkTo.ForeColor = BlendColor(vbButtonShadow, vb3DHighlight,
          lTextAlpha)
      End If
   End If
   If (m_lAlpha &gt; 0) Then
   
      Dim lBlend As Long
      Dim bf As BLENDFUNCTION
      bf.BlendOp = AC_SRC_OVER
      bf.BlendFlags = 0
      bf.SourceConstantAlpha = m_lAlpha
      bf.AlphaFormat = 0
      CopyMemory lBlend, bf, 4
      AlphaBlend picNothing.hDC, _
         87, 1, m_tBI(0).bmiHeader.biWidth, m_tBI(0).bmiHeader.biHeight, _
         m_hDC(lIndexTo), _
         0, 0, m_tBI(0).bmiHeader.biWidth, m_tBI(0).bmiHeader.biHeight, _
         lBlend
   End If
   
   m_lAlpha = m_lAlpha - 4
   If (m_lAlpha &lt; -128) Then
      tmr.Enabled = False
   End If
   picNothing.Refresh
   '
End Sub

Private Function CreateFromHBitmap( _
      ByVal lIndex As Long, _
      ByVal hBmp As Long _
   )
Dim lHDC As Long
Dim lhWnd As Long
Dim lhDCDesktop As Long
Dim lhBmpOld As Long
Dim tBmp As BITMAP
   GetObjectAPI hBmp, Len(tBmp), tBmp
   If (Create(lIndex, tBmp.bmWidth, tBmp.bmHeight)) Then
      lhWnd = GetDesktopWindow()
      lhDCDesktop = GetDC(lhWnd)
      If (lhDCDesktop &lt;&gt; 0) Then
         lHDC = CreateCompatibleDC(lhDCDesktop)
         ReleaseDC lhWnd, lhDCDesktop ' 2003-07-05: Corrected for GDI leak in
          Win98
         If (lHDC &lt;&gt; 0) Then
            lhBmpOld = SelectObject(lHDC, hBmp)
            LoadPictureBlt lIndex, lHDC
            SelectObject lHDC, lhBmpOld
            DeleteDC lHDC
         End If
      End If
   End If
   
End Function

Private Function Create( _
        ByVal lIndex As Long, _
        ByVal lWidth As Long, _
        ByVal lHeight As Long _
    ) As Boolean
   ClearUp lIndex
   m_hDC(lIndex) = CreateCompatibleDC(Me.hDC)
   If (m_hDC(lIndex) &lt;&gt; 0) Then
       If (CreateDIB(lIndex, m_hDC(lIndex), lWidth, lHeight, m_hDib(lIndex)))
        Then
           m_hBmpOld(lIndex) = SelectObject(m_hDC(lIndex), m_hDib(lIndex))
           Create = True
       Else
           DeleteDC m_hDC(lIndex)
           m_hDC(lIndex) = 0
       End If
   End If
End Function

Private Sub LoadPictureBlt( _
        ByVal lIndex As Long, _
        ByVal lHDC As Long, _
        Optional ByVal lSrcLeft As Long = 0, _
        Optional ByVal lSrcTop As Long = 0, _
        Optional ByVal lSrcWidth As Long = -1, _
        Optional ByVal lSrcHeight As Long = -1, _
        Optional ByVal eRop As RasterOpConstants = vbSrcCopy _
    )
    If lSrcWidth &lt; 0 Then lSrcWidth = m_tBI(lIndex).bmiHeader.biWidth
    If lSrcHeight &lt; 0 Then lSrcHeight = m_tBI(lIndex).bmiHeader.biHeight
    BitBlt m_hDC(lIndex), 0, 0, lSrcWidth, lSrcHeight, lHDC, lSrcLeft, lSrcTop,
     eRop
End Sub

Private Sub ClearUp(ByVal lIndex As Long)
   If (m_hDC(lIndex) &lt;&gt; 0) Then
      If (m_hDib(lIndex) &lt;&gt; 0) Then
         SelectObject m_hDC(lIndex), m_hBmpOld(lIndex)
         DeleteObject m_hDib(lIndex)
      End If
      DeleteObject m_hDC(lIndex)
   End If
   m_hDC(lIndex) = 0: m_hDib(lIndex) = 0: m_hBmpOld(lIndex) = 0: m_lPtr(lIndex)
    = 0
End Sub

Private Function CreateDIB( _
      ByVal lIndex As Long, _
      ByVal lHDC As Long, _
      ByVal lWidth As Long, _
      ByVal lHeight As Long, _
      ByRef hDib As Long _
   ) As Boolean
   With m_tBI(lIndex).bmiHeader
      .biSize = Len(m_tBI(lIndex).bmiHeader)
      .biWidth = lWidth
      .biHeight = lHeight
      .biPlanes = 1
      .biBitCount = 32
      .biCompression = BI_RGB
      .biSizeImage = lWidth * 4 * lHeight
   End With
   hDib = CreateDIBSection( _
         lHDC, _
         m_tBI(lIndex), _
         DIB_RGB_COLORS, _
         m_lPtr(lIndex), _
         0, 0)
   CreateDIB = (hDib &lt;&gt; 0)
End Function

Private Property Get BlendColor( _
      ByVal oColorFrom As OLE_COLOR, _
      ByVal oColorTo As OLE_COLOR, _
      Optional ByVal Alpha As Long = 128 _
   ) As Long
Dim lCFrom As Long
Dim lCTo As Long
   OleTranslateColor oColorFrom, 0, lCFrom
   OleTranslateColor oColorTo, 0, lCTo
Dim lSrcR As Long
Dim lSrcG As Long
Dim lSrcB As Long
Dim lDstR As Long
Dim lDstG As Long
Dim lDstB As Long
   lSrcR = lCFrom And &amp;HFF
   lSrcG = (lCFrom And &amp;HFF00&amp;) \ &amp;H100&amp;
   lSrcB = (lCFrom And &amp;HFF0000) \ &amp;H10000
   lDstR = lCTo And &amp;HFF
   lDstG = (lCTo And &amp;HFF00&amp;) \ &amp;H100&amp;
   lDstB = (lCTo And &amp;HFF0000) \ &amp;H10000
     
   
   BlendColor = RGB( _
      ((lSrcR * Alpha) / 255) + ((lDstR * (255 - Alpha)) / 255), _
      ((lSrcG * Alpha) / 255) + ((lDstG * (255 - Alpha)) / 255), _
      ((lSrcB * Alpha) / 255) + ((lDstB * (255 - Alpha)) / 255) _
      )
      
End Property

Private Function DrawText(ByVal hDC As Long, ByVal sText As String, rc As RECT,
 ByVal lFlags As Long)
Dim lPtr As Long
   If (m_tOSV.dwPlatformId = VER_PLATFORM_WIN32_NT) Then
      lPtr = StrPtr(sText)
      If Not (lPtr = 0) Then
         DrawTextW hDC, ByVal lPtr, -1, rc, lFlags
      End If
   Else
      DrawTextA hDC, sText, -1, rc, lFlags
   End If

End Function
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">Controls</a>&#160;.&#160;<a href="../index.html">S Grid 2</a>&#160;.&#160;<a href="article.html">MusicLibrary SGrid Sample</a>&#160;.&#160;<a href="VB6_Music_Library.html">VB6 Music Library</a>&#160;.&#160;frmAbout.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2004 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 1 February 2004</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/Controls/S_Grid_2/Music_Library/VB6_Music_Library_zip_frmAbout_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:30:06 GMT -->
</html>