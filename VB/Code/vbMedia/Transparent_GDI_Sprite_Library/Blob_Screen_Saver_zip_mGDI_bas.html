<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/Transparent_GDI_Sprite_Library/Blob_Screen_Saver_zip_mGDI_bas.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:48 GMT -->
<head>
<title>vbAccelerator - Contents of code file: mGDI.bas</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: mGDI.bas" /><link rel="stylesheet" href="../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../index.html">VB</a>&#160;.&#160;<a href="../../index.html">Code</a>&#160;.&#160;<a href="../index.html">vbMedia</a>&#160;.&#160;<a href="article.html">Transparent GDI Sprite Library</a>&#160;.&#160;<a href="Blob_Screen_Saver.html">Blob Screen Saver</a>&#160;.&#160;mGDI.bas</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="Blob_Screen_Saver.html"><img src="../../../../res/get.png" width="8" height="8" alt="Download Page" />Blob Screen Saver</a> (266K)</p><p /><p class="nav"><a href="Simple_Transparent_Sprites_Demo.html"><img src="../../../../res/get.png" width="8" height="8" alt="Download Page" />Simple Transparent Sprites Demo</a> (35K)</p><p /><p class="nav"><a href="Transparent_Sprite_Classes.html"><img src="../../../../res/get.png" width="8" height="8" alt="Download Page" />Transparent Sprite Classes</a> (8K)</p><br /><br /><img src="../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:12055</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=12055&amp;type=Zip&amp;title=Blob%20Screen%20Saver%2Ezip%5FmGDI">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../res/update.png" width="8" height="8" alt="Update" />24 Jun 1998<br />First Posted</p><br /><br /><img src="../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><br /><br /><img src="../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: mGDI.bas</h1><pre>Attribute VB_Name = "mGDI"
Option Explicit

' API Declares:

' This is most useful but Win32 only.  Particularly try the
' LOADMAP3DCOLORS for a quick way to sort out those
' embarassing gray backgrounds in your fixed bitmaps!
Declare Function LoadImage Lib "user32" Alias "LoadImageA" ( _
    ByVal hInst As Long, _
    ByVal lpsz As String, _
    ByVal un1 As Long, _
    ByVal n1 As Long, ByVal n2 As Long, _
    ByVal un2 As Long _
    ) As Long
Public Const IMAGE_BITMAP = 0
Public Const IMAGE_ICON = 1
Public Const IMAGE_CURSOR = 2
Public Const LR_COLOR = &amp;H2
Public Const LR_COPYDELETEORG = &amp;H8
Public Const LR_COPYFROMRESOURCE = &amp;H4000
Public Const LR_COPYRETURNORG = &amp;H4
Public Const LR_CREATEDIBSEm_cTileION = &amp;H2000
Public Const LR_DEFAULTCOLOR = &amp;H0
Public Const LR_DEFAULTSIZE = &amp;H40
Public Const LR_LOADFROMFILE = &amp;H10
Public Const LR_LOADMAP3DCOLORS = &amp;H1000
Public Const LR_LOADTRANSPARENT = &amp;H20
Public Const LR_MONOCHROME = &amp;H1
Public Const LR_SHARED = &amp;H8000

' Creates a memory DC
Declare Function CreateCompatibleDC Lib "gdi32" ( _
    ByVal hDC As Long _
    ) As Long
' Creates a bitmap in memory:
Declare Function CreateCompatibleBitmap Lib "gdi32" ( _
    ByVal hDC As Long, _
    ByVal nWidth As Long, ByVal nHeight As Long _
    ) As Long
' Places a GDI Object into DC, returning the previous one:
Declare Function SelectObject Lib "gdi32" _
    (ByVal hDC As Long, ByVal hObject As Long _
    ) As Long
' Deletes a GDI Object:
Declare Function DeleteObject Lib "gdi32" _
    (ByVal hObject As Long _
    ) As Long
' Copies Bitmaps from one DC to another, can also perform
' raster operations during the transfer:
Declare Function BitBlt Lib "gdi32" ( _
    ByVal hDestDC As Long, _
    ByVal X As Long, ByVal Y As Long, _
    ByVal nWidth As Long, ByVal nHeight As Long, _
    ByVal hSrcDC As Long, _
    ByVal xSrc As Long, ByVal ySrc As Long, _
    ByVal dwRop As Long _
    ) As Long
Public Const SRCCOPY = &amp;HCC0020
Public Const SRCAND = &amp;H8800C6
Public Const SRCPAINT = &amp;HEE0086
Public Const SRCINVERT = &amp;H660046

' Strum_cTileure used to hold bitmap information about Bitmaps
' created using GDI in memory:
Type Bitmap
    bmType As Long
    bmWidth As Long
    bmHeight As Long
    bmWidthBytes As Long
    bmPlanes As Integer
    bmBitsPixel As Integer
    bmBits As Long
End Type
' Get information relating to a GDI Object
Declare Function GetObjectAPI Lib "gdi32" Alias "GetObjectA" ( _
    ByVal hObject As Long, _
    ByVal nCount As Long, _
    lpObject As Any _
    ) As Long
' The traditional RECTangle strum_cTileure:
Type RECT
    left As Long
    Top As Long
    Right As Long
    Bottom As Long
End Type
' Fills a RECTangle in a DC with a specified brush
Declare Function FillRect Lib "user32" ( _
    ByVal hDC As Long, _
    lpRect As RECT, _
    ByVal hBrush As Long _
    ) As Long
' Create a brush of a certain colour:
Declare Function CreateSolidBrush Lib "gdi32" ( _
    ByVal crColor As Long _
    ) As Long
Public Const COLOR_BTNFACE = 15
Declare Function SetBkColor Lib "gdi32" (ByVal hDC As Long, ByVal crColor As
 Long) As Long
Declare Function GetDesktopWindow Lib "user32" () As Long
Declare Function GetDC Lib "user32" (ByVal hwnd As Long) As Long
Declare Function ReleaseDC Lib "user32" (ByVal hwnd As Long, ByVal hDC As Long)
 As Long
Declare Function GetSysColor Lib "user32" (ByVal nIndex As Long) As Long
Declare Function timeGetTime Lib "winmm.dll" () As Long

Public Function GDIMakeDCAndBitmap( _
        ByVal bMono As Boolean, _
        ByRef hDC As Long, _
        ByRef hBMP As Long, _
        ByRef hBmpOld As Long, _
        ByVal lDX As Long, _
        ByVal lDY As Long _
    ) As Boolean
' **********************************************************
' GDI Helper function: Makes a bitmap of a specified size
' and creates a DC to hold it.
' **********************************************************
Dim lCDC As Long
Dim lhWnd As Long

    ' Initialise byref variables:
    hDC = 0: hBMP = 0: hBmpOld = 0
    ' Create the DC from the basis DC:
    If (bMono) Then
        lCDC = 0
    Else
        lhWnd = GetDesktopWindow()
        lCDC = GetDC(lhWnd)
    End If
    hDC = CreateCompatibleDC(lCDC)
    If (bMono) Then
        lCDC = hDC
    End If
    
    If (hDC &lt;&gt; 0) Then
        ' If we get one, then time to make the bitmap:
        hBMP = CreateCompatibleBitmap(lCDC, lDX, lDY)
        ' If we succeed in creating the bitmap:
        If (hBMP &lt;&gt; 0) Then
            ' Select the bitmap into the memory DC and
            ' store the bitmap that was there before (need
            ' to do this because you need to Select this
            ' bitmap back into the DC before deleting
            ' the new Bitmap):
            hBmpOld = SelectObject(hDC, hBMP)
            ' Success:
            GDIMakeDCAndBitmap = True
        End If
    End If
    
    If Not (bMono) Then
        ReleaseDC lhWnd, lCDC
    End If

End Function
Public Function GDILoadBitmapIntoDC( _
        ByVal bMono As Boolean, _
        ByVal sFileName As String, _
        ByRef hDC As Long, _
        ByRef hBMP As Long, _
        ByRef hBmpOld As Long _
    ) As Boolean
' **********************************************************
' GDI Helper function: Loads a bitmap from file and Selects
' it into a memory DC.
' **********************************************************
Dim hInst As Long
Dim hDCBasis As Long
Dim lhWnd As Long

    ' Initialise byref variables:
    hDC = 0: hBMP = 0: hBmpOld = 0
    
    ' Now load the sprite bitmap:
    hInst = App.hInstance
    ' This is the quick, diRECT way where we don't get
    ' any extra copies of the bitmaps, as compared to
    ' using the VB pim_cTileure Object:
    hBMP = LoadImage(hInst, sFileName, IMAGE_BITMAP, 0, 0, LR_LOADFROMFILE)
    If (hBMP &lt;&gt; 0) Then
        ' Create a DC to hold the sprite, and Select
        ' the sprite into it:
        If (bMono) Then
            hDCBasis = 0
        Else
            lhWnd = GetDesktopWindow()
            hDCBasis = GetDC(lhWnd)
        End If
        hDC = CreateCompatibleDC(hDCBasis)
        If (hDC &lt;&gt; 0) Then
            ' If DC Is created, Select the bitmap into it:
            hBmpOld = SelectObject(hDC, hBMP)
            GDILoadBitmapIntoDC = True
        End If
        If Not (bMono) Then
            ReleaseDC lhWnd, hDCBasis
        End If
    End If

End Function
Public Function GDILoadPictureIntoDC( _
        ByVal bMono As Boolean, _
        ByRef oPic As StdPicture, _
        ByRef hDC As Long, _
        ByRef hBMP As Long, _
        ByRef hBmpOld As Long _
    ) As Boolean
' **********************************************************
' GDI Helper function: Creates a memory DC containing a new
' copy of bitmap from a StdPim_cTileure.
' **********************************************************
Dim hInst As Long
Dim hDCBasis As Long
Dim lhWnd As Long
Dim hdcTemp As Long
Dim hBmpTemp As Long
Dim hBmpTempOld As Long

    ' Initialise byref variables:
    hDC = 0: hBMP = 0: hBmpOld = 0
        
    ' Create a DC to hold the sprite, and Select
    ' the sprite into it:
    If (bMono) Then
        hDCBasis = 0
    Else
        lhWnd = GetDesktopWindow()
        hDCBasis = GetDC(lhWnd)
    End If
    hdcTemp = CreateCompatibleDC(hDCBasis)
    If (bMono) Then
        hDCBasis = hdcTemp
    End If
    
    If (hdcTemp &lt;&gt; 0) Then
        hBmpTempOld = SelectObject(hdcTemp, oPic.Handle)
    
        hDC = CreateCompatibleDC(hDCBasis)
        If (hDC &lt;&gt; 0) Then
            ' If we get one, then time to make the bitmap:
            Dim tBM As Bitmap
            GetObjectAPI oPic.Handle, Len(tBM), tBM
            tBM.bmHeight = tBM.bmHeight
            hBMP = CreateCompatibleBitmap(hDCBasis, tBM.bmWidth, tBM.bmHeight)
            If (hBMP &lt;&gt; 0) Then
                hBmpOld = SelectObject(hDC, hBMP)
                
                BitBlt hDC, 0, 0, tBM.bmWidth, tBM.bmHeight, hdcTemp, 0, 0,
                 SRCCOPY
                
                GDILoadPictureIntoDC = True
            End If
        End If
        
        SelectObject hdcTemp, hBmpTempOld
        DeleteObject hdcTemp
        
    End If
    If Not (bMono) Then
        ReleaseDC lhWnd, hDCBasis
    End If

End Function

Public Sub GDIClearDCBitmap( _
        ByRef hDC As Long, _
        ByRef hBMP As Long, _
        ByVal hBmpOld As Long _
    )
' **********************************************************
' GDI Helper function: Goes through the steps required
' to clear up a bitmap within a DC.
' **********************************************************
    ' If we have a valid DC:
    If (hDC &lt;&gt; 0) Then
        ' If there is a valid bitmap in it:
        If (hBMP &lt;&gt; 0) Then
            ' Select the original bitmap into the DC:
            SelectObject hDC, hBmpOld
            ' Now delete the unreferenced bitmap:
            DeleteObject hBMP
            ' Byref so set the value to invalid BMP:
            hBMP = 0
        End If
        ' Delete the memory DC:
        DeleteObject hDC
        ' Byref so set the value to invalid DC:
        hDC = 0
    End If
End Sub


</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../index.html">VB</a>&#160;.&#160;<a href="../../index.html">Code</a>&#160;.&#160;<a href="../index.html">vbMedia</a>&#160;.&#160;<a href="article.html">Transparent GDI Sprite Library</a>&#160;.&#160;<a href="Blob_Screen_Saver.html">Blob Screen Saver</a>&#160;.&#160;mGDI.bas</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 28 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/Transparent_GDI_Sprite_Library/Blob_Screen_Saver_zip_mGDI_bas.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:48 GMT -->
</html>