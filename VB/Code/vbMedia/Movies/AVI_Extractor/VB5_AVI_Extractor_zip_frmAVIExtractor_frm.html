<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/Movies/AVI_Extractor/VB5_AVI_Extractor_zip_frmAVIExtractor_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:48 GMT -->
<head>
<title>vbAccelerator - Contents of code file: frmAVIExtractor.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: frmAVIExtractor.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">vbMedia</a>&#160;.&#160;<a href="../index.html">Movies</a>&#160;.&#160;<a href="article.html">AVI Frame Extractor</a>&#160;.&#160;<a href="VB5_AVI_Extractor.html">VB5 AVI Extractor</a>&#160;.&#160;frmAVIExtractor.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_AVI_Extractor.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 AVI Extractor</a> (73K)</p><p /><p class="nav"><a href="VB6_AVI_Extractor.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 AVI Extractor</a> (70K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:13650</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=13650&amp;type=Zip&amp;title=VB5%20AVI%20Extractor%2Ezip%5FfrmAVIExtractor">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" />﻿<p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />23 Nov 2003<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3910">Transparent AVI Player Control</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3909">ComCtl32.DLL Animation Control Class</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2522">Reading Data from Local or External Library Resources</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: frmAVIExtractor.frm</h1><pre>VERSION 5.00
Begin VB.Form frmAVIExtractor 
   Caption         =   "vbAccelerator AVI Frame Extractor"
   ClientHeight    =   7740
   ClientLeft      =   2520
   ClientTop       =   2550
   ClientWidth     =   7500
   BeginProperty Font 
      Name            =   "Tahoma"
      Size            =   8.25
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "frmAVIExtractor.frx":0000
   LinkTopic       =   "Form1"
   ScaleHeight     =   7740
   ScaleWidth      =   7500
   Begin VB.PictureBox picTab 
      BorderStyle     =   0  'None
      Height          =   7635
      Index           =   0
      Left            =   60
      ScaleHeight     =   7635
      ScaleWidth      =   7395
      TabIndex        =   23
      TabStop         =   0   'False
      Top             =   60
      Width           =   7395
      Begin VB.PictureBox picPalette 
         AutoRedraw      =   -1  'True
         Height          =   1275
         Left            =   2160
         ScaleHeight     =   81
         ScaleMode       =   3  'Pixel
         ScaleWidth      =   341
         TabIndex        =   27
         Top             =   6240
         Width           =   5175
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   8
         Left            =   0
         TabIndex        =   24
         Top             =   5820
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   7
         Left            =   0
         TabIndex        =   18
         Top             =   5220
         Width           =   1995
      End
      Begin VB.TextBox txtAVIFile 
         Height          =   315
         Left            =   0
         TabIndex        =   1
         Top             =   300
         Width           =   6975
      End
      Begin VB.CommandButton cmdPick 
         Caption         =   "..."
         Height          =   315
         Left            =   7020
         TabIndex        =   2
         Top             =   300
         Width           =   375
      End
      Begin VB.CheckBox chkTransparent 
         Caption         =   "&amp;Transparent"
         Height          =   255
         Left            =   2160
         TabIndex        =   21
         Top             =   1380
         Width           =   4095
      End
      Begin VB.PictureBox picFrame 
         AutoRedraw      =   -1  'True
         Height          =   4215
         Left            =   2160
         ScaleHeight     =   4155
         ScaleWidth      =   5115
         TabIndex        =   22
         Top             =   1680
         Width           =   5175
      End
      Begin VB.ComboBox cboFrame 
         Height          =   315
         Left            =   2160
         Style           =   2  'Dropdown List
         TabIndex        =   20
         Top             =   1020
         Width           =   5175
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   6
         Left            =   0
         TabIndex        =   16
         Top             =   4620
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   5
         Left            =   0
         TabIndex        =   14
         Top             =   4020
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   4
         Left            =   0
         TabIndex        =   12
         Top             =   3420
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   3
         Left            =   0
         TabIndex        =   10
         Top             =   2820
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   2
         Left            =   0
         TabIndex        =   8
         Top             =   2220
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   1
         Left            =   0
         TabIndex        =   6
         Top             =   1620
         Width           =   1995
      End
      Begin VB.TextBox txtInfo 
         Height          =   315
         Index           =   0
         Left            =   0
         TabIndex        =   4
         Top             =   1020
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " Pa&amp;lette:"
         Height          =   255
         Index           =   12
         Left            =   2160
         TabIndex        =   26
         Top             =   6000
         Width           =   4095
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " T&amp;ype FourCC"
         Height          =   255
         Index           =   9
         Left            =   0
         TabIndex        =   25
         Top             =   5580
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " Handler Fo&amp;urCC"
         Height          =   255
         Index           =   7
         Left            =   0
         TabIndex        =   17
         Top             =   4980
         Width           =   1995
      End
      Begin VB.Line linSep 
         BorderColor     =   &amp;H80000010&amp;
         X1              =   0
         X2              =   7320
         Y1              =   720
         Y2              =   720
      End
      Begin VB.Label lblFilename 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;AVI File"
         Height          =   255
         Left            =   0
         TabIndex        =   0
         Top             =   0
         Width           =   7335
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " F&amp;rame:"
         Height          =   255
         Index           =   8
         Left            =   2160
         TabIndex        =   19
         Top             =   780
         Width           =   4095
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Compression"
         Height          =   255
         Index           =   6
         Left            =   0
         TabIndex        =   15
         Top             =   4380
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Name"
         Height          =   255
         Index           =   5
         Left            =   0
         TabIndex        =   13
         Top             =   3780
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Height"
         Height          =   255
         Index           =   4
         Left            =   0
         TabIndex        =   11
         Top             =   3180
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Width"
         Height          =   255
         Index           =   3
         Left            =   0
         TabIndex        =   9
         Top             =   2580
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Bits/Pixel"
         Height          =   255
         Index           =   2
         Left            =   0
         TabIndex        =   7
         Top             =   1980
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " Frame &amp;Duration"
         Height          =   255
         Index           =   1
         Left            =   0
         TabIndex        =   5
         Top             =   1380
         Width           =   1995
      End
      Begin VB.Label lblInfo 
         BackStyle       =   0  'Transparent
         Caption         =   " &amp;Frames"
         Height          =   255
         Index           =   0
         Left            =   0
         TabIndex        =   3
         Top             =   780
         Width           =   1995
      End
   End
   Begin VB.Menu mnuFileTOP 
      Caption         =   "&amp;File"
      Begin VB.Menu mnuFile 
         Caption         =   "&amp;Open..."
         Index           =   0
         Shortcut        =   ^O
      End
      Begin VB.Menu mnuFile 
         Caption         =   "-"
         Index           =   1
      End
      Begin VB.Menu mnuFile 
         Caption         =   "&amp;Save Frame..."
         Index           =   2
         Shortcut        =   ^S
      End
      Begin VB.Menu mnuFile 
         Caption         =   "Save &amp;Bitmap Strip..."
         Index           =   3
      End
      Begin VB.Menu mnuFile 
         Caption         =   "Save &amp;Palette..."
         Index           =   4
      End
      Begin VB.Menu mnuFile 
         Caption         =   "-"
         Index           =   5
      End
      Begin VB.Menu mnuFile 
         Caption         =   "E&amp;xit"
         Index           =   6
      End
   End
End
Attribute VB_Name = "frmAVIExtractor"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private m_cAVI As cAVIFrameExtract

Private Sub pLoadDialog()
   Dim cD As New cCommonDialog
   Dim sFile As String
   If (cD.VBGetOpenFileName( _
      sFile, _
      Filter:="AVI Files (*.AVI)|*.AVI|All Files (*.*)|*.*", _
      FilterIndex:=1, _
      DefaultExt:="AVI", _
      Owner:=Me.hWnd)) Then
      pLoad sFile
   End If
End Sub

Private Sub pSaveFrame()
Dim cD As New cCommonDialog
Dim sFile As String
   
   sFile = pParseName(m_cAVI.Name) &amp; "_" &amp; cboFrame.ListIndex + 1 &amp; ".bmp"
   If (cD.VBGetSaveFileName( _
      sFile, _
      Filter:="Bitmap Files (*.BMP)|*.BMP|All Files (*.*)|*.*", _
      FilterIndex:=1, _
      DefaultExt:="BMP", _
      Owner:=Me.hWnd)) Then
      
      Dim hBmp As Long
      hBmp = m_cAVI.FrameBitmap(cboFrame.ListIndex + 1)
      
      Dim cDS As New cDIBSectionSave
      If (m_cAVI.bitsPerPixel = 8) Then
         cDS.Save8BitBitmap sFile, hBmp, m_cAVI.Palette, True
      Else
         cDS.Save24BitBitmap sFile, hBmp
      End If
      
      
      
   End If
   
End Sub

Private Function pParseName(ByVal sName As String) As String
Dim i As Long
Dim sChar As String
Dim sRet As String
   For i = 1 To Len(sName)
      sChar = Mid(sName, i, 1)
      Select Case sChar
      Case "&amp;"
         sRet = sRet &amp; "_"
      Case "?"
         sRet = sRet &amp; " "
      Case ":"
         sRet = sRet &amp; "_"
      Case "*"
         sRet = sRet &amp; "_"
      Case """"
      Case "'"
      Case "#"
         sRet = sRet &amp; "_"
      Case "."
         sRet = sRet &amp; "_"
      Case Else
         sRet = sRet &amp; sChar
      End Select
   Next i
   pParseName = sRet
End Function

Private Sub pSaveBitmapStrip()
Dim cD As New cCommonDialog
Dim sFile As String
Dim fD As New frmBitmapStripDialog
   
   fD.AVI = m_cAVI
   
   fD.Show vbModal, Me
   If Not (fD.Cancelled) Then
      sFile = pParseName(m_cAVI.Name)
      If (cD.VBGetSaveFileName( _
         sFile, _
         Filter:="Bitmap Files (*.BMP)|*.BMP|All Files (*.*)|*.*", _
         FilterIndex:=1, _
         DefaultExt:="BMP", _
         Owner:=Me.hWnd)) Then
         
         pCreateBitmapStripAndSave _
            sFile, fD.startFrame, fD.endFrame, fD.NewBackColor
         
      End If
   End If
End Sub

Private Sub pCreateBitmapStripAndSave( _
      ByVal sFileName As String, _
      ByVal startFrame As Long, _
      ByVal endFrame As Long, _
      ByVal oBackColor As OLE_COLOR _
   )
Dim cDC As New cMemDC
   cDC.Create
Dim cB As New cBmp
   cB.Create m_cAVI.Width, m_cAVI.Height * (endFrame - startFrame + 1)
   cDC.SelectObject cB
   
   If (oBackColor &lt;&gt; -1) Then
      cDC.Fill oBackColor
   End If
   
Dim iFrame As Long
Dim y As Long
   For iFrame = startFrame To endFrame
      m_cAVI.DrawFrame cDC.hDC, iFrame, , y, , , (oBackColor &lt;&gt; -1)
      y = y + m_cAVI.Height
   Next iFrame
   
   cDC.UnselectObject
   
Dim cDS As New cDIBSectionSave
   If (m_cAVI.bitsPerPixel = 8) Then
      cDS.Save8BitBitmap sFileName, cB.hBmp, m_cAVI.Palette, True
   Else
      cDS.Save24BitBitmap sFileName, cB.hBmp
   End If
   
End Sub

Private Sub pSavePalette()
Dim cP As cPalette
Dim cD As New cCommonDialog
Dim sFile As String
   If (cD.VBGetSaveFileName( _
      sFile, _
      Filter:="Palette Files (*.PAL)|*.PAL|All Files (*.*)|*.*", _
      FilterIndex:=1, _
      DefaultExt:="PAL", _
      Owner:=Me.hWnd)) Then
      Set cP = m_cAVI.Palette
      cP.SaveToJASCFile sFile
   End If
End Sub

Private Sub pLoad(ByVal sFile As String)
On Error GoTo ErrorHandler
Dim i As Long

   ' Clear display
   For i = 0 To 8
      txtInfo(i).Text = ""
      txtInfo(i).Enabled = False
   Next i
   mnuFile(2).Enabled = False
   mnuFile(3).Enabled = False
   mnuFile(4).Enabled = False
   cboFrame.Clear
   cboFrame.Enabled = False
   chkTransparent.Enabled = False
   picPalette.Cls
   picFrame.Cls
   
   ' Load the AVI:
   m_cAVI.Filename = sFile
   
   ' Show the details
   txtAVIFile.Text = sFile
   txtInfo(0).Text = m_cAVI.FrameCount
   txtInfo(1).Text = m_cAVI.FrameDuration
   txtInfo(2).Text = m_cAVI.bitsPerPixel
   txtInfo(3).Text = m_cAVI.Width
   txtInfo(4).Text = m_cAVI.Height
   txtInfo(5).Text = m_cAVI.Name
   txtInfo(6).Text = m_cAVI.Compression
   txtInfo(7).Text = m_cAVI.VideoHandlerFourCCString
   txtInfo(8).Text = m_cAVI.VideoTypeFourCCString
   For i = 1 To m_cAVI.FrameCount
      cboFrame.AddItem i
   Next i
   pRenderPalette
   
   ' Re-enable the display:
   For i = 0 To 8
      txtInfo(i).Enabled = True
   Next i
   chkTransparent.Enabled = True
   If (cboFrame.ListCount &gt; 0) Then
      cboFrame.Enabled = True
      cboFrame.ListIndex = 0
      cboFrame_Click
      mnuFile(2).Enabled = True
      mnuFile(3).Enabled = True
      mnuFile(4).Enabled = Not (m_cAVI.Palette Is Nothing)
   End If
         
   Exit Sub
   
ErrorHandler:
   MsgBox "An error occurred: " &amp; Err.Description, vbExclamation
   Exit Sub
   
End Sub

Private Sub pRenderPalette()
   
   picPalette.Cls

   Dim cP As cPalette
   Set cP = m_cAVI.Palette
   If Not cP Is Nothing Then ' else &gt; 8bpp
      
      Dim Index As Long
      Dim x As Long
      Dim y As Long
      Dim palItemWidth As Long
      Dim palItemHeight As Long
      
      palItemWidth = picPalette.ScaleWidth \ 16
      If (cP.Count &gt; 16) Then
         palItemHeight = picPalette.ScaleHeight \ (cP.Count \ 16)
      Else
         palItemHeight = 16
      End If
      
      For Index = 0 To cP.Count - 1
         picPalette.Line (x, y)-(x + palItemWidth, y + palItemHeight),
          RGB(cP.Red(Index), cP.Green(Index), cP.Blue(Index)), BF
         x = x + palItemWidth
         If (x &gt; picPalette.ScaleWidth) Then
            x = 0
            y = y + palItemHeight
         End If
      Next Index
      
   End If
   picPalette.Refresh

End Sub

Private Sub pRenderFrame()
   picFrame.Cls
   m_cAVI.DrawFrame _
      picFrame.hDC, cboFrame.ListIndex + 1, _
      Transparent:=(chkTransparent.value = vbChecked)
   picFrame.Refresh
End Sub

Private Sub cboFrame_Click()
   If (cboFrame.Enabled) Then
      pRenderFrame
   End If
End Sub

Private Sub chkTransparent_Click()
   cboFrame_Click
End Sub

Private Sub cmdPick_Click()
   pLoadDialog
End Sub

Private Sub Form_Load()
   
   ' Set up the AVI Extractor
   Set m_cAVI = New cAVIFrameExtract
   
   ' Load the demo file:
   Dim sDefaultFile As String
   sDefaultFile = App.Path
   If Not (Right(sDefaultFile, 1) = "\") Then
      sDefaultFile = sDefaultFile &amp; "\"
   End If
   sDefaultFile = sDefaultFile &amp; "Download.avi"
   txtAVIFile.Text = sDefaultFile
   pLoad sDefaultFile
   
End Sub

Private Sub Form_Resize()
   On Error Resume Next
   picTab(0).Move 2 * Screen.TwipsPerPixelX, 0, Me.ScaleWidth - 2 *
    Screen.TwipsPerPixelX, Me.ScaleHeight
End Sub

Private Sub mnuFile_Click(Index As Integer)
   Select Case Index
   Case 0 ' Open
      pLoadDialog
   Case 2 ' Save frame
      pSaveFrame
   Case 3 ' Save bitmap strip
      pSaveBitmapStrip
   Case 4 ' Save palette
      pSavePalette
   Case 6 ' exit
      Unload Me
   End Select
End Sub

Private Sub picTab_Resize(Index As Integer)
Dim lWidth As Long
Dim lHeight As Long
   Select Case Index
   Case 0
      On Error Resume Next
      txtAVIFile.Width = picTab(0).ScaleWidth - txtAVIFile.Left - cmdPick.Width
       - 4 * Screen.TwipsPerPixelX
      cmdPick.Left = picTab(0).ScaleWidth - cmdPick.Width - 2 *
       Screen.TwipsPerPixelX
      linSep.X2 = picTab(0).ScaleWidth - linSep.X1 * 2
      lWidth = picTab(0).ScaleWidth - cboFrame.Left - 2 * Screen.TwipsPerPixelX
      cboFrame.Width = lWidth
      chkTransparent.Width = lWidth
      lHeight = Me.ScaleHeight - picFrame.TOp - lblInfo(12).Height -
       picPalette.Height - 4 * Screen.TwipsPerPixelY
      If (lHeight &lt; m_cAVI.Height * Screen.TwipsPerPixelY) Then
         lHeight = m_cAVI.Height * Screen.TwipsPerPixelY
      End If
      picFrame.Move picFrame.Left, picFrame.TOp, _
         lWidth, lHeight
      pRenderFrame
      lblInfo(12).Move picFrame.Left, _
         picFrame.TOp + picFrame.Height + 2 * Screen.TwipsPerPixelY, _
         lWidth
      picPalette.Move picFrame.Left, _
         lblInfo(12).TOp + lblInfo(12).Height, _
         lWidth
      pRenderPalette
      
   End Select
End Sub
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">vbMedia</a>&#160;.&#160;<a href="../index.html">Movies</a>&#160;.&#160;<a href="article.html">AVI Frame Extractor</a>&#160;.&#160;<a href="VB5_AVI_Extractor.html">VB5 AVI Extractor</a>&#160;.&#160;frmAVIExtractor.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 24 November 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/Movies/AVI_Extractor/VB5_AVI_Extractor_zip_frmAVIExtractor_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:48 GMT -->
</html>