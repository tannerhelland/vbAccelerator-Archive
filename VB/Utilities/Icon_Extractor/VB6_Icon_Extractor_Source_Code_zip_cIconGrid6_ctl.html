<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Utilities/Icon_Extractor/VB6_Icon_Extractor_Source_Code_zip_cIconGrid6_ctl.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:20:03 GMT -->
<head>
<title>vbAccelerator - Contents of code file: cIconGrid6.ctl</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: cIconGrid6.ctl" /><link rel="stylesheet" href="../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../index.html">VB</a>&#160;.&#160;<a href="../index.html">Utilities</a>&#160;.&#160;<a href="article.html">Icon Extractor Utility </a>&#160;.&#160;<a href="VB6_Icon_Extractor_Source_Code.html">VB6 Icon Extractor Source Code</a>&#160;.&#160;cIconGrid6.ctl</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="VB5_Icon_Extractor_Source_Code.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Icon Extractor Source Code</a> (101K)</p><p class="nav"><a href="VB5_Icon_Extractor.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Icon Extractor</a> (48K)</p><p /><p class="nav"><a href="VB6_Icon_Extractor_Source_Code.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Icon Extractor Source Code</a> (97K)</p><p class="nav"><a href="VB6_Icon_Extractor.html"><img src="../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Icon Extractor</a> (44K)</p><br /><br /><img src="../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:2109</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=2109&amp;type=Zip&amp;title=VB6%20Icon%20Extractor%20Source%20Code%2Ezip%5FcIconGrid6">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../res/update.png" width="8" height="8" alt="Update" />19 Dec 2002<br /><p class="update">Icons with Alpha channel (32 bit ARGB) now supported.</p><p class="update">Facility to save all icons from the selected executable or DLL at the same time added.</p><p class="update">XP Visual Styles now supported.</p></p><p class="update"><a href="updates.html">Update History &gt;</a></p><br /><br /><img src="../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1024">IShellFolder Extended Type Library Version 1.2 (ISHF_Ex.Tlb)</a></p><br /><br /><img src="../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: cIconGrid6.ctl</h1><pre>VERSION 5.00
Begin VB.UserControl cIconGrid 
   BackColor       =   &amp;H80000005&amp;
   BorderStyle     =   1  'Fixed Single
   ClientHeight    =   3600
   ClientLeft      =   0
   ClientTop       =   0
   ClientWidth     =   4800
   ScaleHeight     =   3600
   ScaleWidth      =   4800
End
Attribute VB_Name = "cIconGrid"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

Private Type tIconInfo
   hIcon As Long
   lWidth As Long
   lHeight As Long
   lStartY As Long
   lColours As Double
   bSelected As Boolean
End Type
Private m_tI() As tIconInfo
Private m_iIconCount As Long
Private m_lWidth As Long
Private m_lHeight As Long
Private m_lTextHeight As Long
Private m_lStartX As Long
Private m_iSelRow As Long
Private m_bInFocus As Boolean
Private WithEvents m_cScroll As cScrollBars
Attribute m_cScroll.VB_VarHelpID = -1

Private Type RECT
   left As Long
   top As Long
   right As Long
   bottom As Long
End Type
Private Declare Function DestroyIcon Lib "user32" (ByVal hIcon As Long) As Long
Private Declare Function DrawIconEx Lib "user32" (ByVal hdc As Long, ByVal
 xLeft As Long, ByVal yTop As Long, ByVal hIcon As Long, ByVal cxWidth As Long,
 ByVal cyWidth As Long, ByVal istepIfAniCur As Long, ByVal hbrFlickerFreeDraw
 As Long, ByVal diFlags As Long) As Boolean
Private Const DI_MASK = &amp;H1
Private Const DI_IMAGE = &amp;H2
Private Const DI_NORMAL = &amp;H3
Private Const DI_COMPAT = &amp;H4
Private Const DI_DEFAULTSIZE = &amp;H8
Private Declare Function DrawFocusRect Lib "user32" (ByVal hdc As Long, lpRect
 As RECT) As Long
Private Declare Function DrawText Lib "user32" Alias "DrawTextA" (ByVal hdc As
 Long, ByVal lpStr As String, ByVal nCount As Long, lpRect As RECT, ByVal
 wFormat As Long) As Long
Private Const DT_TOP = &amp;H0&amp;
Private Const DT_LEFT = &amp;H0&amp;
Private Const DT_CENTER = &amp;H1&amp;
Private Const DT_RIGHT = &amp;H2&amp;
Private Const DT_VCENTER = &amp;H4&amp;
Private Const DT_BOTTOM = &amp;H8&amp;
Private Const DT_WORDBREAK = &amp;H10&amp;
Private Const DT_SINGLELINE = &amp;H20&amp;
Private Const DT_EXPANDTABS = &amp;H40&amp;
Private Const DT_TABSTOP = &amp;H80&amp;
Private Const DT_NOCLIP = &amp;H100&amp;
Private Const DT_EXTERNALLEADING = &amp;H200&amp;
Private Const DT_CALCRECT = &amp;H400&amp;
Private Const DT_NOPREFIX = &amp;H800&amp;
Private Const DT_INTERNAL = &amp;H1000&amp;
'#if(WINVER &gt;= =&amp;H0400)
Private Const DT_EDITCONTROL = &amp;H2000&amp;
Private Const DT_PATH_ELLIPSIS = &amp;H4000&amp;
Private Const DT_END_ELLIPSIS = &amp;H8000&amp;
Private Const DT_MODIFYSTRING = &amp;H10000
Private Const DT_RTLREADING = &amp;H20000
Private Const DT_WORD_ELLIPSIS = &amp;H40000

Private Declare Function GetSysColorBrush Lib "user32" (ByVal nIndex As Long)
 As Long
Private Declare Function FillRect Lib "user32" (ByVal hdc As Long, lpRect As
 RECT, ByVal hBrush As Long) As Long
Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As
 Long
Private Const TRANSPARENT = 1
Private Const OPAQUE = 2
Private Declare Function GetSysColor Lib "user32" (ByVal nIndex As Long) As Long
Private Declare Function SetBkColor Lib "gdi32" (ByVal hdc As Long, ByVal
 crColor As Long) As Long
Private Declare Function SetBkMode Lib "gdi32" (ByVal hdc As Long, ByVal
 nBkMode As Long) As Long
Private Declare Function SetTextColor Lib "gdi32" (ByVal hdc As Long, ByVal
 crColor As Long) As Long
' don't do it...
Private Declare Function LockWindowUpdate Lib "user32" (ByVal hwndLock As Long)
 As Long

Public Event MouseDown(Button As Integer, Shift As Integer, X As Single, Y As
 Single)

Public Property Get Selected(ByVal i As Long) As Boolean
   Selected = m_tI(i).bSelected
End Property

Public Property Let Selected(ByVal i As Long, ByVal bState As Boolean)
   m_tI(i).bSelected = bState
   m_iSelRow = i
   Draw
End Property

Public Property Get ItemCount() As Long
   ItemCount = m_iIconCount
End Property

Public Sub KeyDown(ByVal KeyCode As Integer, ByVal Shift As Integer)
Dim i As Long
   Select Case KeyCode
   Case vbKeyUp
      If (m_iSelRow &gt; 1) Then
         If (Shift And vbCtrlMask) = vbCtrlMask Then
            m_iSelRow = m_iSelRow - 1
         ElseIf (Shift And vbShiftMask) = vbShiftMask Then
            For i = m_iSelRow To m_iSelRow - 1 Step -1
               m_tI(i).bSelected = True
            Next i
            m_iSelRow = m_iSelRow - 1
         Else
            m_iSelRow = m_iSelRow - 1
            For i = 1 To m_iIconCount
               m_tI(i).bSelected = (i = m_iSelRow)
            Next i
         End If
         If Not (EnsureVisible(m_iSelRow)) Then
            Draw
         End If
      End If
   Case vbKeyDown
      If (m_iSelRow &lt; m_iIconCount) Then
         If (Shift And vbCtrlMask) = vbCtrlMask Then
            m_iSelRow = m_iSelRow + 1
         ElseIf (Shift And vbShiftMask) = vbShiftMask Then
            For i = m_iSelRow To m_iSelRow + 1
               m_tI(i).bSelected = True
            Next i
            m_iSelRow = m_iSelRow + 1
         Else
            m_iSelRow = m_iSelRow + 1
            For i = 1 To m_iIconCount
               m_tI(i).bSelected = (i = m_iSelRow)
            Next i
         End If
         If Not (EnsureVisible(m_iSelRow)) Then
            Draw
         End If
      End If
      
   Case vbKeySpace
      If (m_iSelRow &lt;&gt; 0) Then
         m_tI(m_iSelRow).bSelected = Not (m_tI(m_iSelRow).bSelected)
         If Not (EnsureVisible(m_iSelRow)) Then
            Draw
         End If
      End If
   End Select
End Sub
Public Function EnsureVisible(ByVal iItem As Long) As Boolean
Dim lStartY As Long
Dim lOffset As Long
   If (m_cScroll.Visible(efsVertical)) Then
      lStartY = -m_cScroll.Value(efsVertical)
      If (m_tI(iItem).lStartY + m_tI(iItem).lHeight + m_lTextHeight + 6 +
       lStartY) &gt; m_lHeight Then
         ' Can't see it, must scroll down:
         lOffset = (m_tI(iItem).lStartY + m_tI(iItem).lHeight + lStartY +
          m_lTextHeight + 6) - m_lHeight
         m_cScroll.Value(efsVertical) = m_cScroll.Value(efsVertical) + lOffset
      ElseIf (m_tI(iItem).lStartY + lStartY &lt; 0) Then
         lOffset = (m_tI(iItem).lStartY + lStartY)
         m_cScroll.Value(efsVertical) = m_cScroll.Value(efsVertical) + lOffset
      End If
   End If
End Function
Public Sub MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X
 As Single, ByVal Y As Single)
Dim i As Long, j As Long, iItem As Long
Dim xL As Long, yL As Long
Dim lStartY As Long

   If (Button = 1) Then
      If (m_cScroll.Visible(efsVertical)) Then
         lStartY = -m_cScroll.Value(efsVertical)
      End If
      xL = X \ Screen.TwipsPerPixelX
      yL = Y \ Screen.TwipsPerPixelY
      ' Check which rect it is in:
      For i = 1 To m_iIconCount
         If (yL &gt; m_tI(i).lStartY + lStartY) Then
            If (yL &lt; m_tI(i).lStartY + lStartY + m_lTextHeight + 6 +
             m_tI(i).lHeight) Then
               iItem = i
               Exit For
            End If
         End If
      Next i
      
      If (iItem &gt; 0) Then
         If (Shift And vbCtrlMask) = vbCtrlMask Then
            m_tI(i).bSelected = Not (m_tI(i).bSelected)
         ElseIf (Shift And vbShiftMask) = vbShiftMask Then
            If (iItem &gt; m_iSelRow) Then
               For j = 1 To m_iIconCount
                  m_tI(j).bSelected = (j &lt;= iItem) And (j &gt;= m_iSelRow)
               Next j
            Else
               For j = 1 To m_iIconCount
                  m_tI(j).bSelected = (j &gt;= iItem) And (j &lt;= m_iSelRow)
               Next j
            End If
         Else
            For j = 1 To m_iIconCount
               m_tI(j).bSelected = (j = i)
            Next j
         End If
         m_iSelRow = i
         Draw
      End If
   End If
End Sub

Public Sub Create()
Dim tR As RECT
   m_lWidth = UserControl.ScaleWidth \ Screen.TwipsPerPixelY
   m_lHeight = UserControl.ScaleHeight \ Screen.TwipsPerPixelY
   Set m_cScroll = New cScrollBars
   m_cScroll.Create UserControl.hWnd
   m_cScroll.SmallChange(efsVertical) = 8
   DrawText UserControl.hdc, "Xg", 2, tR, DT_CALCRECT
   m_lTextHeight = tR.bottom - tR.top

End Sub

Public Sub Init(ByRef cI As cFileIcon)
Dim i As Long
   Clear
   If (cI.ImageCount &gt; 0) Then
      m_iIconCount = cI.ImageCount
      ReDim m_tI(1 To m_iIconCount) As tIconInfo
      For i = 1 To m_iIconCount
         With m_tI(i)
            .hIcon = cI.IconHandle(UserControl.hdc, i)
            .lColours = cI.ImageColourCount(i)
            .lWidth = cI.ImageWidth(i)
            .lHeight = cI.ImageHeight(i)
         End With
      Next i
      Draw
   End If
End Sub

Private Sub pSetScroll()
Dim i As Long
Dim lHeight As Long
Dim lProportion As Long

If Not (m_cScroll Is Nothing) Then 'elh 2/20/99 if
  For i = 1 To m_iIconCount
    If (i &gt; 1) Then
      m_tI(i).lStartY = lHeight
    End If
    lHeight = lHeight + m_lTextHeight + m_tI(i).lHeight + 6
  Next i
  If (lHeight &gt; m_lHeight) Then
    m_cScroll.Max(efsVertical) = lHeight - m_lHeight
    lProportion = lHeight \ (lHeight - m_lHeight) + 1
    m_cScroll.LargeChange(efsVertical) = lProportion * m_lHeight
    If Not (m_cScroll.Visible(efsVertical)) Then
      m_cScroll.Visible(efsVertical) = True
    End If
  Else
    If (m_cScroll.Visible(efsVertical)) Then
      m_cScroll.Visible(efsVertical) = False
    End If
  End If
End If
End Sub

Public Sub Draw()
   pSetScroll
   Render
End Sub

Private Sub Render()
Dim i As Long
Dim lStartY As Long
Dim tR As RECT
Dim tTR As RECT
Dim sText As String
Dim hBr As Long
Dim bSelFlag As Boolean
Dim lHDC As Long

   If Not (m_cScroll Is Nothing) Then 'elh 2/20/99 if
      If (m_cScroll.Visible(efsVertical)) Then
         lStartY = -m_cScroll.Value(efsVertical)
      End If
   End If
   tR.right = m_lWidth
   tR.bottom = m_lHeight
   lHDC = UserControl.hdc
   hBr = GetSysColorBrush(vbWindowBackground And &amp;H1F&amp;)
   FillRect lHDC, tR, hBr
   DeleteObject hBr
   hBr = GetSysColorBrush(vbHighlight And &amp;H1F&amp;)
   
   tR.right = m_lWidth
   For i = 1 To m_iIconCount
      tR.top = m_tI(i).lStartY + lStartY
      tR.bottom = tR.top + m_lTextHeight + m_tI(i).lHeight + 6
      If ((tR.bottom) &gt; 0) And (tR.top &lt; m_lHeight) Then
         If (tR.top &gt; m_lHeight) Then
            Exit For
         End If
         If (bSelFlag) And Not (m_tI(i).bSelected) Then
            SetBkMode lHDC, TRANSPARENT
            SetBkColor lHDC, GetSysColor(vbWindowBackground And &amp;H1F&amp;)
            SetTextColor lHDC, GetSysColor(vbWindowText And &amp;H1F&amp;)
         End If
         LSet tTR = tR
         If (m_tI(i).bSelected) Then
            FillRect lHDC, tTR, hBr
         End If
         tTR.right = tTR.right - 2
         tTR.left = 2 + (tTR.right - 4 - m_tI(i).lWidth) \ 2
         DrawIconEx lHDC, tTR.left, tTR.top + 2, m_tI(i).hIcon, m_tI(i).lWidth,
          m_tI(i).lHeight, 0, 0, DI_NORMAL
         
         LSet tTR = tR
         tTR.left = 2
         tTR.right = tTR.right - 2
         tTR.top = tTR.top + 2 + m_tI(i).lHeight + 2
         tTR.bottom = tTR.top + m_lTextHeight
         sText = m_tI(i).lWidth &amp; " x " &amp; m_tI(i).lHeight
         If (m_tI(i).lColours &lt;= 256) Then
            sText = sText &amp; ", " &amp; m_tI(i).lColours &amp; " colours"
         Else
            sText = sText &amp; ", millions of colours"
         End If
         If (m_tI(i).bSelected) Then
            SetBkMode lHDC, OPAQUE
            SetBkColor lHDC, GetSysColor(vbHighlight And &amp;H1F&amp;)
            SetTextColor lHDC, GetSysColor(vbHighlightText And &amp;H1F&amp;)
            bSelFlag = True
         End If
         DrawText lHDC, sText, Len(sText), tTR, DT_SINGLELINE Or
          DT_WORD_ELLIPSIS Or DT_CENTER
         If m_bInFocus And i = m_iSelRow Then
            LSet tTR = tR
            tTR.left = tTR.left + 1
            DrawFocusRect lHDC, tTR
         End If
      End If
   Next i
   
   DeleteObject hBr
   If (bSelFlag) Then
      SetBkMode lHDC, TRANSPARENT
      SetBkColor lHDC, GetSysColor(vbWindowBackground And &amp;H1F&amp;)
      SetTextColor lHDC, GetSysColor(vbWindowText And &amp;H1F&amp;)
   End If
End Sub
Public Sub GotFocus()
   m_bInFocus = True
   Draw
End Sub
Public Sub LostFocus()
   m_bInFocus = False
   Draw
End Sub
Public Sub Clear()
Dim i As Long
   For i = 1 To m_iIconCount
      DestroyIcon m_tI(i).hIcon
   Next i
   Erase m_tI
   m_iIconCount = 0
End Sub

Private Sub UserControl_GotFocus()
   GotFocus
End Sub

Private Sub UserControl_KeyDown(KeyCode As Integer, Shift As Integer)
   KeyDown KeyCode, Shift
End Sub

Private Sub UserControl_LostFocus()
   LostFocus
End Sub

Private Sub UserControl_MouseDown(Button As Integer, Shift As Integer, X As
 Single, Y As Single)
   RaiseEvent MouseDown(Button, Shift, X, Y)
   If (Button And vbLeftButton) = vbLeftButton Then
      MouseDown Button, Shift, X, Y
   End If
End Sub

Private Sub UserControl_Paint()
   Draw
End Sub

Private Sub UserControl_ReadProperties(PropBag As PropertyBag)
   Create
End Sub

Private Sub UserControl_Resize()
   m_lWidth = UserControl.ScaleWidth \ Screen.TwipsPerPixelY
   m_lHeight = UserControl.ScaleHeight \ Screen.TwipsPerPixelY
   Draw
End Sub

Private Sub UserControl_Terminate()
   Clear
   Set m_cScroll = Nothing
End Sub

Private Sub m_cScroll_Change(eBar As EFSScrollBarConstants)
   m_cScroll_Scroll eBar
End Sub

Private Sub m_cScroll_Scroll(eBar As EFSScrollBarConstants)
   Render
End Sub


</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../index.html">VB</a>&#160;.&#160;<a href="../index.html">Utilities</a>&#160;.&#160;<a href="article.html">Icon Extractor Utility </a>&#160;.&#160;<a href="VB6_Icon_Extractor_Source_Code.html">VB6 Icon Extractor Source Code</a>&#160;.&#160;cIconGrid6.ctl</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Utilities/Icon_Extractor/VB6_Icon_Extractor_Source_Code_zip_cIconGrid6_ctl.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:20:03 GMT -->
</html>